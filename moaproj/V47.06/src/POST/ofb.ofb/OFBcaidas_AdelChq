/****o* Lista de Giros/OFB:post:OFBcaidas_AdelChq
* NOMBRE
*   OFB:post:OFBcaidas_AdelChq
* DESCRIPCION
*   Chequea si existe una tx con caida en la tabla jnl_caidas_ach y la recupera
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   
* DOCUMENTACION
*   Documentación relevante que deberá ser actualizada por la modificación de la rutina
* SOURCE
*/
#include "girosCDS.h"


SUB post:OFBcaidas_AdelChq

	local l_status like tlr:jnl_status
	
    CDS SELECT FROM DSC:jnl_caidas_ach_tbl BECOMES DSC:jnl_caidas_ach_ctx WHERE  \
        tlr:jnl_branch == FLD:common:sucursal_activa && \
        tlr:jnl_status > HOST_REJECTED && \
        tlr:jnl_status < RECH_REV_AUTO DESCENDING

    CDS FINDFIRST DSC:jnl_caidas_ach_ctx LOCK
    lib:err_retval = CDSRETVAL
    WHILE (lib:err_retval == 0)
        CDS EXTRACT DSC:jnl_caidas_ach_ctx ALL


        //Hago un select de la jnl_AdelantoChq para recuperar el mismo registro que el de la jnl_caídas
        CDS SELECT FROM "jnl_AdelantoChq" BECOMES "jnl_AdelantoChq_ctx" WHERE  \
            tlr:jnl_branch == FLD:tlr:jnl_branch && \
            tlr:jnl_operator == FLD:tlr:jnl_operator && \
            tlr:jnl_date == FLD:tlr:jnl_date && \
            tlr:jnl_time == FLD:tlr:jnl_time && \
            tlr:jnl_bus_date == FLD:tlr:jnl_bus_date && \
            tlr:jnl_sequence == FLD:tlr:jnl_sequence 
        CDS FINDFIRST "jnl_AdelantoChq_ctx" LOCK
        IF (CDSRETVAL == 0) THEN
			CDS EXTRACT "jnl_AdelantoChq_ctx" ALL
	
            //Imprimo los datos principales de la tx que se esta recuperando
            fprint common:csr_string "Recuperacion de caida en vuelo de Adelanto Monetario siguiente:"
            CALL batch:MISCgeneraLog(common:csr_string,"INFO","N")
            fprint common:csr_string "\n\nSUC: %e      OP: %e      COD TX: %e      NRO.SEC: %e " \
                tlr:jnl_branch,tlr:jnl_operator,siaf:codigo_siaf,tlr:jnl_sequence
            CALL batch:MISCgeneraLog(common:csr_string,"INFO","N")


			call post:OFBenvio_caidas_ach
			IF (retval < 0)
				//caida en vuelo
				return -2
			END IF
			l_status = UNPOSTED
			//actualizo la tabla de AdelantoChq
			CDS CLEARBUFFER "jnl_AdelantoChq_ctx"
			CDS APPEND "jnl_AdelantoChq_ctx" \
				l_status AS tlr:jnl_status
			CDS UPDATE "jnl_AdelantoChq_ctx" LOCK
		END IF
		CDS UNLOCK "jnl_AdelantoChq_ctx" 
		CDS ENDSELECT "jnl_AdelantoChq_ctx"


		CDS FINDNEXT DSC:jnl_caidas_ach_ctx LOCK
		lib:err_retval = CDSRETVAL
		
	END WHILE



END SUB

/*******/

