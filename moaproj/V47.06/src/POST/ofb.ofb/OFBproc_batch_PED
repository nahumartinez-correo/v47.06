/****o*Pago en Destino/OFB:post:OFBproc_batch_PED 
* NOMBRE
*   OFB:post:OFBproc_batch_PED 
* DESCRIPCION
*   Rutina que obtiene los datos de la tabla "estadoPED" para generar el mensaje de cambio de estado
*	de una pieza PED en el centralizado 
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   OFB:batch:OFBbatch_estadoPED
* DOCUMENTACION
*   http://jira.sibs.net/jira/browse/AUT-1684 
* SOURCE
*/
#include "postofb.ofb.h"

SUB post:OFBproc_batch_PED 

	local loc_retval_env  like lib:err_retval
	local jnl_status_aux  like tlr:jnl_status
	local loc_retval_res  like lib:err_retval
	local loc_retval_jnl  like lib:err_retval
	local loc_status like hcomm:tran_status

    jnl_status_aux = CANCEL_REENVIE

    setflag common:rt_flags,batch_mode
 
    //Si hubo caida del rt luego de enviar el mensaje al host y antes de recibir la respuesta
    //se debera chequear que no haya habido caida en vuelo y generar la jnl_caidas_PED
    CALL post:OFBcaidaRT_estadoPED //OFBcaida_rtcupon
    
    //Recupero caidas de estados PED si es que existen
    CALL OFB:post:OFBcaidas_estadoPED //OFBcaidas_cupon
    IF (retval < 0)
		fprint siaf:nota_1,"No se pudo recuperar la caida del cambio de estado PED secuencia %e.\n Termina el proceso RT batch en forma anormal.",tlr:jnl_sequence
		call batch:OFBlog_batch(siaf:nota_1,loc_retval_env,"S","S","AMBOS","N")
		return -1
	END IF

    ;PROCESO TRATAMIENTO CAMBIO DE ESTADO DE PIEZA PED
    ;------------------------------------------------------------------------
    CDS SELECT FROM "estadoPED" BECOMES "estadoPED_ctx" WHERE  \
    tlr:jnl_branch == FLD:common:sucursal_activa && \
    tlr:jnl_status == UNPOSTED ASCENDING SORT "FecHora"

    CDS FINDFIRST "estadoPED_ctx" LOCK
    lib:err_retval = CDSRETVAL

    if (lib:err_retval != 0 )
        fprint siaf:nota_1,"%s","Busqueda registros en tabla estadoPED"
        call batch:OFBlog_batch(siaf:nota_1,lib:err_retval,"S","S","INFO","N")
    end if

    WHILE (lib:err_retval == 0)
        CDS EXTRACT "estadoPED_ctx" ALL
        CDS EXTRACT "estadoPED_ctx" journal_flags
        fprint siaf:nota_1,"\n\n"
        call batch:MISCgeneraLog(siaf:nota_1,"INFO","N")

        fprint siaf:nota_1,"%s","Busqueda registros en tabla estadoPED."
        call batch:OFBlog_batch(siaf:nota_1,lib:err_retval,"S","S","INFO","N")

        fprint siaf:nota_1,\
        "(SUC: %-5.5s OPER: %5.5s FECHA: %e HORA: %e SEC: %4.4s)",tlr:jnl_branch,\
        tlr:jnl_operator,tlr:jnl_date,tlr:jnl_time,tlr:jnl_sequence
        call batch:OFBlog_batch(siaf:nota_1,cdsretval,"N","N","INFO","N")
		
		//CDS BEGINWORK
        
        CDS CLEARBUFFER "estadoPED_ctx"

        CDS APPEND "estadoPED_ctx" \
                jnl_status_aux  AS FLD:tlr:jnl_status \
                common:time_of_day AS FLD:siaf:adt_jnlc_locktime

        CDS UPDATE "estadoPED_ctx" LOCK

        fprint siaf:nota_1,"%s",\
        "Actualizacion hora y estado 99 (RTbatch ) en tabla estadoPED"
        call batch:OFBlog_batch(siaf:nota_1,cdsretval,"S","S","INFO","N")
        
		op:op_branch_id = tlr:jnl_branch
	    op:op_operator_id = tlr:jnl_operator
	    op:op_login_id = post:adt_cupon_operador 
	    common:window_node = post:estadoPED_node     

        call post:OFBenvio_estadoPED //OFBenvio_cupon
        loc_retval_env = retval
        SELECT (loc_retval_env )
            CASE -2
            		//No obtiene respuesta del host... se muere la comunicacion (COMM_stat_init)
                    CDS ENDSELECT "estadoPED_ctx"
                    loc_retval_res = -2
                    break
            CASE -3
            		//No encontro el estadoPED de la directa para hacer la cancelacion
                    CDS ENDSELECT "estadoPED_ctx"
                    loc_retval_res = -3
                    break
            CASE TRAN_CAIDA_VUELO
            		//Hubo una caida en vuelo
                    CDS ENDSELECT "estadoPED_ctx"
                    loc_retval_res = TRAN_CAIDA_VUELO
                    break
        END SELECT

        CDS ENDSELECT DSC:tlr:jnl_context

        IF (loc_retval_res == -3)
        	//No encontro el estadoPED de la directa para hacer la cancelacion. Se desestima la cancelacion del estado
        	loc_status = 100
        	CDS APPEND "estadoPED_ctx" \
                 loc_status AS tlr:jnl_status
        ELSE 
        	CDS APPEND "estadoPED_ctx" \
                hcomm:tran_status  AS tlr:jnl_status \
                giros:CodRespuesta_comm
        END IF



        CDS UPDATE "estadoPED_ctx" LOCK
        
        //CDS COMMITWORK
       
        IF (CDSRETVAL < 0)
            fprint siaf:nota_1,"No se pudo actualizar estadoPED con el status con secuencia %e.\n Termina el proceso RT batch en forma anormal.",tlr:jnl_sequence
            call batch:OFBlog_batch(siaf:nota_1,loc_retval_env,"S","S","AMBOS","N")
            return -1
        ELSE
            fprint siaf:nota_1,"Actualizacion estadoPED segun status : %s",\
            hcomm:tran_status
            call batch:OFBlog_batch(siaf:nota_1,cdsretval,"S","S","INFO","N")
        END IF

        CDS FINDNEXT "estadoPED_ctx" LOCK
        lib:err_retval = CDSRETVAL
    END WHILE

    CDS ENDSELECT "estadoPED_ctx"
    
  	clearfield post:adt_codseq_nro
  	clearfield giros:hora_viaje
	clearfield giros:dia_viaje
    
END SUB

/*******/
