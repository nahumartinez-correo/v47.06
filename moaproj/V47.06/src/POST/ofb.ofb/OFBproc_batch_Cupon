/****o* Cobro por Cuenta de Terceros/OFB:post:OFBproc_batch_Cupon
* NOMBRE
*   OFB:post:OFBproc_batch_Cupon
* DESCRIPCION
*   Rutina que obtiene los datos de la tabla "cupon" para generar el mensaje de ingreso/cancelacion de cupon al host 
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   OFB:batch:OFBrecupCaidasVuelo
* DOCUMENTACION
*   http://jira.sibs.net/jira/browse/AUT-1684 
* SOURCE
*/
#include "postofb.ofb.h"

SUB post:OFBproc_batch_Cupon

	local loc_retval_env  like lib:err_retval
	local jnl_status_aux  like tlr:jnl_status
	local loc_retval_res  like lib:err_retval
	local loc_retval_jnl  like lib:err_retval
	local loc_status	like hcomm:tran_status

    jnl_status_aux = CANCEL_REENVIE

    setflag common:rt_flags,batch_mode
 
    //Si hubo caida del rt luego de enviar el mensaje al host y antes de recibir la respuesta
    //se debera chequear que no haya habido caida en vuelo y generar la jnl_caidas_cup
    CALL post:OFBcaida_rtcupon
    
    //Recupero caidas de cupones si es que existen
    CALL OFB:post:OFBcaidas_cupon
    IF (retval < 0)
		fprint siaf:nota_1,"No se pudo recuperar la caida del Cupón secuencia %e.\n Termina el proceso RT batch en forma anormal.",tlr:jnl_sequence
		call batch:OFBlog_batch(siaf:nota_1,loc_retval_env,"S","S","AMBOS","N")
		return -1
	END IF
    
    

    ;PROCESO TRATAMIENTO INGRESO/CANCELACION DE CUPON
    ;------------------------------------------------------------------------
    CDS SELECT FROM "cupon" BECOMES "cupon_ctx" WHERE  \
    tlr:jnl_branch == FLD:common:sucursal_activa && \
    tlr:jnl_status == UNPOSTED SORT "primary"

    CDS FINDFIRST "cupon_ctx" LOCK
    lib:err_retval = CDSRETVAL

    if (lib:err_retval != 0 )
        fprint siaf:nota_1,"%s","Busqueda registros en tabla cupon"
        call batch:OFBlog_batch(siaf:nota_1,lib:err_retval,"S","S","INFO","N")
    end if

    WHILE (lib:err_retval == 0)
        CDS EXTRACT "cupon_ctx" ALL
        CDS EXTRACT "cupon_ctx" journal_flags
        fprint siaf:nota_1,"\n\n"
        call batch:MISCgeneraLog(siaf:nota_1,"INFO","N")

        fprint siaf:nota_1,"%s","Busqueda registros en tabla cupon."
        call batch:OFBlog_batch(siaf:nota_1,lib:err_retval,"S","S","INFO","N")

        fprint siaf:nota_1,\
        "(SUC: %-5.5s OPER: %5.5s FECHA: %e HORA: %e SEC: %4.4s)",tlr:jnl_branch,\
        tlr:jnl_operator,tlr:jnl_date,tlr:jnl_time,tlr:jnl_sequence
        call batch:OFBlog_batch(siaf:nota_1,cdsretval,"N","N","INFO","N")
		
		CDS BEGINWORK
        
        CDS CLEARBUFFER "cupon_ctx"

        CDS APPEND "cupon_ctx" \
                jnl_status_aux  AS FLD:tlr:jnl_status \
                common:time_of_day AS FLD:siaf:adt_jnlc_locktime

        CDS UPDATE "cupon_ctx" LOCK

        fprint siaf:nota_1,"%s",\
        "Actualizacion hora y estado 99 (RTbatch ) en tabla cupon"
        call batch:OFBlog_batch(siaf:nota_1,cdsretval,"S","S","INFO","N")
        
		op:op_branch_id = tlr:jnl_branch
	    op:op_operator_id = tlr:jnl_operator
	    op:op_login_id = post:adt_cupon_operador 
	    common:window_node = post:adt_cupon_node     

        call post:OFBenvio_cupon
        loc_retval_env = retval
        SELECT (loc_retval_env )
            CASE -2
            		//No obtiene respuesta del host... se muere la comunicacion (COMM_stat_init)
                    CDS ENDSELECT "cupon_ctx"
                    loc_retval_res = -2
                    break
            CASE -3
            		//No encontro el cupon de la directa para hacer la cancelacion
                    CDS ENDSELECT "cupon_ctx"
                    loc_retval_res = -3
                    break
            CASE TRAN_CAIDA_VUELO
            		//Hubo una caida en vuelo
                    CDS ENDSELECT "cupon_ctx"
                    loc_retval_res = TRAN_CAIDA_VUELO
                    break
        END SELECT

        CDS ENDSELECT DSC:tlr:jnl_context

        IF (loc_retval_res == -3)
        	//No encontro el cupon de la directa para hacer la cancelacion. Se desestima el cupon
        	loc_status = 100
        	CDS APPEND "cupon_ctx" \
                loc_status AS tlr:jnl_status 
        ELSE 
        	CDS APPEND "cupon_ctx" \
                hcomm:tran_status  AS tlr:jnl_status \
                giros:CodRespuesta_comm        
        END IF
        
        CDS UPDATE "cupon_ctx" LOCK
        
        CDS COMMITWORK
       
        IF (CDSRETVAL < 0)
            fprint siaf:nota_1,"No se pudo actualizar cupon con el status con secuencia %e.\n Termina el proceso RT batch en forma anormal.",tlr:jnl_sequence
            call batch:OFBlog_batch(siaf:nota_1,loc_retval_env,"S","S","AMBOS","N")
            return -1
        ELSE
            fprint siaf:nota_1,"Actualizacion cupon segun status : %s",\
            hcomm:tran_status
            call batch:OFBlog_batch(siaf:nota_1,cdsretval,"S","S","INFO","N")
        END IF

        CDS FINDNEXT "cupon_ctx" LOCK
        lib:err_retval = CDSRETVAL
    END WHILE

    CDS ENDSELECT "cupon_ctx"
    
  	clearfield post:adt_codseq_nro
  	clearfield giros:hora_viaje
	clearfield giros:dia_viaje
    
END SUB

/*******/
