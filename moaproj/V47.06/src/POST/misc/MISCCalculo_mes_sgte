/****o* Plazo de Guarda/OFB:post:MISCCalculo_mes_sgte
* NOMBRE
*   OFB:post:MISCCalculo_mes_sgte
* DESCRIPCION
*   Esta rutina calcula, a partir de la fecha de entrada,el primer dia habil del mes siguiente.
* ENTRADAS
*   fecha:si se llama desde la TX de REcepcion de Piezas se pasa de parametro el common:suystem_date
*         si se llama desde la TX de Modificacion de Piezas se pasa de parametro la fecha de recepcion de la pieza
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   OFB:post:FVALplazo_guarda_CM
* DOCUMENTACION
*   SC50211.doc:http://livelink.intranet.siemens-itron.com.ar/livelink/livelink.exe?func=doc.ViewDoc&nodeId=619898&
* SOURCE
*/
#include "postmisc.h"

Sub post:MISCCalculo_mes_sgte(fecha)

local provincia like post:ProvCod
local flag like adt_stockp_rendicion

    flag = "T"
    //CALL  csr:date_add (common:system_date, 1,"m", stockp_vtofecha)
    //SC 50211
    CALL  csr:date_add (fecha, 1,"m", stockp_vtofecha)
    CALL  csr:day_of (stockp_vtofecha)
    lib:err_retval =  - (RETVAL - 1)
    CALL  csr:date_add (stockp_vtofecha, lib:err_retval,"d", stockp_vtofecha)
    WHILE (flag == "T")
      IF (adt_plagua_habil == "S") THEN
        /*Hay que tener en cuenta solo los dias habiles*/
        CALL csr:day_of_week(stockp_vtofecha)
        lib:err_retval = RETVAL
        IF (lib:err_retval == 7) THEN /*Sabado*/
            CALL  csr:date_add (stockp_vtofecha, 2,"d", stockp_vtofecha)
        END IF
        IF (lib:err_retval == 1) THEN/*Domingo*/
            CALL  csr:date_add (stockp_vtofecha, 1,"d", stockp_vtofecha)
        END IF
        FPRINT provincia,"%1.1s",common:sucursal_activa /* provincia de la sucursal activa*/
        /*VERIFICO SI ES FERIADO*/
        CDS SELECT FROM DSC:siaf:adt_feri_table BECOMES DSC:post:feriado_ctx WHERE \
        siaf:adt_feri_fecha == stockp_vtofecha && \
        (siaf:adt_feri_Op_Banc == "O" || siaf:adt_feri_Op_Banc == "A") && \
        ((siaf:adt_feri_tipo == "N") || \
        (siaf:adt_feri_tipo == "P" && siaf:adt_feri_prov == provincia) || \
        (siaf:adt_feri_tipo == "L" && siaf:adt_feri_suc == common:sucursal_activa))
        CDS FINDFIRST DSC:feriado_ctx NOLOCK
        IF (CDSRETVAL == 0)//Si es feriado
            CALL  csr:date_add (stockp_vtofecha, 1,"d", stockp_vtofecha)
        ELSE
            CDS ENDSELECT DSC:feriado_ctx
            flag = "F"
        END IF
      ELSE
            flag = "F"
      END IF

    END WHILE
End Sub

/*******/
