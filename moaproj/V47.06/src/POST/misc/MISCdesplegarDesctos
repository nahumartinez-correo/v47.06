/****o* Pendientes de agrupación/OFB:post:MISCdesplegarDesctos
* NOMBRE
*   OFB:post:MISCdesplegarDesctos
* DESCRIPCION
*   Se encarga de desplegar un picklist con los descuentos asociados a un determinado servicio.
* ENTRADAS
*   pServicio es el servicio por el cual se va a buscar una asociación de descuentos a
*	mostrar en un picklist
* SALIDAS
*   pDescuento es el código de descuento seleccionado.
* IMPACTOS
*   Una modificación erroneo puede probocar que no se muestren correctamente los descuentos
* en el picklist de selección.
* REFERENCIADO POR
*   post:PBdescuento
* DOCUMENTACION
*   SC 50187
* SOURCE
*/
#include "postmisc.h"

SUB post:MISCdesplegarDesctos(pServicio,pDescuento)
    local varl_picstr like common:csr_string
	local l_retval like lib:err_retval
	LOCAL INDICE LIKE post:cantidad
	CALL csr:f_sadroppl "Descuentos"
    call csr:f_sanewpl "Descuentos", PIC:post:PICtipo_descto
	cds endselect adt_descto_ctx
	cds join post:adt_descto_cod,adt_descto_desc \
	from adt_descto_tbl as "d", adt_descto_Serv_tbl as "s" \
	BECOMES adt_descto_ctx \
	where "d".post:adt_descto_cod == "s".post:adt_descto_cod && \
		  "s".adt_descto_serv_cod == pServicio
	cds findfirst adt_descto_ctx nolock
	
	do
        if (cdsretval < 0) then
            break
        end if
        cds extract dsc:adt_descto_ctx post:adt_descto_cod,adt_descto_desc
        fprint varl_picstr, "%-5.5s   %-20.20s" adt_descto_cod, adt_descto_desc
        call csr:f_saaddpl "Descuentos" varl_picstr
        cds findnext dsc:adt_descto_ctx nolock
    loop
	cds endselect adt_descto_ctx
	call drv:MISCf_sacombopl ("Descuentos",varl_picstr,drv:button)

	l_retval = retval

	IF (l_retval >= 0) THEN
		CALL csr:str_search (common:csr_string," ",INDICE)
		varl_picstr = substr(varl_picstr, 0, INDICE)
    	CALL csr:trimfield varl_picstr
		move varl_picstr to pDescuento
	END IF

	return l_retval
		
END SUB

/*******/
