/****o*Clientes Mayoristas/OFB:post:MISCconsulta_simon
*NOMBRE
*   OFB:post:MISCconsulta_simon
*DESCRIPCION
*   Se obtiene los campos BULK's y se va armando el picklist dinamico para
*   la visualizacion de los 
* ENTRADAS
*   datos:se pasa el campo bulk correspondientes a los bits 105-106-107.
*   long:se pasa la longitud de dichos campos.
* SALIDAS
*   Retorna el Valor de loc_retval: "0" cuando pudo armar el picklist, "-2" cuando no pudo armar
* IMPACTOS
* REFERENCIADO POR
*   OFB:post:OFBConsulta_simon
*DOCUMENTACION
*SOURCE
*/
#include "postmisc.h"

SUB post:MISCconsulta_simon(datos,long)
local posicion like post:cantidad
local longitud like post:Longitud_105
local l_operativo like post:operativo
local l_beneficio like post:beneficio
local l_importe like siaf:importe
local l_nombre_benef like common:csr_string
local l_cod_reimp like adt_benTmp_cod_reimp
local loc_retval like lib:err_retval
local l_suc_cli    like adt_benTmp_suc_cli
/*********************************************************************
 Se obtiene los campos BULK's y se va armando el picklist dinamico para
 la visualizacion de los beneficios
 La consulta multiple retorna:
**********************************************************************/

    fprint longitud,"%s",long
    posicion = 0
    loc_retval = -2

    clearfield beneficio
    clearfield operativo

    while (posicion < longitud )

        clearfield common:csr_string
                    
        l_beneficio = substr(datos,posicion,20)
        posicion = posicion + 20
        
        select (giros:codigo_procesamiento)
            case 970100
                l_operativo = substr(datos,posicion,20)
                posicion = posicion + 20
        end select
        l_importe = substr(datos,posicion,12)
        l_importe = l_importe / 100
        posicion = posicion + 12

        l_nombre_benef = substr(datos,posicion,40)
        posicion = posicion + 40
        
        select (giros:codigo_procesamiento)
            case 970090
                l_cod_reimp = substr(datos,posicion,6)
                posicion = posicion + 6
                    
                l_suc_cli =  substr(datos,posicion,8)
                posicion = posicion + 8
        end select

        call csr:trimfield l_beneficio
        call csr:trimfield l_operativo
        call csr:trimfield l_importe
        call csr:trimfield l_nombre_benef

        //more data.
        beneficio = l_beneficio


        if (l_beneficio.numchars != 0)
            
            select (giros:codigo_procesamiento)
                case 970090
                    FPRINT common:csr_string "%-20.20s %-40.40s %06.6s %-8.8s %e" \
                    l_beneficio, l_nombre_benef, l_cod_reimp, l_suc_cli, l_importe
                    break
                case 970100
                    FPRINT common:csr_string "%-20.20s %-20.20s %-40.40s %e" \
                    l_operativo, l_beneficio, l_nombre_benef, l_importe
                    operativo = l_operativo
            end select

            CALL CSR:f_saaddpl("pic_beneficios",common:csr_string)
            if (retval < 0 )
                loc_retval = -2
                msgbox \
                "ERROR AL AGREGAR BENEFICIOS AL PICKLIST.",OFB_OK,"Atención"
                break
            else
                loc_retval = 0
            end if
        end if
    end while

    return loc_retval

END SUB

/*******/
