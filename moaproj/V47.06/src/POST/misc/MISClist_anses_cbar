/****o* Pendientes de agrupación/OFB:post:MISClist_anses_cbar
* NOMBRE
*   OFB:post:MISClist_anses_cbar
* DESCRIPCION
*   Arma el listado de totales de pagos por cta de anses con codigo de barra
* REFERENCIADO POR
*   OFB:post:TOTSpagos_anses
* DOCUMENTACION
*  |html <a href="http://livelink/livelink/livelink.exe?func=doc.ViewDoc&nodeId=570096&">Solicitudes de Cambio</a>
* SOURCE
*/
#include "postmisc.h"

SUB post:MISClist_anses_cbar

    local clie_post like codigo_barras
    local nro_comprob like codigo_barras
    local total_importe like siaf:importe
    local cant_cupon like siaf:i
    local total_cupon like siaf:i
    local total_general like siaf:importe
    local idx like siaf:i
    local i_idx like siaf:i
    local importe like siaf:importe
    local clie_oper like codigo_barras
    local nro_oper like tlr:jnl_operator
    local desc_cupon like common:csr_string

    /*-Impresion del listado Totales de pagos por cuenta de ANSES con Codigo de Barras*/
    LET siaf:indica_calcu = 0 // inicializa cant. de hojas

    clearfield codigo_operativo
    clearfield nro_comprob
    clearfield total_general
    clearfield clie_post
    clearfield total_importe
    clearfield l_tot_importe
    clearfield cant_cupon
    clearfield l_cant_cupon
    clearfield total_cupon

    idx = 0x8000
    i_idx = 0
    do

        clearfield nro_oper
        clearfield clie_oper
        clearfield nro_comprob
        clearfield importe
        clearfield clie_post
        call csr:myPickcontrol("anses_codbar",common:csr_string,idx)
        IF (RETVAL < 0) THEN
	    BREAK
    	END IF 

    	/*Extraigo campos del picklist y los coloco en variables*/
        let nro_oper = substr(common:csr_string,0,5)
        let codigo_operativo = SUBSTR(common:csr_string,6,7)
        let clie_oper = substr(common:csr_string,6,7)
        let nro_comprob = SUBSTR(common:csr_string,13,11)
        let importe = SUBSTR(common:csr_string,34,16)

        fprint common:csr_string "%-5s          %7s                                              %11s    %16.16e", \
        nro_oper,codigo_operativo,nro_comprob,importe
        CALL MISCwrite_pct       

        let total_importe = total_importe + importe

        let cant_cupon = cant_cupon + 1
        let l_cant_cupon = l_cant_cupon + 1
        let total_cupon = total_cupon + 1
        clearfield common:csr_string

        i_idx = idx + 1
        call csr:myPickcontrol("anses_codbar",common:csr_string,i_idx)
        IF (RETVAL < 0) THEN //no hay otro registro siguiente.
            clearfield  clie_post //para que realice el corte
        ELSE
            let clie_post = substr(common:csr_string,6,7)
    	END IF

        IF (clie_oper != clie_post) THEN /*Si el prox. es distinto*/
            clearfield desc_cupon

           
            if (cant_cupon == 1) then
                desc_cupon = "CUPON"
            else
                desc_cupon = "CUPONES"
            end if
            fprint common:csr_string "-------------------------------------------------------------------------------------------------"
            CALL MISCwrite_pct
            fprint common:csr_string \
            "TOTAL          %7s                                         %-5.5e %-7.7s       %16.16e", \
            codigo_operativo,cant_cupon,desc_cupon,total_importe
            CALL MISCwrite_pct

            let total_general = total_general + total_importe //se va calculando el total general
            total_importe = total_importe * 100
            let l_tot_importe = total_importe
            
            //ACP-Codigo de barras
            FPRINT common:csr_string,"%s%s%7s%4.4d%9.9d",common:sucursal_activa,common:system_date,codigo_operativo,l_cant_cupon,l_tot_importe
            
            CALL MISCwrite_pct

            clearfield cant_cupon
            clearfield l_cant_cupon
            clearfield total_importe
            clearfield l_tot_importe
        END IF

        idx = idx + 1         
    loop
    fprint common:csr_string " "
    CALL MISCwrite_pct
    fprint common:csr_string "-------------------------------------------------------------------------------------------------"
    CALL MISCwrite_pct

    if (total_cupon == 1) then
        desc_cupon = "CUPON"
    else
        desc_cupon = "CUPONES"
    end if
    
    fprint common:csr_string,"TOTAL GENERAL ABONADO POR LA SUCURSAL                         %-5.5e %-7.7s        %16.16e", \
    total_cupon,desc_cupon,total_general
    CALL MISCwrite_pct

END SUB

/*******/
