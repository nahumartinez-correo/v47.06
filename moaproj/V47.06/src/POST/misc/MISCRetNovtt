/****o* Expedición/OFB:post:MISCRetNovtt
* NOMBRE
*   OFB:post:MISCRetNovtt
* DESCRIPCION
*   Actualiza la NovTT para las piezas transferidas pendientes de aceptacion 
*   cuando el operador hace un cierre forzado de caja
* ENTRADAS
*   p_jnl_operator_ori,p_jnl_sequence_ori,p_jnl_date_ori,p_jnl_time_ori
* SALIDAS
*   Retval = -801 si se produjo un error
*	Retval = 0 si no se produjo error
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*  post:MISCRetPzasPend
* DOCUMENTACION
*   http://jira.sibs.net/jira/browse/AUT-50242
* SOURCE
*/
#include "postmisc.h"

SUB post:MISCRetNovtt(p_jnl_operator_ori,p_jnl_sequence_ori,p_jnl_date_ori,p_jnl_time_ori)
	LOCAL l_cdsretval LIKE lib:err_retval
    LOCAL l_jnl_operator_ori LIKE post:jnl_operator_ori   
    LOCAL l_jnl_sequence_ori LIKE post:jnl_sequence_ori   
    LOCAL l_jnl_date_ori LIKE post:jnl_date_ori  
    LOCAL l_jnl_time_ori LIKE post:jnl_time_ori 
    LOCAL l_fecha LIKE post:adt_novtt_fecha       
  
    l_jnl_operator_ori = p_jnl_operator_ori
    l_jnl_sequence_ori = p_jnl_sequence_ori
    l_jnl_date_ori = p_jnl_date_ori  
    l_jnl_time_ori = p_jnl_time_ori
    
    FPRINT l_fecha "%s%s" ,l_jnl_date_ori,l_jnl_time_ori
    
    CDS SELECT FROM "NovTT" BECOMES "NovTT_ctx" \
        WHERE post:adt_novtt_op ==  l_jnl_operator_ori \
        && post:adt_novtt_sequence == l_jnl_sequence_ori \
        && post:adt_novtt_fecha == l_fecha
    
	CDS FINDFIRST "NovTT_ctx" LOCK

	LET l_cdsretval = CDSRETVAL
	
	IF (l_cdsretval == -801) THEN
	    CDS ENDSELECT "NovTT_ctx"
	    RETURN l_cdsretval
	END IF
	
	DO
		IF (l_cdsretval != 0) THEN
			BREAK
		END IF
        
        CLEARFIELD post:adt_novtt_guia_cont
        CLEARFIELD post:adt_novtt_op2
        CLEARFIELD post:adt_novtt_est_pieza

    	CDS APPEND "NovTT_ctx" \
    		post:adt_novtt_guia_cont \
    		post:adt_novtt_op2 
        
        CDS UPDATE "NovTT_ctx" LOCK
  
		LET l_cdsretval = CDSRETVAL
	
    	IF (l_cdsretval == -801) THEN
    	    CDS ENDSELECT "NovTT_ctx"
    	    RETURN l_cdsretval
    	END IF
	
		CDS FINDNEXT "NovTT_ctx" LOCK
		LET l_cdsretval = CDSRETVAL
		
		CDS CLEARBUFFER "NovTT_ctx"
	LOOP
	
	CDS ENDSELECT "NovTT_ctx"
	RETURN 0
	    
END SUB      

/*******/
