/****o* Clientes Medios/OFB:post:MISCcm_ctrl_items
* NOMBRE
*   OFB:post:MISCcm_ctrl_items
* DESCRIPCION
*   Se corrobora que dentro de una venta de facturacion diferida los items a enviar no sean más de 10 (incluyendo descuentos y adelanto)
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   OFB:plataf:MISCModalidadesCM,OFB:post:MISCconsumo_anticipo
* DOCUMENTACION
* https://jirats.atos.net.ar/jira/browse/AUT-2330
* SOURCE
*/
#include "postmisc.h"

SUB post:MISCcm_ctrl_items

	local varl_cant    like giros:NroGiro_fld
	local loc_retval   like lib:err_retval
	local varl_codigo  like siaf:codigo_siaf
        local varl_nrotrn  like post:item_nrotrn
	local varl_especie like post:item_especie

	//AUT -2330 PS 10447
	if (ConsumoAnticipo) then
		varl_cant = 1
	else
		varl_cant = 0
	endif

	CDS SELECT FROM DSC:TABPresuDeta BECOMES DSC:CONPresuDeta \
	WHERE post:fecha == fecha && post:op_operator_id ==  post:op_operator_id  && \
	post:PresNro == post:PresNro && \
	((siaf:adt_tran_fliatran != "PCL") || \
	(siaf:codigo_siaf == 99450 || siaf:codigo_siaf == 99300 ) ) 

	CDS FINDFIRST DSC:CONPresuDeta NOLOCK
    	lib:err_retval = CDSRETVAL
	if ( lib:err_retval == -801  )
		CDS ENDSELECT DSC:CONPresuDeta
		return 0
	else
		if ( lib:err_retval < 0 )
			msgbox "Error al obtener los datos del presupuesto.",OFB_OK,"Atencion"
			return -1
		end if
	end if

	WHILE (lib:err_retval ==  0 )

		clearfield varl_codigo
    		CDS EXTRACT DSC:CONPresuDeta \
			siaf:codigo_siaf into varl_codigo 

		if (varl_codigo == 99300 && !post:ConvenioSesionV)
			;NO AGREGAR TRX A LOS ITEMS
		else
			if ( varl_codigo == 8500 )

    				CDS EXTRACT DSC:CONPresuDeta \
					post:PreDNroTrn into varl_nrotrn

    				CDS SELECT FROM DSC:tes:AuxEspV_tbl becomes DSC:tes:AuxEspV_ctx \
        			WHERE tes:adt_evaux_suc == op:op_branch_id && \
              			tes:adt_evaux_op == op:op_operator_id && \
              			tes:adt_evaux_prestran == varl_nrotrn && \
				tes:adt_evaux_es_venta == "S"
	
    				CDS FINDFIRST dsc:tes:AuxEspV_ctx NOLOCK
    				do
    					if (CDSRETVAL != 0) then
    	   					break
    					end if

					CDS EXTRACT dsc:tes:AuxEspV_ctx \
					tes:adt_evaux_codigo   into varl_especie
						
					varl_cant = varl_cant + 1

    					CDS FINDNEXT dsc:tes:AuxEspV_ctx NOLOCK
    				loop

				CDS ENDSELECT dsc:tes:AuxEspV_ctx
			else
				varl_cant = varl_cant + 1
			end if
		end if

    		CDS FINDNEXT DSC:CONPresuDeta LOCK
    		lib:err_retval = CDSRETVAL
		if ( lib:err_retval == -801 )
			loc_retval = 0
			break
		else
			if ( lib:err_retval < 0 )
				msgbox "Error al obtener los datos del presupuesto.",OFB_OK,"Atencion"
				loc_retval = -1
				break
			end if
		end if
	
	END WHILE

	if ( varl_cant > 10 )
		msgbox "La cantidad de productos del presupuesto supera al maximo permitido.",\
		OFB_OK,"Atencion"
		loc_retval = -2
	end if

	return loc_retval

END SUB

/*******/
