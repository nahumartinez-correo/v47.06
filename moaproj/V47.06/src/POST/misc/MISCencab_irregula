/****o* Reportes/OFB:post:MISCencab_irregula
* NOMBRE
*   OFB:post:MISCencab_irregula
* DESCRIPCION
*   Encabezado de la Consulta de Irregularidades.
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   post:MISCirregu_oper, post:MISCirregu_sucursal, post:MISCirr_FCremNeg_op, post:MISCirr_FCremNeg_suc,
*   post:MISCirregu_ope_franq, post:MISCirregu_suc_franq, post:MISCirrO_UPnoc_sel, post:MISCirrOp_ttgir_sel,
*   post:MISCirrS_UPnoc_sel, post:MISCirrSuc_ttgir_sel, post:MISCpzas_sin_destino, post:MISCwrite_irre 
*   post:MISCirregu_suc_web
* DOCUMENTACION
*   Documentación relevante que deberá ser actualizada por la modificación de la rutina
* SOURCE
*/
#include "postmisc.h"

SUB post:MISCencab_irregula
	//cgw:NI50276 - Encabezado de la Consulta de Irregularidades.
	LOCAL loc_spaces LIKE siaf:i
	LOCAL l_imprimible LIKE siaf:i
	LOCAL l_visible LIKE siaf:i
	LOCAL l_espacios LIKE siaf:i
	LOCAL espaciosBlancos LIKE siaf:i
	LOCAL cadena LIKE common:csr_string
	LOCAL Lsuc_name LIKE admin:adt_br_bank_name

  post:cantidad = 0 //cgw:inicializo contador de lineas de impresion.
  
  MOVE " " TO common:csr_string //cgw:estaba arrastrando valores por pantalla.
    
	LET FLD:tes:INDsheet_header = OFB:post:MISCencab_irregula
	
	l_imprimible = 125
	l_visible = 104
	
	SELECT (siaf:pantalla_impreso)
		CASE "D"
		CASE "I"
			l_espacios = l_imprimible
			//Control de pie de pagina cantidad total de hojas al final del reporte.            
			FPRINT common:csr_string, "<pie>1</pie><long>%s</long><txS>75</txS><txF>Courier New</txF><st>0</st>", l_imprimible
		BREAK
		CASE "P"
			l_espacios = l_visible
		BREAK
	END SELECT
	CALL post:MISCwrite_irre

  //Cuenta la cant. hojas (= cant. de encab.)
  LET siaf:indica_calcu = siaf:indica_calcu + 1

  common:csr_string = " "
  CALL post:MISCwrite_irre

  FPRINT common:csr_string, "%8e - %e Pag.:%s", common:system_date,common:time_of_day, siaf:indica_calcu
  espaciosBlancos = l_espacios - common:csr_string.NUMCHARS - common:emp_desc.NUMCHARS
  FPRINT common:csr_string "%s%*.*s%s", common:emp_desc, espaciosBlancos, espaciosBlancos," ",common:csr_string
  CALL post:MISCwrite_irre
  MOVE " " TO common:csr_string
  CALL post:MISCwrite_irre
  
  CALL CSR:trimfield(mot_irregula)

	SELECT (siaf:codigo_siaf_list)
		CASE 9540
			FPRINT common:csr_string,"CONSULTA DE IRREGULARIDADES: %-s AL CIERRE DE SUCURSAL",mot_irregula
		BREAK
		CASE 9550
			FPRINT common:csr_string,"CONSULTA DE IRREGULARIDADES: %-s AL CIERRE FORZADO DE SUCURSAL",mot_irregula
		BREAK
		CASE ELSE
			FPRINT common:csr_string,"CONSULTA DE IRREGULARIDADES: %-s DE SUCURSAL",mot_irregula
		BREAK
	END SELECT
	espaciosBlancos = (l_espacios - common:csr_string.NUMCHARS) / 2
	FPRINT common:csr_string "%*.*s%s", espaciosBlancos, espaciosBlancos," ", common:csr_string
	CALL post:MISCwrite_irre

  FPRINT common:csr_string,"ENTRE LOS DIAS %8e Y %8e",adt_umov_fecha_ini,adt_umov_fecha_fin
  espaciosBlancos = (l_espacios - common:csr_string.NUMCHARS) / 2
  FPRINT common:csr_string "%*.*s%s", espaciosBlancos, espaciosBlancos," ", common:csr_string
  CALL post:MISCwrite_irre
  MOVE " " TO common:csr_string
  CALL post:MISCwrite_irre

  LET op:adt_login_id = op:op_login_id
  ;Busco descripcion de la sucursal
  CLEARFIELD Lsuc_name
  CALL post:CDSadt_br_dsc_load(common:sucursal_activa,Lsuc_name)

  FPRINT common:csr_string,"OPERADOR:%-5.5s", op:op_operator_id
  FPRINT cadena,"SUCURSAL: %5.5s - %-35.35s",op:op_branch_id,Lsuc_name
  espaciosBlancos = l_espacios - common:csr_string.NUMCHARS - cadena.NUMCHARS
  FPRINT common:csr_string "%s%*.*s%s", cadena, espaciosBlancos, espaciosBlancos," ",common:csr_string
  CALL post:MISCwrite_irre
  MOVE " " TO common:csr_string
  CALL post:MISCwrite_irre
    
  //FPRINT common:csr_string,"MOTIVO: %-42.42s LISTADO POR: %-10.10s",mot_irregula,opcion_irregula

  FPRINT common:csr_string,"LISTADO POR: %-8.8s", opcion_irregula
  FPRINT cadena,"MOTIVO: %-42.42s",mot_irregula
  espaciosBlancos = l_espacios - common:csr_string.NUMCHARS - cadena.NUMCHARS
  FPRINT common:csr_string "%s%*.*s%s", cadena, espaciosBlancos, espaciosBlancos," ",common:csr_string
  CALL post:MISCwrite_irre

END SUB

/*******/
