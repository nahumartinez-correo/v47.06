/****o* International Postal Services/OFB:post:MISCact_tt_internac
* NOMBRE
*   OFB:post:MISCact_tt_internac
* DESCRIPCION
*   Actualiza pais y contenido IPS cargasdo en pantalla en la tabla track & trace.
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
* 	RETURN (0): Actualizo tabla track_trace para cantidad 1.
*   RETURN (-2): No pudo acceder al primer registro de la tabla track_trace.
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   OFB:post:SVAL_tt_internac
* DOCUMENTACION
*   https://jirats.atos.net.ar/jira/browse/AUT-2033
* SOURCE
*/
#include "postmisc.h"

SUB post:MISCact_tt_internac
	LOCAL cont LIKE post:cant_opc
	LOCAL l_track_trace LIKE post:track_trace
	/*Actualiza Pais y Contenido IPS a la tabla de track_trace*/
	IF (post:cantidad == 1) THEN
		//Actualiza track & trace de cantidad 1
		CALL post:MISCact_tt_inter1
		RETURN (0)
	END IF
	//Actualiza track & trace para cantidad > 1
	cont = 0
	WHILE (cont < a_desproteger)
		//gpb:AUT-2379
		//cambio por campo sin autoskip
		//l_track_trace = track[cont]
		l_track_trace = track_ips[cont]
		IF (l_track_trace.numchars > 0) THEN
		
			IF (cli_tipo == "") THEN		
				CDS SELECT FROM TT_TAB BECOMES TT_CTX WHERE \
					fecha == common:system_date && \
					operador_tt == operador_tt && \
					PresNro_tt == PresNro_tt && \
					NroTrn_tt == NroTrn_tt && \
					track_trace == l_track_trace && \
					pag_act == pag_act
			ELSE
				CDS SELECT FROM TT_TAB BECOMES TT_CTX WHERE \
					fecha == fecha && \
					operador_tt == operador_tt && \
					PresNro_tt == PresNro_tt && \
					post:NroTrn_tt == post:PreDNroTrn && \
					track_trace == l_track_trace && \
					pag_act == pag_act
			END IF
					
			CDS FINDFIRST TT_CTX LOCK
			IF (CDSRETVAL == 0) THEN
				CDS CLEARBUFFER TT_CTX
				CDS APPEND TT_CTX \
					ips_pais[cont] AS jnl_ips_pais \
					ips_pais_cod[cont] AS jnl_ips_pais_cod \
					ips_contenido[cont] AS jnl_ips_cont \
					ips_contenido_cod[cont] AS jnl_ips_cont_cod
				CDS UPDATE TT_CTX LOCK
			END IF
			CDS ENDSELECT TT_CTX
		END IF
		cont = cont + 1
	END WHILE
END SUB

/*******/
