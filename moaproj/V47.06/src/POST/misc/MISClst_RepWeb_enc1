/****h* MOSAIC/Caja Unificada/OFB:post:MISClst_RepWeb_enc1
* NOMBRE
*   OFB:post:MISClst_RepWeb_enc1
* DESCRIPCION
*   Genera el encabezado del Reporte de Lista de Operaciones web.
* ENTRADAS
*   p_lineas_cl = inicializa el contador de lineas en cero
*   p_l_imprimible = cantidad de caracteres a imprimir por linea
*   link_num = canal de comunicacion numerico
*   report_name = nombre del reporte
*   p_scl = es la escala de la letra
*   p_ln = cantidad de lineas a imprimir por hoja
*   p_size = tamaño de la letra
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   post:MISClst_RepWeb_enc
* DOCUMENTACION
*   http://jira.sibs.net/jira/browse/AUT-1440
* SOURCE
*/
#include "postmisc.h"

SUB post:MISClst_RepWeb_enc1(p_lineas_cl,p_l_imprimible,link_num,report_name,p_scale,p_lines,p_textfont) 

    LOCAL l_imprimible     LIKE siaf:i
    LOCAL l_visible        LIKE siaf:i
    LOCAL l_espacios       LIKE siaf:i
    LOCAL espaciosBlancos  LIKE siaf:i
    LOCAL cadena           LIKE common:csr_string_aux
    LOCAL cadena0          LIKE common:csr_string
    LOCAL cadena1          LIKE common:csr_string
    LOCAL cadena2          LIKE common:csr_string_aux
    LOCAL cadena3          LIKE common:csr_string
    LOCAL cadena4          LIKE common:csr_string
    LOCAL cadena5          LIKE common:csr_string
    LOCAL line_csr_aux     LIKE common:csr_string_aux
    LOCAL split            LIKE common:csr_string_4096
    LOCAL l_oper_Web       LIKE post:operador_RWeb
    LOCAL l_fecha_Rweb     LIKE common:system_date
    
    LET FLD:tes:INDsheet_header = OFB:post:MISClst_RepWeb_enc
    cadena2 = line_csr_aux
    
    // cuenta la cant. hojas (= cant. de encab.)
    LET siaf:indica_calcu = siaf:indica_calcu + 1
    
    siaf:lineas_cl = p_lineas_cl 
    l_imprimible = p_l_imprimible
    
    cadena0 = "<pie>1</pie><long>"
    cadena1 = "</long><or>0</or><scl>"  
    cadena3 = "</scl><ln>"
    cadena4 = "</ln><txS>80</txS><txF>Courier New</txF><st>"
    cadena5 = "</st>"
    
    FPRINT line_csr_aux, "%s%s%s%s%s%s%s%s%s" , cadena0, l_imprimible, cadena1, p_scale, cadena3, p_lines , cadena4 ,p_textfont ,cadena5
    //Ej salida de line_csr_aux 
    //<pie>1</pie><long>l_imprimible</long><or>1</or><scl>p_scl</scl><ln>p_ln</ln><txS>100</txS><txF>Courier New</txF><st>p_size</st>
    //<pie>1</pie><long>142</long><or>1</or><scl>90</scl><ln>46</ln><txS>100</txS><txF>Courier New</txF><st>12</st>
    IF (siaf:pantalla_impreso != "P") THEN
        CALL giros:MISCwrite_file_ipa2(link_num,line_csr_aux)
    END IF
    
    CALL giros:MISCwrite_file_ipa2(link_num," ") //se agrega un renglon en blanco
    
    LET op:adt_login_id = op:op_login_id
    ;Busco descripcion de la sucursal
    admin:adt_br_branch = common:sucursal_activa
    CALL admin:CDSadt_br_load
    l_espacios = l_imprimible
    FPRINT line_csr_aux, "%8e - %e Pag.:%s", common:system_date,common:time_of_day, siaf:indica_calcu
    espaciosBlancos = l_espacios - line_csr_aux.numchars - common:emp_desc.numchars
    FPRINT line_csr_aux "%s%*.*s%s", common:emp_desc, espaciosBlancos, espaciosBlancos," ",line_csr_aux
    CALL giros:MISCwrite_file_ipa2(link_num,line_csr_aux)
    CALL giros:MISCwrite_file_ipa2(link_num," ")
    FPRINT line_csr_aux report_name
    espaciosBlancos = (l_espacios - line_csr_aux.numchars) / 2
    
    FPRINT line_csr_aux "%*.*s%s", espaciosBlancos, espaciosBlancos," ", line_csr_aux
    CALL giros:MISCwrite_file_ipa2(link_num,line_csr_aux)

    IF (tes:list_web == "X") THEN //se instancia con valor "X" en los cierres y vacio a demanda.
        FPRINT line_csr_aux "AL %8e",siaf:fecha_proceso
        espaciosBlancos = (l_espacios - line_csr_aux.numchars) / 2
        FPRINT line_csr_aux "%*.*s%s", espaciosBlancos, espaciosBlancos," ", line_csr_aux
    ELSE
        FPRINT line_csr_aux "AL %8e",post:fecha_Rweb
        espaciosBlancos = (l_espacios - line_csr_aux.numchars) / 2
        FPRINT line_csr_aux "%*.*s%s", espaciosBlancos, espaciosBlancos," ", line_csr_aux
    ENDIF
    
    CALL giros:MISCwrite_file_ipa2(link_num,line_csr_aux)
    CALL giros:MISCwrite_file_ipa2(link_num," ")
    
    FPRINT line_csr_aux,"OPERADOR:%-5.5s", op:op_operator_id
    FPRINT cadena,"SUCURSAL: %5.5s - %-35.35s",op:op_branch_id,admin:adt_br_bank_name
    espaciosBlancos = l_espacios - line_csr_aux.numchars - cadena.numchars
    FPRINT line_csr_aux "%s%*.*s%s", cadena, espaciosBlancos, espaciosBlancos," ",line_csr_aux
    CALL giros:MISCwrite_file_ipa2(link_num,line_csr_aux)

    CALL giros:MISCwrite_file_ipa2(link_num," ") //se agrega un renglon en blanco
  
    //TITULOS DETALLE REPORTE
    IF (siaf:pantalla_impreso != "P") THEN
           split = "--------------------------------------------------------------------------------------------------------------"
    ELSE
           split = "--------------------------------------------------------------------------------------------------------------"
    END IF
    line_csr_aux = split
    CALL giros:MISCwrite_file_ipa2(link_num,line_csr_aux)
    IF (RWeb_senasa == "S")
    	FPRINT line_csr_aux, "                ID DE CUPON                         FECHA VTO.        IMPORTE      OPERADOR"
    ELSE	
    	line_csr_aux = "DESCRIPCION                      CANT.       IMPORTE WEB    IMPORTE S/IMP.              IMP.             TOTAL"
    END IF
    CALL giros:MISCwrite_file_ipa2(link_num,line_csr_aux)
    line_csr_aux = split
    CALL giros:MISCwrite_file_ipa2(link_num,line_csr_aux)
  
END SUB 

/*******/
