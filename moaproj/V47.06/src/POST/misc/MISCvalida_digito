/****o* Pendientes de agrupación/OFB:post:MISCvalida_digito
* NOMBRE
*   OFB:post:MISCvalida_digito
* DESCRIPCION
* 	Rutina de validación del Dígito Verificador del
* 	Correo Argentino. Juan Pablo Macchi - Mayo 1998
* ENTRADAS
* 	Scanneado del Track&Trace               
*   (Ej: CC960889433AR)                     
* SALIDAS
* 	En caso de fallar la validación informa
*   de este suceso mediante un mensaje y   
*   retorna -2. Sino retorna 0.            
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   Objetos que llaman al objeto documentado (obligatorio)
* DOCUMENTACION
*   Documentación relevante que deberá ser actualizada por la modificación de la rutina
* SOURCE
*/

#include "postmisc.h"

SUB post:MISCvalida_digito(barra)
    LOCAL nsum LIKE post:cantidad
    LOCAL resto LIKE post:cantidad
    LOCAL numeros like pick_name
    
    //Verifica que los caracteres intermedios sean todos numericos
    numeros = substr(barra,2,9)
    CALL MISCesNumerico(numeros)
    IF (RETVAL == FALSO)
        /*Los caracteres intermedios no son todos numericos*/
        MSGBOX "LOS CARACTERES INTERMEDIOS DEBEN SER NUMERICOS",0x00,"ERROR"
        RETURN (-2)
    END IF    
    
    
    //Calculo el digito verificador
    /* Multiplico por los pesos especificados */
    nsum = SUBSTR(barra,2,1) *8 + SUBSTR(barra,3,1) * 6 + \
        SUBSTR(barra,4,1) *4 + SUBSTR(barra,5,1) *2 + \
        SUBSTR(barra,6,1) *3 + SUBSTR(barra,7,1) *5 + \
        SUBSTR(barra,8,1) *9 + SUBSTR(barra,9,1) *7
    nsum = nsum - ((nsum/11)*11)
    IF (nsum == 0) THEN
        nsum = 5
    ELSE
        IF (nsum == 1) THEN
            nsum = 0
        ELSE
            nsum = 11 - nsum
        END IF
    END IF
    IF (nsum != SUBSTR(barra,10,1)) THEN
        MSGBOX "EL TRACK&TRACE LEIDO NO ES CORRECTO",0x00,"ERROR"
        RETURN (-2)
    ELSE
        /* T&T Correcto */
        RETURN (0)
    END IF
END SUB

/*******/
