/****o* Cobro por Cuenta de Terceros/OFB:post:MISCval_num_Cupon
* NOMBRE
*   OFB:post:MISCval_num_Cupon
* DESCRIPCION
*		Valida si el tipo de caracteres es numerico o alfanumerico
* ENTRADAS
*	l_tipo = ALF (alfanumerico), NUM (numerico)
* SALIDAS
*		0 cupon valido
*		-2 cupon invalido
* REFERENCIADO POR
*		post:FVALid_cupon_CCT
* DOCUMENTACION
*   50361 - https://jirats.siemens.com.ar/jira/browse/AUT-1684
* SOURCE
*/
#include "postmisc.h"

SUB post:MISCval_num_Cupon(l_tipo,l_codigo)
	LOCAL l_tipo_cupon LIKE post:adt_cct_tipocupon
	LOCAL l_id_cupon LIKE post:id_cupon_CCT
	LOCAL i LIKE siaf:cant
	LOCAL long LIKE siaf:cant
	LOCAL l_caracter LIKE common:csr_string
	LOCAL Indice LIKE lib:err_retval
	LOCAL ValidChars LIKE common:csr_string
	LOCAL l_cadena_1 LIKE common:csr_string
	LOCAL l_cadena_2 LIKE common:csr_string
	LOCAL a LIKE siaf:cant

	LET l_tipo_cupon = l_tipo
	LET l_id_cupon = l_codigo
	
 	LET long = l_id_cupon.NUMCHARS
 	LET i = 0
 	
 	IF (l_tipo_cupon == "ALF") THEN
 		CALL csr:fld_to_upper(l_id_cupon)//transformo a mayúscula
 	END IF
 	
 	WHILE (i <= long)
 		l_caracter = SUBSTR(l_id_cupon,i,1)
 		
 		IF (l_tipo_cupon == "NUM") THEN
 				IF (l_caracter != ".") THEN
					CALL  csr:rangechk(l_caracter,"0-9","n")
					//si no es numérico
					IF (RETVAL != 0) THEN
						RETURN (-2)
					END IF
				END IF
 		ELSE
			ValidChars = ".0123456789abcdefghijklmn\xF1opqrstuvwxyzABCDEFGHIJKLMN\xD1OPQRSTUVWXYZ"
			CALL CSR:str_search(ValidChars,l_caracter,Indice)
			
			//Hago la validacion si el caracter es la letra ñ o Ñ
			IF (l_caracter == "\xF1") THEN
				l_caracter = "\xD1"
				a = i + 1
				l_cadena_1 = SUBSTR (l_id_cupon,0,i)
				l_cadena_2 = SUBSTR (l_id_cupon,a,long)
				FPRINT l_id_cupon ,"%s%s%s",l_cadena_1,l_caracter,l_cadena_2
				LET l_codigo = l_id_cupon
			END IF
			
			IF (Indice < 0) THEN
				RETURN (-2)
			END IF
 		END IF
 		i = i + 1
 	END WHILE
 	
 	RETURN (0)

END SUB

/*******/
