/****o* Reportes/OFB:post:MISCirre_todos_FCNeg
* NOMBRE
*   OFB:post:MISCirre_todos_FCNeg
* DESCRIPCION
*   Reporte Irregularidades - Todas Las Irregularidades.
*   Motivo: Fallos Crem Negativos.
*   Realiza el query de la tabla control_franq segun la fecha y/o operador seleccionado, y ordenada por:
*   Operador, fecha, hora y secuencia.
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   RETURN (-2): No hay Fallos a reportar.
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   post:MISCirre_todos_suc
* DOCUMENTACION
*   Documentación relevante que deberá ser actualizada por la modificación de la rutina
* SOURCE
*/
#include "postmisc.h"

SUB post:MISCirre_todos_FCNeg /*Todas las Irregularidades Fallos Crem Negativos*/
    LOCAL loc_modif  LIKE post:cant_barras  
    LOCAL oper_irre  LIKE tlr:jnl_operator
    
    //cgw:NI50276 - Se realiza el query teniendo en cuenta la clave alternativa "fcrOpFHS" que ordena por:
    //Operador, fecha, hora y secuencia.

    IF (post:opcion_irregula == "SUCURSAL") THEN
    	//Por Sucursal
        CDS SELECT FROM DSC:FallosCrem_tbl BECOMES DSC:FallosCrem_ctx WHERE \
            (post:FalloCrem_fecha >= FLD:adt_umov_fecha_ini && \
             post:FalloCrem_fecha <= FLD:adt_umov_fecha_fin && \
             (post:FalloCrem_estado == "N" || \
              post:FalloCrem_estado == "S" || \
              post:FalloCrem_estado == "A")) \
        ASCENDING SORT "fcrOpFHS"
        //ASCENDING SORT "fcrFechO"
    ELSE
        //Por Operador
        CDS SELECT FROM DSC:FallosCrem_tbl BECOMES DSC:FallosCrem_ctx WHERE \
            (post:FalloCrem_op == adt_umev_op && \
             post:FalloCrem_fecha >= FLD:adt_umov_fecha_ini && \
             post:FalloCrem_fecha <= FLD:adt_umov_fecha_fin && \
             (post:FalloCrem_estado == "N" || \
              post:FalloCrem_estado == "S" || \
              post:FalloCrem_estado == "A")) \
              ASCENDING SORT "fcrOpFHS"

         //ASCENDING SORT "fcrFechO"
    END IF

    CDS FINDFIRST DSC:FallosCrem_ctx NOLOCK
    LET lib:err_retval = CDSRETVAL
  
    IF (post:irregu_esp == "S" && lib:err_retval == 0) THEN  
        //cgw:NI50276: imprime titulo de separacion de Irregularidades especiales,
        //si es la primera irregularidad especial a reportar.                     
         CALL post:MISCtitulo_irreg_esp                                                                  
         CLEARFIELD post:irregu_esp //limpia variable de control de titulo de irregularidades especiales.
     END IF                                                                                              

    //cgw:CDS FINDAGAIN para prevenir no perder contexto, por uso de otros CDS SELECT de otras tablas.
    CDS FINDAGAIN DSC:FallosCrem_ctx NOLOCK
    lib:err_retval = CDSRETVAL

    oper_irre = -1 //cgw:NI50276 - Intancia variable utilizada para el corte de control por operador.
    loc_modif = 0
    WHILE (lib:err_retval == 0)
        CDS EXTRACT DSC:FallosCrem_ctx ALL

        //cgw:NI50276 - Corte de Control por Operador.
	      //El reporte de todas las irregularidades, solo imprime los operadores con irregularidades en el
	      //rango fecha de selección, a diferecia del reporte del motivo irregularidad particular por sucursal.
	      //que imprime todos los operadores que operaron en el rango seleccionado.
        IF (oper_irre != post:FalloCrem_op) THEN
            oper_irre = post:FalloCrem_op
            //cgw:NI50276 - Imprime Operador.
            post:operator_irre = oper_irre
            CALL post:MISCencab_oper_irre
        END IF

        CALL post:MISCwrite_irre_FCrem

	    loc_modif = 1        
        CDS FINDNEXT DSC:FallosCrem_ctx NOLOCK
        LET lib:err_retval = CDSRETVAL
    END WHILE
    IF (loc_modif == 0) THEN
    	CDS ENDSELECT DSC:FallosCrem_ctx    	
	    RETURN (-2)
	ELSE
		post:flag_irregularidades = "S"
    END IF
    
    CDS ENDSELECT DSC:FallosCrem_ctx
END SUB

/*******/
