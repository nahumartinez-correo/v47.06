/****o* Reportes/OFB:post:MISCirre_todos_desc
* NOMBRE
*   OFB:post:MISCirre_todos_desc
* DESCRIPCION
*   Reporte Irregularidades - Todas Las Irregularidades.
*   Motivo: Descuento de Timbres y Sobres Prefranqueados.
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   RETURN (-2): No hay Desc. a reportar.
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   post:MISCirre_todos_suc
* DOCUMENTACION
*   Documentación relevante que deberá ser actualizada por la modificación de la rutina
* SOURCE
*/
#include "postmisc.h"

SUB post:MISCirre_todos_desc
    LOCAL loc_modif  LIKE post:cant_barras
    LOCAL oper_irre  LIKE tlr:jnl_operator

    IF (post:opcion_irregula == "SUCURSAL") THEN
    	//Por Sucursal
	    CDS SELECT FROM DSC:tlr:TABjnl BECOMES DSC:tlr:CONjscn WHERE ( \
    	    tlr:jnl_date >= FLD:adt_umov_fecha_ini &&\
        	tlr:jnl_date <= FLD:adt_umov_fecha_fin && \
        	(post:jnl_flia_tran == "CSP" || post:jnl_flia_tran == "CSE") && \
        	 post:tipo_descto != "") 
     ELSE
        //Por Operador
    	CDS SELECT FROM DSC:tlr:TABjnl BECOMES DSC:tlr:CONjscn WHERE \
        	(tlr:jnl_date >= FLD:adt_umov_fecha_ini && \
        	 tlr:jnl_date <= FLD:adt_umov_fecha_fin && \
        	 tlr:jnl_operator == adt_umev_op && \
        	 (post:jnl_flia_tran == "CSP" || post:jnl_flia_tran == "CSE") && \
        	 post:tipo_descto != "") 
    END IF 	
    CDS FINDFIRST DSC:tlr:CONjscn NOLOCK
    LET lib:err_retval = CDSRETVAL

    IF (post:irregu_esp == "S" && lib:err_retval == 0) THEN
        //cgw:NI50276: imprime titulo de separacion de Irregularidades especiales,
        //si es la primera irregularidad especial a reportar. 
        CALL post:MISCtitulo_irreg_esp
        CLEARFIELD post:irregu_esp //limpia variable de control de titulo de irregularidades especiales.
    END IF


    //cgw:CDS FINDAGAIN para prevenir no perder contexto, por uso de otros CDS SELECT de otras tablas.
    CDS FINDAGAIN DSC:tlr:CONjscn NOLOCK
    lib:err_retval = CDSRETVAL

    oper_irre = -1 //cgw:NI50276 - Intancia variable utilizada para el corte de control por operador.

    loc_modif = 0
    WHILE (lib:err_retval == 0)    
        CDS EXTRACT DSC:tlr:CONjscn post:tipo_descto tlr:jnl_operator
           IF (oper_irre != tlr:jnl_operator) THEN    
               oper_irre = tlr:jnl_operator
               //cgw:NI50276 - Imprime Operador.
               post:operator_irre = oper_irre
               CALL post:MISCencab_oper_irre
               CDS FINDAGAIN DSC:tlr:CONjscn NOLOCK
            END IF
       
        
        CALL post:MISCwrite_irre_desc
	    loc_modif = 1        
        CDS FINDNEXT DSC:tlr:CONjscn NOLOCK
        LET lib:err_retval = CDSRETVAL
    END WHILE
    IF (loc_modif == 0) THEN
    	CDS ENDSELECT DSC:tlr:CONjscn 	
	    RETURN (-2)
	  ELSE
		post:flag_irregularidades = "S"
    END IF

    CDS ENDSELECT DSC:tlr:CONjscn
END SUB

/*******/
