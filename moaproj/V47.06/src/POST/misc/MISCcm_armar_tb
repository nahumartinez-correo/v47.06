/****o* Transferencias Bancarias/OFB:post:MISCcm_armar_tb
* NOMBRE
*   OFB:post:MISCcm_armar_tb
* DESCRIPCION
*   Esta rutina envia en el bit 110 los datos de id y saldo de las transferencias bancarias utilizadas en la TX de
*   Cobranza de facturas vencidas o en la TX de aplicacion de adelantos.
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   OFB:post:MISCclimed_ingr_fact
* DOCUMENTACION
*   http://jira.sibs.net/jira/browse/AUT-350
* SOURCE
*/
#include "postmisc.h"

SUB post:MISCcm_armar_tb


	local loc_retval       like lib:err_retval
	local varl_importe     like siaf:importe
	local varl_importe2    like common:csr_string
	local varl_nro_tb      like post:adt_tranb_nro

	post:BulkFldCant   = 0
	clearfield post:datos_msg110

	CDS SELECT FROM DSC:post:Transf_Banc_tbl BECOMES DSC:post:Transf_Banc_ctx \
	WHERE post:adt_tranb_op == op:op_operator_id && \
	post:adt_tranb_marca == "*"

	CDS FINDFIRST DSC:post:Transf_Banc_ctx NOLOCK
    	lib:err_retval = CDSRETVAL
	if ( lib:err_retval == -801  )
		CDS ENDSELECT DSC:Transf_Banc_ctx
		return 0
	else
		if ( lib:err_retval < 0 )
			msgbox "Error al obtener los datos de las Transferencias Bancarias.",\
			OFB_OK,"Atencion"
			CDS ENDSELECT DSC:Transf_Banc_ctx
			return -1
		end if
	end if

	WHILE (lib:err_retval ==  0 )

		clearfield siaf:nota_1
		clearfield varl_nro_tb
		clearfield varl_importe

		CDS EXTRACT DSC:Transf_Banc_ctx \
			post:adt_tranb_nro      into varl_nro_tb \
			post:adt_tranb_consumo into varl_importe
	
		varl_importe2 = varl_importe * 100
	
		fprint siaf:nota_1,"%015.15s%012.12s",varl_nro_tb,varl_importe2

		fprint post:datos_msg110,"%s%s",post:datos_msg110,siaf:nota_1
		post:BulkFldCant   = post:BulkFldCant + 1

    		CDS FINDNEXT DSC:Transf_Banc_ctx NOLOCK
    		lib:err_retval = CDSRETVAL
		if ( lib:err_retval == -801 )
			loc_retval = 0
			break
		else
			if ( lib:err_retval < 0 )
				msgbox \
				"Error al obtener los datos de las Transferencias Bancarias.",\
				OFB_OK,"Atencion"
				loc_retval = -1
				break
			end if
		end if
	
	END WHILE

	fprint post:datos_msg110,"%s%02.2s%s",post:consumo_operacion,\
	post:BulkFldCant,post:datos_msg110

	CDS ENDSELECT DSC:Transf_Banc_ctx

	return loc_retval
END SUB

/*******/
