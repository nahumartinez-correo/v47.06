/****o* Pago en Destino/OFB:post:MISCrecuperoCM_PED
* NOMBRE
*   OFB:post:MISCrecuperoCM_PED
* DESCRIPCION
*   Recupera los datos del Cliente Medio
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*		post:PBcargaFilePED
* DOCUMENTACION
*   NI50479
* SOURCE
*/
#include "postmisc.h"

SUB post:MISCrecuperoCM_PED
	LOCAL l_CloTipDoc LIKE post:CloTipDoc
	LOCAL l_CloCuit LIKE post:CloCuit

	//Estos son los datos basicos del Emisor que no deben faltar en el mensaje al HOST
	IF (post:cli_tipoPED.NUMCHARS > 0 && post:cliente_numero_ped.NUMCHARS > 0 && post:TipoDocEmisPED.NUMCHARS > 0 && post:NroDocEmisPED.NUMCHARS > 0) THEN
		RETURN 0
	END IF
	
	//Traigo los datos basicos del Cliente de la Presu
	CDS SELECT FROM DSC:post:TABPresu BECOMES DSC:post:CONPresu \
		WHERE post:fecha == common:system_date \
		&& post:PresNro == post:PresNro
	
	CDS FINDFIRST DSC:post:CONPresu NOLOCK
	IF (CDSRETVAL == 0) THEN
		CDS EXTRACT DSC:post:CONPresu \
			post:CloTipDoc INTO l_CloTipDoc \
			post:CloCuit INTO l_CloCuit
	ELSE
		MSGBOX "No se pudo recuperar los datos del Emisor de la tabla Presu",OFB_OK,"Error"
		RETURN -2	
	END IF
	CDS ENDSELECT DSC:post:CONPresu

	//Recupero los datos de la CliMed
	CDS SELECT FROM DSC:post:TABCliMed BECOMES DSC:post:CONCliMed \
		WHERE post:CliMed_tipo == 5 \
		&& post:CloTipDoc == l_CloTipDoc \
		&& post:CloCuit == l_CloCuit

	CDS FINDFIRST DSC:post:CONCliMed NOLOCK
	IF (CDSRETVAL == 0) THEN
		CDS EXTRACT DSC:post:CONCliMed \
			post:es_juridica INTO post:es_juridicaPED \
			post:CloRazon INTO post:CliRazonPED \
			post:CloTipDoc INTO post:TipoDocEmisPED \
			giros:NroDocEmisLG_FCuit INTO post:NroDocEmisPED \
			post:CliMed_tipo INTO post:cli_tipoPED \
			post:CliMed_numero INTO post:cliente_numero_ped \
			giros:NInscRegEmisGm_fld INTO post:NInscRegEmisPED \
			post:CloCuit INTO post:CuitEmisPED \
			giros:OcupacionEmisGm_fld INTO post:OcupacionEmisPED \
			giros:OtrosOcupEmisGm_fld INTO post:OtrosOcupEmisPED \
			giros:CodPaisResidEmisGm INTO post:CodPaisResidEmisPED \
			post:CloApellido INTO post:CliApellidoPED \
			post:CloNombre INTO post:CliNombrePED \
			post:IvaCod INTO post:IvaCodEmisPed	
	ELSE
		CDS ENDSELECT DSC:post:CONCliMed
		MSGBOX "No se pudo recuperar los datos del Emisor de la tabla CliMed",OFB_OK,"Error"
		RETURN -2
	END IF
	CDS ENDSELECT DSC:post:CONCliMed
	
	//Traigo la descripcion del pais
	CALL giros:CDSbusca_primer_pais(post:CodPaisResidEmisPED,post:PaisResidEmisPED)

	//Recupero los datos de la CliMedSucu
	CDS SELECT FROM DSC:post:TABCliMedSucu BECOMES DSC:post:CONCliMedSucu \
		WHERE post:CliMed_tipo == 5 \
		&& post:CloTipDoc == l_CloTipDoc \
		&& post:CloCuit == l_CloCuit
	
	CDS FINDFIRST DSC:post:CONCliMedSucu NOLOCK
	IF (CDSRETVAL == 0) THEN
		CDS EXTRACT DSC:post:CONCliMedSucu \
			post:CloCalle INTO post:CalleEmisPED \
			post:CloNroDir INTO post:NroCalleEmisPED \
			post:CloCodPost INTO post:CodPostEmisPED \
			post:CloTelefono INTO post:TelEmisPED \
			post:CloCelular INTO post:CelEmisPED \
			post:CloMail INTO post:CliEmisMailPED \
			giros:CiudadEmisGm_fld INTO post:CiudadEmisPED \
			post:CloProvin INTO post:CodProvEmisPED \
			post:CloLoc INTO post:LocEmisPED_WU \
			post:CloPiso INTO post:PisoEmisPED
	ELSE
		CDS ENDSELECT DSC:post:CONCliMedSucu
		MSGBOX "No se pudo recuperar los datos del Emisor de la tabla CliMedSucu",OFB_OK,"Error"
		RETURN -2
	END IF
	CDS ENDSELECT DSC:post:CONCliMedSucu
	
	//Traigo la descripcion de la Provincia
	CALL MISCProvinciaFVAL(post:CodProvEmisPED,post:ProvSucEmisPED)
	RETURN 0

END SUB

/*******/
