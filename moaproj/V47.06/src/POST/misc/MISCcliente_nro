/****o* Uso General/OFB:post:MISCcliente_nro
* NOMBRE
*   OFB:post:MISCcliente_nro
* DESCRIPCION
*   Setea los campos post:climed_tipo y post:climed_numero utilizados en mensajeria segun el cliente seleccionado.
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   OFB:tes:MISCsave_Debito,OFB:post:SVALrecepChk,OFB:post:SVALidentifica_cli,OFB:post:MISCalta_cheque
* DOCUMENTACION
*   http://jira.sibs.net/jira/browse/AUT-350
* SOURCE
*/
#include "postMISC.h"

SUB post:MISCcliente_nro(prm_clitipo,prm_destino)
local l_destino like common:csr_string
l_destino = prm_destino
call csr:fld_to_upper(l_destino)
if (l_destino != "H" && l_destino != "L") then
	// pongo valor por default
	l_destino = "H"
endif

	SELECT (prm_clitipo)

		CASE CTACTE
		        IF (l_destino == "H") THEN //Modo Host
			    climed_tipo = CTACTE_HOST
			END IF
			IF (l_destino == "L") THEN //Modo Local
			    climed_tipo = prm_clitipo
			END IF
			climed_numero = post:CloCuit
			BREAK
		CASE OCASIONAL
			climed_tipo = OCASIONAL
			CALL post:CDScli_dig_verif
			IF (RETVAL == 0)
			    climed_numero = post:CloCuit
			ELSE
			    FPRINT climed_numero,"%s%s",post:adt_TipoDoc_tipo,post:CloCuit
			ENDIF
			BREAK
		CASE CLIUP
		        IF (l_destino == "H" || ModalidadTB || ConsumoAnticipo) THEN //Modo Host
			    climed_tipo = UP_HOST
			ELSE
				IF (l_destino == "L") THEN //Modo Local
			    		climed_tipo = prm_clitipo
			    	ENDIF
			END IF
			
			climed_numero = post:NisUP
			BREAK
		CASE CLIMED
		        // Para Cliente Medio ya existe un tratamiento propio para setear
		        // los campos post:climed_tipo y post:climed_numero.
			BREAK

	END SELECT

	IF (l_destino == "H") THEN //Modo Host
	    fprint post:cliente,"%1.1s%-11.11s",post:climed_tipo,post:climed_numero
	END IF

END SUB
	





/*******/
