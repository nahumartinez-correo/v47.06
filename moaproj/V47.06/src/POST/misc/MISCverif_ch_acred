/****o* Lista de Giros/OFB:post:MISCverif_ch_acred
* NOMBRE
*   OFB:post:MISCverif_ch_acred
* DESCRIPCION
*   Si está realizando un Adelanto Monetario verifica si el monto del cheque a enviar por RT BATCH fue
*	acreditado en el momento o si se acredita desde adm regional.
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   OFB:post:MISCacredito_adel,post:SVALreemp_chqrob,OFB:post:SVALcanc_reemp,OFB:post:SVALcancReempRob
*	OFB:post:SVALreemp_chqrech_up,OFB:post:MISCcalc_adel_lg
* REFERENCIADO POR
*   OFB:post:MISCpaga_cheque
* DOCUMENTACION
*   http://jira.sibs.net/jira/browse/AUT-520
* SOURCE
*/
#include "postMISC.h"

SUB post:MISCverif_ch_acred
local l_new_date like post:fec_e
	
	if (post:ModalidadAT) then
		post:jnl_cheques_adel = "S" //indica que los cheques estan asociados a una operacion de Adelanto CM
		FPRINT post:cheq_adel_id,"%015.15s", post:nro_recibo
		
		if (siaf:motivo == "ADEMO")
	
			post:cheq_tipo_adel = "G"
			select (siaf:moneda)
			case "03"
				post:cheq_acreditado = "N"//indica que el monto del cheque NO es acreditado en el momento al saldo de adelanto(se hace por Adm Regional)
				break
			case "15"
				//si es certificado en plazo entonces acredito el saldo en el momento
				/*Se debe consultar de la tabla MedPago el atributo post:adt_mp_diasatras y verificar que
				 la fecha de emisión del cheque + los días del campo post:adt_mp_diasatras 
				 sea mayor o igual a la fecha actual para saber si el cheque está en plazo.*/
				call  csr:date_add (fec_e,  post:adt_mp_dias_plazo,  "d",  l_new_date)
				call csr:date_compare(common:system_date ,l_new_date)
				/*si l_new_date es igual o mayor a la fecha del dia entonces el cheque esta en plazo*/
				If (RETVAL == 0 || RETVAL == 2) Then
					post:cheq_acreditado = "S"
				else
					post:cheq_acreditado = "N"
				endif
				break
			end select
		else
			post:cheq_acreditado = "S" //indica que el monto del cheque es acreditado en el momento al saldo de adelanto
			clearfield post:cheq_tipo_adel
		end if
	else
		post:jnl_cheques_adel = "N"//indica que los cheques NO estan asociados a una operacion de Adelanto CM
	endif
END SUB

/*******/
