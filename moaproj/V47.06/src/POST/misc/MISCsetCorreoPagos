/****o* Pendientes de agrupación/OFB:post:MISCsetCorreoPagos
* NOMBRE
*   OFB:post:MISCsetCorreoPagos
* DESCRIPCION
*   Rutina que reemplaza una cadena por otra,si esta en la tabla leyendaPF
* ENTRADAS
*   p_bob_leyendaPF = "ticket" o "comprobante"
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   post:MISCobtiene_impreSf1
*		post:MISCleo_datos_bob
* DOCUMENTACION
*   NI50455 CORREO PAGOS
* SOURCE
*/
#include "postmisc.h"

SUB post:MISCsetCorreoPagos(p_bob_leyendaPF)
	LOCAL l_retval LIKE lib:err_retval
	LOCAL l_sf1_comprobante LIKE post:sf1_comprobante
	LOCAL l_sf1_ticket LIKE post:sf1_ticket
	LOCAL INICIO LIKE post:cantidad 
	LOCAL FIN LIKE post:cantidad 
	LOCAL LONGITUD LIKE post:cantidad 
	LOCAL l_bob_leyendaPF LIKE common:csr_string
	
	LET l_bob_leyendaPF = p_bob_leyendaPF

	CDS SELECT FROM DSC:post:leyendaPF_tbl BECOMES DSC:post:leyendaPF_ctx \
		WHERE post:adt_act_leyendaPF == "S" && \
					post:adt_codigo_leyendaPF == siaf:codigo_siaf && \
					post:adt_bob_leyendaPF == l_bob_leyendaPF
		
		
	CDS FINDFIRST DSC:post:leyendaPF_ctx NOLOCK
	LET l_retval = CDSRETVAL

	//modifico el indicador de segmentos de multi-lenguaje para que no moleste
	//al formatear el comprobante...
	INIT 67,"^^"

	WHILE (l_retval == 0)
		CDS EXTRACT DSC:post:leyendaPF_ctx ALL

		//Cambio la descripcion del campo post:sf1_comprobante a mano
		//cuando el cambio ya lo realice SEPSA , hay que actualizar los campos post:adt_act_leyendaPF = "N" en 
		//la tabla leyendaPF de main
		IF (post:adt_bob_leyendaPF == "comprobante") THEN
			CALL csr:str_search (post:sf1_comprobante,adt_busca_leyendaPF,INICIO) 
			//FIN = INICIO + post:adt_reemp_leyendaPF.NUMCHARS
			FIN = INICIO + post:adt_busca_leyendaPF.NUMCHARS
			LONGITUD = post:sf1_comprobante.NUMCHARS
			FPRINT l_sf1_comprobante "%s%s%s", SUBSTR(post:sf1_comprobante,0,INICIO),post:adt_reemp_leyendaPF,SUBSTR(post:sf1_comprobante,FIN,LONGITUD)
			LET post:sf1_comprobante = l_sf1_comprobante
		//Cambio la descripcion del campo post:sf1_ticket	
		ELSEIF (post:adt_bob_leyendaPF == "ticket") THEN
			CALL csr:str_search (post:sf1_ticket,post:adt_busca_leyendaPF,INICIO) 
			//FIN = INICIO + post:adt_reemp_leyendaPF.NUMCHARS
			FIN = INICIO + post:adt_busca_leyendaPF.NUMCHARS
			LONGITUD = post:sf1_ticket.NUMCHARS
			FPRINT l_sf1_ticket "%s%s%s", SUBSTR(post:sf1_ticket,0,INICIO),post:adt_reemp_leyendaPF,SUBSTR(post:sf1_ticket,FIN,LONGITUD)
			LET post:sf1_ticket = l_sf1_ticket		
		END IF

		CDS FINDNEXT DSC:post:leyendaPF_ctx NOLOCK
		LET l_retval = CDSRETVAL
	END WHILE
	
	CDS ENDSELECT DSC:post:leyendaPF_ctx

	//reestablezco el separador de multi-lenguaje
	INIT 67,"$$"

END SUB

/*******/
