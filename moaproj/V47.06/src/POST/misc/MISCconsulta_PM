/****o*Clientes Mayoristas/OFB:post:MISCconsulta_PM
*NOMBRE
*   OFB:post:MISCconsulta_PM
*DESCRIPCION
*   Se obtiene los campos BULK's y se va armando el picklist dinamico para
*   la visualizacion de los Productos Mayoristas.
* ENTRADAS
*   datos:se pasa el campo bulk correspondientes a los bits 105-106-107.
*   long:se pasa la longitud de dichos campos.
* SALIDAS
*   Retorna el Valor de loc_retval: "0" cuando pudo armar el picklist, "-2" cuando no pudo armar
*   el picklist o los productos superan los 999.
* IMPACTOS
* REFERENCIADO POR
*   OFB:post:OFBConsulta_PM_sap
*DOCUMENTACION
*   |html <a href="http://livelink.intranet.siemens-itron.com.ar/livelink/livelink.exe?func=doc.ViewDoc&nodeId=550881&">Mensajería CC</a>
*SOURCE
*/
#include "postmisc.h"

SUB post:MISCconsulta_PM(datos,long)

	local posicion          like post:cantidad
	local longitud          like post:Longitud_105
	local varl_clave_mosaic like post:adt_pm_clMosaic
	local varl_clave_sap 	like post:adt_pm_material
	local varl_desc		like post:adt_pm_matdesc
	local varl_um		like post:adt_pm_um
	local varl_array	like common:csr_string
	local loc_retval	like lib:err_retval
			
	/*********************************************************************
	Se obtiene los campos BULK's y se va armando el picklist dinamico para
	la visualizacion de los Productos Mayoristas.
	La consulta multiple retorna:
	CLAVE MOSAIC. - CLAVE SAP - DESCRIPCION - UNIDAD DE MEDIDA
	GUARDAR TODO ESO EN EL CAMPO siaf:paquete_rcv
	**********************************************************************/
	
	fprint longitud,"%s",long
	posicion = 0
	loc_retval = -2
	
	
	while ( posicion < longitud )

		clearfield common:csr_string

		varl_clave_mosaic = substr(datos,posicion,40)
		posicion = posicion + 40

		varl_clave_sap = substr(datos,posicion,18)
		posicion = posicion + 18

		varl_desc = substr(datos,posicion,40)
		posicion = posicion + 40

		varl_um = substr(datos,posicion,3)
		posicion = posicion + 3

		call csr:trimfield varl_clave_mosaic
		call csr:trimfield varl_clave_sap
		call csr:trimfield varl_desc
		call csr:trimfield varl_um
		//more data, se alinea a izquierda para que los espacios en blanco queden a derecha.
		fprint post:cc_ult_item ,"%-40.40s",varl_clave_mosaic
		
		if ( varl_clave_mosaic.numchars != 0)
			//armo el array que se usa en la rutina post:MISCactualizar_tabla
			//para cargar la tabla de productos mayoristas
			if (siaf:idx < 999) then
				fprint varl_array "%-40.40s%-18.18s%-30.30s%3.3s", varl_clave_mosaic,varl_clave_sap,varl_desc,varl_um
				siaf:paquete_rcv[siaf:idx] = varl_array
				let siaf:idx = siaf:idx + 1
			else
				loc_retval = -2
				msgbox "LA CANTIDAD DE PRODUCTOS SUPERA LOS 999",OFB_OK, "Atencion"
				break
			endif
			FPRINT common:csr_string "%-18.18s %-30.30s %3.3s    %-40.40s" \
    	    		varl_clave_sap,varl_desc,varl_um,varl_clave_mosaic
	   	    	CALL CSR:f_saaddpl("prodmay_pic",common:csr_string)
   			if (retval < 0 )
				loc_retval = -2
				msgbox \
				"ERROR AL AGREGAR PRODUCTOS AL PICKLIST.",OFB_OK,"Atencion"
				break
			else
				loc_retval = 0
			end if
		
		end if

	end while

	return loc_retval

END SUB

/*******/
