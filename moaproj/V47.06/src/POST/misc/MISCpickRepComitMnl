/****o* Cobro por Cuenta de Terceros/OFB:post:MISCpickRepComitMnl
* NOMBRE
*   OFB:post:MISCpickRepComitMnl
* DESCRIPCION
*   Carga el PickList con los datos de los Comitentes
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*  post:PREC6670
* DOCUMENTACION
*   https://jirats.siemens.com.ar/jira/browse/AUT-1684
* SOURCE
*/
#include "postmisc.h"

SUB post:MISCpickRepComitMnl
    LOCAL l_Comitente LIKE common:csr_string
    LOCAL l_Comitente_id LIKE common:csr_string
    LOCAL l_cdsretval LIKE lib:err_retval 

    //Lo hago al principio porque se requiere que entre en la transaccion
    CALL csr:f_sadroppl "comitente"
    CALL csr:f_sanewpl  "comitente" pic:PICcomitente_Mnl 290 200

    CDS ENDSELECT DSC:ParamCCT_ctx
    
    CDS SELECT SEMIDISTINCT post:adt_cct_comit_desc,post:adt_cct_comitente FROM DSC:ParamCCT_table BECOMES DSC:ParamCCT_ctx WHERE \
        post:adt_cct_cod_barra == "N" && \
        post:adt_cct_vig_inic <= fld:common:system_date && \
        (adt_cct_vig_fin >=  fld:common:system_date || adt_cct_vig_fin == "        ") \
        SORT "Comiten"    

    CDS FINDFIRST DSC:ParamCCT_ctx NOLOCK
    l_cdsretval = CDSRETVAL
    
    IF (l_cdsretval < 0) THEN
        CDS ENDSELECT DSC:ParamCCT_ctx
        RETURN 0
    END IF

    DO
        CLEARFIELD l_Comitente
        CLEARFIELD l_Comitente_id        

	    CDS EXTRACT DSC:ParamCCT_ctx post:adt_cct_comit_desc INTO l_Comitente, \
	                                 post:adt_cct_comitente  INTO l_Comitente_id 
        CLEARFIELD common:csr_string    
        FPRINT common:csr_string "%-36.36s       %10.10s" l_Comitente, l_Comitente_id

        CALL csr:f_saaddpl "comitente" common:csr_string           
                           
        CDS FINDNEXT DSC:ParamCCT_ctx NOLOCK
        l_cdsretval = CDSRETVAL
        IF (l_cdsretval < 0)
            BREAK
        END IF
    LOOP

    CDS ENDSELECT DSC:ParamCCT_ctx

END SUB

/*******/
