/****o* Expedición/OFB:post:MISCRetJournal
* NOMBRE
*   OFB:post:MISCRetJournal
* DESCRIPCION
*   Actualiza el Journal para las piezas transferidas pendientes de aceptacion 
*   cuando el operador hace un cierre forzado de caja
* ENTRADAS
*   p_jnl_branch_ori,p_jnl_operator_ori,p_jnl_date_ori,p_jnl_time_ori,p_jnl_bus_date_ori,p_jnl_sequence_ori
* SALIDAS
*   Retval = -801 si se produjo un error
*	Retval = 0 si no se produjo error
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*  post:MISCRetPzasPend
* DOCUMENTACION
*   http://jira.sibs.net/jira/browse/AUT-50242
* SOURCE
*/
#include "postmisc.h"

SUB post:MISCRetJournal(p_jnl_branch_ori,p_jnl_operator_ori,p_jnl_date_ori,p_jnl_time_ori,p_jnl_bus_date_ori,p_jnl_sequence_ori)
	LOCAL l_cdsretval LIKE lib:err_retval
  	LOCAL l_jnl_branch_ori LIKE post:jnl_branch_ori       
    LOCAL l_jnl_operator_ori LIKE post:jnl_operator_ori     
    LOCAL l_jnl_date_ori LIKE post:jnl_date_ori         
    LOCAL l_jnl_time_ori LIKE post:jnl_time_ori         
    LOCAL l_jnl_bus_date_ori LIKE post:jnl_bus_date_ori    
    LOCAL l_jnl_sequence_ori LIKE post:jnl_sequence_ori  
    
    l_jnl_branch_ori = p_jnl_branch_ori
    l_jnl_operator_ori = p_jnl_operator_ori
    l_jnl_date_ori = p_jnl_date_ori  
    l_jnl_time_ori = p_jnl_time_ori
    l_jnl_bus_date_ori = p_jnl_bus_date_ori
    l_jnl_sequence_ori = p_jnl_sequence_ori
	
    CDS SELECT FROM "journal" BECOMES "journal_ctx" \
        WHERE tlr:jnl_branch == l_jnl_branch_ori \
        && tlr:jnl_operator == l_jnl_operator_ori \
        && tlr:jnl_date == l_jnl_date_ori \
        && tlr:jnl_time == l_jnl_time_ori \
        && tlr:jnl_bus_date == l_jnl_bus_date_ori \
        && tlr:jnl_sequence == l_jnl_sequence_ori
        
	CDS FINDFIRST "journal_ctx" LOCK

	LET l_cdsretval = CDSRETVAL
	
	IF (l_cdsretval == -801) THEN
	    CDS ENDSELECT "journal_ctx"
	    RETURN l_cdsretval
	END IF
	
	DO
		IF (l_cdsretval != 0) THEN
			BREAK
		END IF
        
        CLEARFIELD post:jnl_guia_cont
        CLEARFIELD post:jnl_fecha_llencont
        CLEARFIELD post:jnl_hora_llencont

    	CDS APPEND "journal_ctx" \
    		post:jnl_guia_cont \
    		post:jnl_fecha_llencont \
    		post:jnl_hora_llencont
        
        CDS UPDATE "journal_ctx" LOCK
  
		LET l_cdsretval = CDSRETVAL
	
    	IF (l_cdsretval == -801) THEN
    	    CDS ENDSELECT "journal_ctx"
    	    RETURN l_cdsretval
    	END IF
	
		CDS FINDNEXT "journal_ctx" LOCK
		LET l_cdsretval = CDSRETVAL
		
		CDS CLEARBUFFER "journal_ctx"
	LOOP
	
	CDS ENDSELECT "journal_ctx"
	RETURN 0
	    
END SUB  

/*******/
