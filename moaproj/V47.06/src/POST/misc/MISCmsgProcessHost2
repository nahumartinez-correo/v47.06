/****o* Mensajería/OFB:post:MISCmsgProcessHost2
* NOMBRE
*   OFB:post:MISCmsgProcessHost2
* DESCRIPCION
*   Esta OFB se encarga de armar el mensaje a enviar al host2 en base a los
*	parametros de entrada
* ENTRADAS
*   pName nombre del script a ejecutar en el servidor
*	pWait (true o false) indica que se espera una confirmación de que el proceso termino.
*	pParameters cadena con los parámetros que necesitará el script separados por punto y coma.
*	pfieldForprocess campo de tipo string donde se devolverá el mensaje resultante
* SALIDAS
*   pfieldForprocess mensaje resultante
* IMPACTOS
*   La modificación erronea puede producir que no se ejecuten los scripts
* REFERENCIADO POR
*   tes:MISCenv_esp_total, post:MISCverif_bloqNovTT, post:MISCenv_stockHist
*	post:MISCenv_NovTT, post:MISCenv_merch_total, post:MISCenv_jnl_total
*	post:MISCenv_Excep, tes:MISCLanzaProceso
* DOCUMENTACION
*   Documentación relevante que deberá ser actualizada por la modificación de la rutina
* SOURCE
*/

SUB post:MISCmsgProcessHost2 (pName, pParameters, pfieldForprocess)
;Los parámetros son todos de tipo string
;pName contendrá el nombre del script a ejecutarse desde el host2
;pParameters contendrá una cadena con todos los parámetros separados por punto y coma (;)
;pfieldForprocess será el campo donde se almacenará el mensaje resultante para enviar al host2
;Si se quiere concatenar varios procesos se deberá ejecutar esta rutina las veces necesarias
;y en ese caso este campo contendrá el mensaje del proceso anterior y devolverá concatenado
;el siguiente proceso.
LOCAL INDICE_INF LIKE post:cantidad
LOCAL INDICE     LIKE post:cantidad
LOCAL l_cantidad     LIKE post:cantidad
LOCAL l_subcadena1 LIKE common:csr_string
LOCAL l_subcadenaAux LIKE common:csr_string
LOCAL l_subcadenaAux2 LIKE common:csr_string
LOCAL l_EncontroCaracter LIKE siaf:i
	call post:MISCinsertTagOrValue (pfieldForprocess,"<process>")
		call post:MISCinsertTagOrValue (pfieldForprocess,"<name>")
			call post:MISCinsertTagOrValue (pfieldForprocess,pName)
		call post:MISCinsertTagOrValue (pfieldForprocess,"</name>")
		if (pParameters != "") then
			call post:MISCinsertTagOrValue (pfieldForprocess,"<parameters>")
        	
        	INDICE_INF = 0
			l_subcadenaAux = pParameters
			CALL csr:str_search (l_subcadenaAux,";",INDICE,INDICE_INF)
			l_EncontroCaracter = retval == 0
			WHILE (l_EncontroCaracter)
				call post:MISCinsertTagOrValue (pfieldForprocess,"<parameter>")
				l_cantidad = INDICE - INDICE_INF
				l_subcadena1 = SUBSTR(l_subcadenaAux,0,l_cantidad)
				call post:MISCinsertTagOrValue (pfieldForprocess,l_subcadena1)
				call post:MISCinsertTagOrValue (pfieldForprocess,"</parameter>")
				INDICE = INDICE + 1
				INDICE_INF = l_subcadenaAux.numchars
				l_subcadenaAux = SUBSTR(l_subcadenaAux,INDICE,INDICE_INF)
				INDICE_INF = 0
				CALL csr:str_search (l_subcadenaAux,";",INDICE,INDICE_INF)
				l_EncontroCaracter = retval == 0
			END WHILE
			if (pParameters != "") then
        		call post:MISCinsertTagOrValue (pfieldForprocess,"<parameter>")
        		call post:MISCinsertTagOrValue (pfieldForprocess,l_subcadenaAux)
        		call post:MISCinsertTagOrValue (pfieldForprocess,"</parameter>")
        	end if
			call post:MISCinsertTagOrValue (pfieldForprocess,"</parameters>")
		end if
	call post:MISCinsertTagOrValue (pfieldForprocess,"</process>")
END SUB

/*******/
