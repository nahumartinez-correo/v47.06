/****o* Cobro por Cuenta de Terceros/OFB:post:MISClst_RepCREM_enc1
* NOMBRE
*   OFB:post:MISClst_RepCREM_enc1
* DESCRIPCION
*   Genera el encabezado del Reporte CREM Cliente Mayorista con Cobro por CT.
* ENTRADAS
*   p_lineas_cl = inicializa el contador de lineas en cero
*   p_l_imprimible = cantidad de caracteres a imprimir por linea
*	link_num = canal de comunicacion numerico
*	report_name = nombre del reporte
*	p_scl = es la escala de la letra
*	p_ln = cantidad de lineas a imprimir por hoja
*	p_size = tamaño de la letra
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   post:MISClst_RepCREM_enc
* DOCUMENTACION
*   https://jirats.siemens.com.ar/jira/browse/AUT-1684
* SOURCE
*/
#include "postmisc.h"

SUB post:MISClst_RepCREM_enc1(p_lineas_cl,p_l_imprimible,link_num,report_name,p_scl,p_ln,p_style)

	LOCAL l_imprimible     LIKE siaf:i
	LOCAL l_visible        LIKE siaf:i
	LOCAL l_espacios       LIKE siaf:i
	LOCAL espaciosBlancos  LIKE siaf:i
	LOCAL cadena           LIKE common:csr_string_aux
	LOCAL cadena0          LIKE common:csr_string
	LOCAL cadena1          LIKE common:csr_string
	LOCAL cadena2          LIKE common:csr_string_aux
	LOCAL cadena3          LIKE common:csr_string
	LOCAL cadena4          LIKE common:csr_string
	LOCAL cadena5          LIKE common:csr_string
	LOCAL line_csr_aux     LIKE common:csr_string_aux
	LOCAL split            LIKE common:csr_string_4096
	LOCAL l_oper_CCT  LIKE post:operador_CCT
	
	LET FLD:tes:INDsheet_header = OFB:post:MISClst_RepCREM_enc
	cadena2 = line_csr_aux
	
	// cuenta la cant. hojas (= cant. de encab.)
	LET siaf:indica_calcu = siaf:indica_calcu + 1
	
	siaf:lineas_cl = p_lineas_cl 
	l_imprimible = p_l_imprimible
	
	cadena0 = "<pie>1</pie><long>"
	cadena1 = "</long><or>0</or><scl>"	
	cadena3 = "</scl><ln>"
	cadena4 = "</ln><txS>90</txS><txF>Courier New</txF><st>"
	cadena5 = "</st>"
	
	FPRINT line_csr_aux, "%s%s%s%s%s%s%s%s%s" , cadena0, l_imprimible, cadena1, p_scl, cadena3, p_ln , cadena4 ,p_style ,cadena5
	//Ej salida de line_csr_aux 
	//<pie>1</pie><long>l_imprimible</long><or>1</or><scl>p_scl</scl><ln>p_ln</ln><txS>100</txS><txF>Courier New</txF><st>p_size</st>
	//<pie>1</pie><long>142</long><or>1</or><scl>90</scl><ln>46</ln><txS>100</txS><txF>Courier New</txF><st>12</st>
	
	IF (siaf:pantalla_impreso != "P") THEN
		CALL post:MISCwrite_file_CCT(link_num,line_csr_aux)
	END IF

	CALL post:MISCwrite_file_CCT(link_num," ")
	
	LET op:adt_login_id = op:op_login_id
	;Busco descripcion de la sucursal
	admin:adt_br_branch = common:sucursal_activa
	CALL admin:CDSadt_br_load
	l_espacios = l_imprimible
	FPRINT line_csr_aux, "%8e - %e Pag.:%s", common:system_date,common:time_of_day, siaf:indica_calcu
	espaciosBlancos = l_espacios - line_csr_aux.NUMCHARS - common:emp_desc.NUMCHARS
	FPRINT line_csr_aux "%s%*.*s%s", common:emp_desc, espaciosBlancos, espaciosBlancos," ",line_csr_aux
	CALL post:MISCwrite_file_CCT(link_num,line_csr_aux)
	CALL post:MISCwrite_file_CCT(link_num," ")
	FPRINT line_csr_aux report_name
	espaciosBlancos = (l_espacios - line_csr_aux.NUMCHARS) / 2
	FPRINT line_csr_aux "%*.*s%s", espaciosBlancos, espaciosBlancos," ", line_csr_aux
	CALL post:MISCwrite_file_CCT(link_num,line_csr_aux)
    
	CALL post:MISCwrite_file_CCT(link_num," ")
	
	l_oper_CCT = post:operador_CCT
	CALL post:MISCFtrim(l_oper_CCT)    
	IF (l_oper_CCT != "TODOS") THEN
		//si el filtro no son todos los operadores, le mueve el id del operador seleccionado.
		FPRINT l_oper_CCT "%-5.5s" post:oper_id_CCT
		CALL post:MISCFtrim(l_oper_CCT)
	END IF

  FPRINT line_csr_aux,"OPERADOR:%-5.5s", op:op_operator_id
  FPRINT cadena,"SUCURSAL: %5.5s - %-35.35s",op:op_branch_id,admin:adt_br_bank_name
  espaciosBlancos = l_espacios - line_csr_aux.NUMCHARS - cadena.NUMCHARS
  FPRINT line_csr_aux "%s%*.*s%s", cadena, espaciosBlancos, espaciosBlancos," ",line_csr_aux
  CALL post:MISCwrite_file_CCT(link_num,line_csr_aux)

  CALL post:MISCwrite_file_CCT(link_num," ")

	//Filtros reporte a demanda
	IF (tes:list_cobro_ct == "") THEN //se instancia con valor "X" en los cierres y vacio a demanda.
		FPRINT line_csr_aux,"OPERADOR: %-5.5s         FECHA: %-10.10e - %-10.10e        T.REND: %-18.18s", \
			l_oper_CCT,post:fechad_CCT,post:fechah_CCT,post:tipo_rend_CCT        
		CALL post:MISCwrite_file_CCT(link_num,line_csr_aux)
		FPRINT line_csr_aux,"COMITENTE: %-50.50s",\
		post:comitente_CCT   
		CALL post:MISCwrite_file_CCT(link_num,line_csr_aux)       
	END IF
    
	//TITULOS DETALLE REPORTE
	split = "------------------------------------------------------------------------------------------------"
	line_csr_aux = split
	CALL post:MISCwrite_file_CCT(link_num,line_csr_aux)
	line_csr_aux = "ID DE CUPON                                            PIEZA             IMPORTE   CDD  OPERADOR"
	CALL post:MISCwrite_file_CCT(link_num,line_csr_aux)
	line_csr_aux = split
	CALL post:MISCwrite_file_CCT(link_num,line_csr_aux)
	
END SUB 

/*******/
