/****o* Pendientes de agrupación/OFB:post:MISCvpiGralResponse
* NOMBRE
*   OFB:post:MISCvpiGralResponse
* DESCRIPCION
*   Obtiene la descripcion en funcion al codigo de respuesta del POS/VPI  de la tabla ResponseVPI
* ENTRADAS
*   p_vpi_origen: VPI => Respuesta del dispositivo POS
*                 HOST => Respuesta del HOST de las tarjeteras
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   post:MISCvpiClosePort,post:MISCvpiPurchase
* DOCUMENTACION
*   NI50525 POS Integrado
* SOURCE
*/
#include "postmisc.h"

SUB post:MISCvpiGralResponse(p_vpi_origen,p_vpi_response)
	LOCAL l_vpi_origen LIKE post:adt_rvpi_origen
	LOCAL l_vpi_response LIKE post:adt_rvpi_codigo
	LOCAL l_retval LIKE lib:err_retval
	LOCAL l_msge LIKE common:csr_string
	LOCAL l_msge_header LIKE common:csr_string
		
	l_vpi_origen = p_vpi_origen
	l_vpi_response = p_vpi_response

	CALL post:MISCFtrim(l_vpi_response)
	IF (l_vpi_response.NUMCHARS == 0) THEN
		LET post:adt_vpi_leyenda_off = "Error general en la operacion"
		LET l_vpi_response = "999"
		FPRINT l_msge,"%s",post:adt_vpi_leyenda_off
		FPRINT l_msge_header,"%s ERROR [%s]  - VERFIQUE MENSAJE DEL VISOR DEL POS",l_vpi_origen,l_vpi_response
		MSGBOX l_msge,0x00,l_msge_header
		
		LET post:adt_vpi_status = "ERR"
		FYIMSG drv:fyimsg_list,"Error al ejecutar comando en dispositivo POS, reintente..."	
		NAP 3000
		RETURN -2
	END IF
	
	CDS SELECT FROM DSC:post:ResponseVPI_tbl BECOMES DSC:post:ResponseVPI_ctx \
		WHERE post:adt_rvpi_origen == l_vpi_origen \
		&& post:adt_rvpi_codigo == l_vpi_response
		
	CDS FINDFIRST DSC:post:ResponseVPI_ctx NOLOCK
	LET l_retval = CDSRETVAL
	IF (l_retval == 0) THEN
		CDS EXTRACT DSC:post:ResponseVPI_ctx \
			post:adt_rvpi_info INTO post:adt_vpi_leyenda_off \
			post:adt_rvpi_status INTO post:adt_vpi_status
	END IF
	
	CDS ENDSELECT DSC:post:ResponseVPI_ctx
	
	IF (l_retval != 0) THEN
		IF (l_vpi_origen == "VPI") THEN
			post:adt_vpi_leyenda_off = "NO SE ENCONTRO CODIGO DE ERROR"
		ELSE
			CDS SELECT FROM DSC:post:ResponseVPI_tbl BECOMES DSC:post:ResponseVPI_ctx \
				WHERE post:adt_rvpi_origen == l_vpi_origen \
				&& post:adt_rvpi_codigo == "XX"
				
			CDS FINDFIRST DSC:post:ResponseVPI_ctx NOLOCK
			LET l_retval = CDSRETVAL
			IF (l_retval == 0) THEN
				CDS EXTRACT DSC:post:ResponseVPI_ctx \
					post:adt_rvpi_info INTO post:adt_vpi_leyenda_off
			ELSE
				post:adt_vpi_leyenda_off = "NO SE ENCONTRO CODIGO DE ERROR"
			END IF
			CDS ENDSELECT DSC:post:ResponseVPI_ctx		
		END IF 
	END IF
	
	//Si es un error o si NO esta habilitada la operacion OFFLINE, entonces lo tomo como un error
	IF (post:adt_vpi_status == "ERR" || (common:adt_vpi_Offline == "N" && post:adt_vpi_status == "OFF")) THEN
		FPRINT l_msge,"%s",post:adt_vpi_leyenda_off
		FPRINT l_msge_header,"%s ERROR [%s] - VERFIQUE MENSAJE DEL VISOR DEL POS",l_vpi_origen,l_vpi_response
		MSGBOX l_msge,0x00,l_msge_header		
		
		IF (l_vpi_origen == "VPI") THEN
			FYIMSG drv:fyimsg_list,"Error al ejecutar comando en dispositivo POS, reintente..."	
		END IF
;	ELSEIF (post:adt_vpi_status == "OK" && l_vpi_origen == "VPI") THEN
;		FYIMSG drv:fyimsg_list,FLD:post:adt_vpi_leyenda_off	
		NAP 3000
	END IF
	
	RETURN l_retval
END SUB

/*******/
