/****o* Registro VAP/OFB:post:MISCwrite_file_vap
* NOMBRE
*   OFB:post:MISCwrite_file_vap
* DESCRIPCION
*   Graba linea del reporte de Piezas VAP
* ENTRADAS
*   link_num = canal de comunicacion del archivo
*		argument = linea a imprimir
*   split = linea a imprimir como separador
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*		post:MISClst_RepVAP
*		post:MISClst_RepVAP_Enc1
* DOCUMENTACION
*   NI50437
* SOURCE
*/
#include "postmisc.h"

SUB post:MISCwrite_file_vap (link_num,argument,split)
	LOCAL linea_aux       LIKE common:csr_string_aux
	LOCAL Lstring         LIKE common:csr_string_aux
	LOCAL si_encabezado   LIKE tes:es_tesoro
	LOCAL l_split         LIKE common:csr_string
	LOCAL l_argument      LIKE common:csr_string

	CLEARFIELD si_encabezado
	LET l_split = split
	LET l_argument = argument

	//gpb:12-08-2010 lo hago solo cuando mando a imprimir
	IF (siaf:pantalla_impreso != "P") THEN
		IF (siaf:lineas_cl == 64) THEN
		
			CALL CSR:file_putline(link_num,"<FORMFEED>")
			LET siaf:lineas_cl = 0
			
			/*se resguarda el campo porque sino lo blanquea en el encabezado*/
			LET linea_aux = l_argument
			
			IF (tes:INDsheet_header != 0) THEN
				CALL FLD:tes:INDsheet_header
				si_encabezado = "S" //hay encabezado
			END IF
			
			LET l_argument = linea_aux //recupero linea a imprimir
	
		END IF
	END IF
	
	Lstring = l_argument
	CALL csr:trimfield(Lstring)

	IF (si_encabezado == "S" && l_split == "S") THEN
			Lstring = ""
		CALL csr:trimfield(Lstring)
	END IF
	
	IF (Lstring.NUMCHARS > 0  || (Lstring.NUMCHARS == 0 && si_encabezado != "S" )) THEN
		//imprime la linea en blanco, si tiene valor o si es blanco,
		//sin ser la primera linea, luego del encabezado.
		CALL CSR:file_putline(link_num,l_argument)
	
		IF (RETVAL < 0) THEN
			FPRINT tes:string_aux,"Error al grabar el archivo %d",link_num
			ERRMSG LST:common:errmsg_list,FLD:tes:string_aux
			RETURN (-2)
		END IF    
		  
		siaf:lineas_cl = siaf:lineas_cl + 1
	END IF

END SUB

/*******/
