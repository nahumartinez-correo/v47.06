/****o* SiMon/OFB:post:PDOCrep_diario_PCT
* NOMBRE
*   OFB:post:PDOCrep_diario_PCT
* DESCRIPCION
*   Impresion del Reporte Diario PCT - Totales de Pago por Cuenta y Orden por Impresora.
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   post:TOTSrep_diario_PCT
* DOCUMENTACION
*   Documentación relevante que deberá ser actualizada por la modificación de la rutina
* SOURCE
*/
#include "postpdoc.h"

SUB post:PDOCrep_diario_PCT
    LOCAL Lstring           LIKE common:csr_string_200
    LOCAL cli_operativo     LIKE common:csr_string
    LOCAL cli_operativo_ant LIKE common:csr_string
    LOCAL Lcliente_ant      LIKE post:adt_benTmp_cli
    LOCAL Lcliente_desc_ant LIKE post:adt_benTmp_cli_desc
    LOCAL Loperativo_ant    LIKE post:adt_benTmp_operativo
    LOCAL Lcliente          LIKE post:adt_benTmp_cli
    LOCAL Lcliente_desc     LIKE post:adt_benTmp_cli_desc
    LOCAL Loperativo        LIKE post:adt_benTmp_operativo
    LOCAL Loper_pag         LIKE post:asign_falloBe_NomPag
    LOCAL Lcomprobante      LIKE post:adt_benTmp_benef
    LOCAL Limporte          LIKE siaf:importe
    LOCAL Lfecha_pago       LIKE common:system_date
    LOCAL Lcodigo_siaf      LIKE siaf:codigo_siaf
    LOCAL LOnSite           LIKE post:SiNo
    LOCAL l_post_horiz      LIKE post:etiq_ref_horiz

    LOCAL total_importe     LIKE siaf:importe
    LOCAL cant_cupon        LIKE siaf:i
    LOCAL total_cupon       LIKE siaf:i
    LOCAL total_general     LIKE siaf:importe
    LOCAL idx               LIKE siaf:i

    /* cgw: Impresion Reporte Diario PCT - Totales de Pago por Cuenta y Orden, por impresora. */
    siaf:indica_calcu = 0 // inicializa cant. de hojas.

    //limpia variables de control del corte de control.
    CLEARFIELD cli_operativo_ant //control de corte control.
    CLEARFIELD Lcliente_ant
    CLEARFIELD Lcliente_desc_ant
    CLEARFIELD Loperativo_ant

    //limpia totalizadores corte control.
    CLEARFIELD total_importe
    CLEARFIELD cant_cupon
    //limpia totalizadores generales.
    CLEARFIELD total_cupon
    CLEARFIELD total_general

    FYIMSG LST:common:fyimsg_list, "Imprimiendo Documento, un momento por favor..." 

    //Imprime Encabezado Principal de la hoja.
    CALL post:PDOCenc_RepDiarioPCT
    post:etiq_ref_horiz = 6 //ultima linea impresa.

    idx = 0x8000

    DO

        CLEARFIELD cli_operativo //control de corte control.
        CLEARFIELD Lcliente
        CLEARFIELD Lcliente_desc
        CLEARFIELD Loperativo
        CLEARFIELD Loper_pag
        CLEARFIELD Lcomprobante
        CLEARFIELD Limporte
        CLEARFIELD Lfecha_pago
        CLEARFIELD Lcodigo_siaf

        CALL csr:myPickcontrol("rep_diarioPCT",Lstring,idx)
        IF (RETVAL < 0) THEN
	        BREAK
    	END IF 

    	/*Extrae campos del picklist y los coloca en variables*/
        cli_operativo = SUBSTR(Lstring,41,31) //corte control
        Lcliente = SUBSTR(Lstring,41,10)
        Lcliente_desc = SUBSTR(Lstring,0,40)
        Loperativo = SUBSTR(Lstring,52,20)
        Loper_pag = SUBSTR(Lstring,73,41)
        Lcomprobante = SUBSTR(Lstring,115,20)
        Lfecha_pago = SUBSTR(Lstring,136,8)
        Limporte = SUBSTR(Lstring,145,16)
        Lcodigo_siaf = SUBSTR(Lstring,162,5)
        CALL csr:trimfield Lcomprobante

        IF (cli_operativo != cli_operativo_ant) THEN

            //Corte control Cliente - Operativo.            
            IF (cli_operativo_ant.numchars > 0) THEN
                //Imprime totales del corte control Cliente - Operativo.
                //Imprime los codigos de barras.
                CALL post:PDOCtot_cctrlRepDPCT(Lcliente_ant, Lcliente_desc_ant, Loperativo_ant, cant_cupon, total_importe)
                l_post_horiz = post:etiq_ref_horiz
            END IF

            //Reserva variables de control de corte operativo.
            cli_operativo_ant = cli_operativo
            Lcliente_ant = Lcliente
            Lcliente_desc_ant = Lcliente_desc
            Loperativo_ant = Loperativo
        	
            //Imprime Titulos.
            CALL post:PDOCtit_RepDiarioPCT(Lcliente, Lcliente_desc, Loperativo)
            l_post_horiz = post:etiq_ref_horiz
            
            //limpia totalizadores corte control.
            CLEARFIELD total_importe
            CLEARFIELD cant_cupon
        END IF

        IF (post:TipoRepPCT == "D") THEN
            //Reporte Detallado - Imprime lineas de detalle.

            l_post_horiz = post:etiq_ref_horiz
            post:etiq_ref_horiz = l_post_horiz + 1 //imprimira 1 linea de detalle
            CALL post:PDOChojas_tot_RepPCT //Controla si hay un salto de pagina.
            IF (post:etiq_ref_horiz == 6) THEN
                //Hubo salto de pagina y se imprimio encabezado.
                //Imprime Titulos.
                l_post_horiz = post:etiq_ref_horiz
                CALL post:PDOCtit_RepDiarioPCT(Lcliente, Lcliente_desc, Loperativo)
                l_post_horiz = post:etiq_ref_horiz
            END IF

            l_post_horiz = l_post_horiz + 1
            PRT STYLE 0
            PRT TEXTFONT 8
            PRT TEXTFONTNAME "Courier New"
            PRT TEXTSIZE *95
            PRT POSITIONABS l_post_horiz,7
            CLEARFIELD LOnSite
            IF (Lcodigo_siaf == 6630) THEN
                //OnSite
                LOnSite = "S"
            END IF
            CLEARFIELD common:csr_string
            FPRINT common:csr_string "%-41.41s %20.20s %16.16e %10.10e   %s", \
                   Loper_pag, Lcomprobante, Limporte, Lfecha_pago, LOnSite
            PRT PRINT EDIT common:csr_string
	        PRT CARRIAGERETURN
	        post:etiq_ref_horiz = l_post_horiz //ultima linea impresa.
        END IF

        //Contadores de Totales por corte control.
        total_importe = total_importe + Limporte
        cant_cupon = cant_cupon + 1
        //Contadores de Totales Generales.
        total_general = total_general + Limporte
        total_cupon = total_cupon + 1

        idx = idx + 1
    LOOP

    //Imprime totales del corte control Cliente - Operativo.
    //Imprime los codigos de barras.
    CALL post:PDOCtot_cctrlRepDPCT(Lcliente_ant, Lcliente_desc_ant, Loperativo_ant, cant_cupon, total_importe)
    l_post_horiz = post:etiq_ref_horiz
    
    //Imprime Totales Generales.
    CALL post:PDOCtot_gralRepDPCT(Lcliente_ant, Lcliente_desc_ant, Loperativo_ant, total_cupon, total_general)

END SUB

/*******/
