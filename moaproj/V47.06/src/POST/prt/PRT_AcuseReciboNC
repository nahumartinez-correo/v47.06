/****o* Notas de Credito CM/OFB:post:PRT_AcuseReciboNC
* NOMBRE
*   OFB:post:PRT_AcuseReciboNC
* DESCRIPCION
*   Esta rutina confecciona el Acuse de Recibo que se emite cuando la sucursal 
*   realiza una Nota de Crédito a Clientes Medios
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   OFB:post:DOCSacuse_reciboNC
* DOCUMENTACION
*    http://jira.sibs.net/jira/browse/AUT-354
* SOURCE
*/
#include "postprt.h"

SUB post:PRT_AcuseReciboNC
	LOCAL formulario LIKE adt_fact_anula
	LOCAL lfact_nro LIKE fact_AcuseRecib
	LOCAL espaciosBlancos  LIKE siaf:i
	LOCAL loc_oc LIKE adt_oc_desc
	LOCAL loc_importe LIKE adt_oc_desc
	
	/*Obtengo el dato la factura que anulo*/
	CDS SELECT FROM DSC:TABPresuDeta BECOMES DSC:CONPresuDeta2 \
		WHERE post:fecha == FLD:common:system_date && \
		post:op_operator_id == FLD:op:op_operator_id && \
		post:PresNro == post:PresNro && \
		siaf:codigo_siaf == 99510
	
	CDS FINDFIRST DSC:CONPresuDeta2 NOLOCK
	IF (CDSRETVAL == 0) THEN
		CDS EXTRACT DSC:CONPresuDeta2 \
			post:fact_nro \
			post:adt_oc_desc
		
		//le saco los ceros de adelante.
		lfact_nro = SUBSTR(post:fact_nro,3,12)
	END IF
	CDS ENDSELECT DSC:CONPresuDeta2

	fact_AcuseRecib = NroFact_Inic
	
	CALL post:PRTbodyAcuseRecNC 
	PRT TEXTSIZE *110
	PRT POSITIONABS *2960 *360  

	SELECT (Tipo_Formulario)
		CASE "1"
			formulario = "A"
		BREAK
		CASE "2"
			formulario = "B"
		BREAK
	END SELECT

	/* Antes de armar la línea centro los datos de la OC y el importe*/
	FPRINT loc_oc adt_oc_desc
	CALL csr:trimfield(loc_oc)
	espaciosBlancos = (29 - loc_oc.NUMCHARS) / 2
	FPRINT loc_oc "%*.*s%s", espaciosBlancos, espaciosBlancos," ", loc_oc

	FPRINT loc_importe "%-12.12s" importe_sesion
	CALL csr:trimfield(loc_importe)
	espaciosBlancos = (12 - loc_importe.NUMCHARS) / 2
	FPRINT loc_importe "%*.*s%s", espaciosBlancos, espaciosBlancos," ", loc_importe
	
	FPRINT common:csr_string "%1.1s %13.13e %-29.29s %1.1s %13.13e %10.10e   %-14.14s",formulario,fact_AcuseRecib,loc_oc,formulario,lfact_nro,common:system_date,loc_importe
	
	PRT TEXTFONTNAME "Courier New"
	PRT TEXTSIZE *110
	
  //gpb:ART-252	
	PRT STYLE 0
	
	PRT PRINT EDIT common:csr_string
	PRT NEWLINE
	PRT POSITIONABS - *600 

END SUB

/*******/
