/****o* Clientes Medios/OFB:post:CDSCalcTotVentasOC
* NOMBRE
*   OFB:post:CDSCalcTotVentasOC
* DESCRIPCION
*   Esta rutina acumula los vectores con iva, sin iva, sin iva 11 decimales y sin iva de las tx
*   a las que se les aplica el descuento. Estos vectores se grabaran luego en la TX 99350.
*   El vector docfis_imp_siva_tx(acumulado tx sin iva) se resta del docfis_imp_siva(acumulado sin
*   iva del descuento) y se compara contra los importes de la AuxIva para ver si hay que ajustar
*   diferencias en el dcto para que me quede bien el journal.
* ENTRADAS
*   ivaCM:alicuota del producto al cual se le aplica el dcto OC
*   importeCM:importe del descuento aplicado al producto
*   familia_aux:familia del producto
*   l_sin_iva:importe sin iva del producto
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   OFB:post:CDSCalcTotVentasBA,OFB:post:CDSCalcTotVentasOPC
* REFERENCIADO POR
*   OFB:post:MISCDcto_OC
* DOCUMENTACION
*   Documentación relevante que deberá ser actualizada por la modificación de la rutina
* SOURCE
*/
#include "postcds.h"

SUB post:CDSCalcTotVentasOC(ivaCM,importeCM,familia_aux,l_sin_iva)
local familia like siaf:adt_tran_fliatran
local imp_calc like siaf:ImporteSinIva
local suma_imp_fijo like common:csr_string
local ind like siaf:i
local entro like common:csr_string

	if (!ConvenioSesionV && post:adt_oc_nro.numchars == 0) then
		 //Borro vectores
    		call csr:cleararray(docfis_importe)
		call csr:cleararray(docfis_tasa_por)
		call csr:cleararray(docfis_imp_siva)
		call csr:cleararray(docfis_imp_siva4)
		call csr:cleararray(docfis_imp_siva_tx)
		
	endif
	
	IF (familia_aux == "CST" || familia_aux == "CME") then
		//SC 50170
		CALL MISCdesglosaIva(importeCM,ivaCM,post:cantidad,0)		
	ELSE
	//SC 50170
		CALL MISCdesglosaIva(importeCM,ivaCM,1,0)		
	ENDIF
	
	/*Acumulo en los vectores el total por alicuota*/
	ind = 0
	entro = "N"
	while (ind < 20 && docfis_tasa_por[ind].NUMCHARS != 0)
		if (docfis_tasa_por[ind] == ivaCM)
			docfis_importe[ind] = docfis_importe[ind] + importeCM
			docfis_imp_siva[ind] = docfis_imp_siva[ind] + ImporteSinIva
			docfis_imp_siva4[ind] = docfis_imp_siva4[ind] + ImporteSinIva_bis
			docfis_imp_siva_tx[ind] = docfis_imp_siva_tx[ind] + l_sin_iva
			entro = "S"
			break
		end if
		ind = ind + 1
	end while
	if (entro == "N")
		docfis_tasa_por[ind] = ivaCM
		docfis_importe[ind] = importeCM
		docfis_imp_siva[ind] = ImporteSinIva
		docfis_imp_siva4[ind] = ImporteSinIva_bis
		docfis_imp_siva_tx[ind] = l_sin_iva
	end if
		
END SUB

/*******/
