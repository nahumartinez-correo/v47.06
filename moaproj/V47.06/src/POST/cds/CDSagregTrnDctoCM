/****o* Clientes Medios/OFB:post:CDSagregTrnDctoCM
* NOMBRE
*   OFB:post:CDSagregTrnDctoCM
* DESCRIPCION
*   Agrega, modifica o borra el descuento de cliente medio sobre el servicio, bien de cambio.
* ENTRADAS
*   codigo =  codigo de la transaccion sobre la que se le aplica el descuento.
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   post:CDSagregTrnDctoOpcCM
* REFERENCIADO POR
*   OFB:post:MISCAct_DctoSesionCM,OFB:post:MISCagregTrnDctoCM,OFB:post:MISCRemito_Item,OFB:post:SVALDcto_Sesion
* DOCUMENTACION
*   http://jira.sibs.net/jira/browse/AUT-2355
* SOURCE
*/
#include "postcds.h"

SUB post:CDSagregTrnDctoCM(codigo)


/*MFM - Esta rutina graba, modifica y borra la transacción
 de descuento a cliente medio (99300) en la PresuDet.  */

	if (!post:OperacionCob && !post:OperacionAut) then
		If (codigo != 8500) Then
			CDS SELECT FROM DSC:TABPresuDeta BECOMES DSC:CONPresuDeta2 \
			WHERE post:fecha    == FLD:common:system_date && \
			post:op_operator_id == FLD:op:op_operator_id && \
			post:PresNro        == post:PresNro && \
			PreDNroTrnAsoc      == PreDNroTrnAsoc && \
			siaf:codigo_siaf    == 99300
		Else
			CDS SELECT FROM DSC:TABPresuDeta BECOMES DSC:CONPresuDeta2 \
			WHERE post:fecha    == FLD:common:system_date && \
			post:op_operator_id == FLD:op:op_operator_id && \
			post:PresNro        == post:PresNro && \
			PreDNroTrn          == PreDNroTrnCM && \
			PreDNroTrnAsoc      == PreDNroTrnAsoc && \
			siaf:codigo_siaf    == 99300 
		End If
	else
		If (codigo != 8500) Then
			CDS SELECT FROM DSC:TABPresuDeta BECOMES DSC:CONPresuDeta2 \
			WHERE post:fecha    == FLD:common:system_date && \
			post:op_operator_id == FLD:op:op_operator_id && \
			post:PresNro        == post:PresNro && \
			PreDNroTrnAsoc      == PreDNroTrnAsoc && \
			siaf:codigo_siaf    == 99300 && \
			post:remito_nro == post:remito_nro
		Else
			CDS SELECT FROM DSC:TABPresuDeta BECOMES DSC:CONPresuDeta2 \
			WHERE post:fecha    == FLD:common:system_date && \
			post:op_operator_id == FLD:op:op_operator_id && \
			post:PresNro        == post:PresNro && \
			PreDNroTrn          == PreDNroTrnCM && \
			PreDNroTrnAsoc      == PreDNroTrnAsoc && \
			siaf:codigo_siaf    == 99300 && \
			post:remito_nro == post:remito_nro			
		End If
	end if
		       
	CDS FINDFIRST DSC:CONPresuDeta2 LOCK
	lib:err_retval = CDSRETVAL
   
	IF (lib:err_retval != 0) THEN	
		if ((Dcto_CliMed > 0) || (codigo == 40002 && post:DctoCM_bc > 0)) Then
			call CDSIncSecSesion(PresNro,"TRN",1, enAnulacion)
			PreDNroTrn = retval
			siaf:codigo_siaf        = 99300
			siaf:adt_tran_fliatran  = "PCL"
			siaf:adt_tran_afec_efvo = "-"
			LET tlr:INDtran_name    = LST:post:TRANDescuentoCM
			PreDEst                 = 1
			PrePEst                 = 1
			siaf:adt_tran_jer_pres  = 7
			siaf:moneda = 99
			//post:AdmiteMP = "S"
			call post:MISCRestFlia

			
			CDS CLEARBUFFER DSC:CONPresuDeta2
			CDS APPEND DSC:CONPresuDeta2 post:fecha post:op_operator_id \
			post:PresNro siaf:adt_tran_jer_pres post:PreDNroTrn \
			tlr:INDtran_name post:PreDEst \
			siaf:codigo_siaf \
			post:PreDForFis Imp_DctoCliMed AS siaf:importe siaf:adt_tran_afec_efvo \
			siaf:adt_tran_fliatran post:PrePEst \
			post:PresIngTas post:PresIngGra post:PresIngExe
			CDS APPEND DSC:CONPresuDeta2 siaf:moneda post:ImporteSinIva as siaf:ImporteSinIva \
			post:ImporteConIva as siaf:ImporteConIva
			/*Agrego el campo tarifa (tarifa basica del por producto), el descuento aplicado (por prod o sesion) y
			el campo TotalSesionCM (total bruto de la sesion CM)*/
			CDS APPEND DSC:CONPresuDeta2 post:tarifa post:TotalSesionCM \
			prod_DescuentoCM  PreDNroTrnAsoc post:bonificaCM post:AliTrnCM 	post:AdmiteMP
			//NI 50324
			if (codigo == 40002 && post:DctoCM_bc > 0) then
				post:Dcto_CliMed = post:DctoCM_bc
			 	CDS APPEND DSC:CONPresuDeta2 post:DctoCM_bc
			endif
			CDS APPEND DSC:CONPresuDeta2 post:Dcto_CliMed
			/*Agrego vectores*/
			CDS APPEND DSC:CONPresuDeta2 \
			array docfis_tasa_por \
			array docfis_importe \
			array docfis_imp_siva \
			array docfis_imp_siva4
			
			/* MLD Por Cobranza CM agrego el remito que lo generó */
			CDS APPEND DSC:CONPresuDeta2 \
			remito_nro
			/* MLD Por Cobranza CM agrego si el descto es por sesion en un auxiliar*/
			CDS APPEND DSC:CONPresuDeta2 \
			post:PorSesion
			//A pedido de Horacio Araujo, se journaliza el dcto local anterior.
			CDS APPEND DSC:CONPresuDeta2 \
			post:Dcto_CliMed_Ant
			
			CDS INSERT DSC:CONPresuDeta2 LOCK   
			//PS Cliente Medio + Bienes de Cambio
			if (codigo == 8500 && post:cod_agrupacion.numchars != 0) then
				post:PreDNroTrnCM = post:PreDNroTrn 
			endif
		else
			//servirá para estado presu = 3
			CDS UNLOCK DSC:post:CONPresuDeta2
			CDS ENDSELECT DSC:post:CONPresuDeta2
			return -2
		end if
	ELSE
		//cds extract DSC:CONPresuDeta2 post:AliTrnCM siaf:ImporteSinIva siaf:ImporteConIva
		//SC 50170
		//solo borro el iva si no estoy modificando pues ya lo hice al ingresar a modificar
		//la TX correspondiente.
		if (estado_presu != 3) then
			cds extract DSC:CONPresuDeta2 post:AliTrnCM siaf:ImporteConIva
			CALL MISCdesglosaIva(siaf:ImporteConIva, post:AliTrnCM, 1,0)
			call MISCborrarIva
		endif
		if ((Dcto_CliMed > 0) || (codigo == 40002 && post:DctoCM_bc > 0)) Then
			CDS CLEARBUFFER DSC:CONPresuDeta2
			CDS APPEND DSC:CONPresuDeta2 Imp_DctoCliMed AS siaf:importe post:tarifa post:TotalSesionCM \
			post:ImporteSinIva as siaf:ImporteSinIva post:ImporteConIva as siaf:ImporteConIva
			
			//NI 50324
			if (codigo == 40002 && post:DctoCM_bc > 0) then
				post:Dcto_CliMed = post:DctoCM_bc
			 	CDS APPEND DSC:CONPresuDeta2 post:DctoCM_bc
			endif
			CDS APPEND DSC:CONPresuDeta2 post:Dcto_CliMed
			
			//PS Bienes de Cambio
			if (codigo == 8500 && post:cod_agrupacion.numchars != 0) then
				CALL MISCdesglosaIva(post:Imp_DctoCliMed, tes:adt_espv_aliciva, 1,0)
				CDS APPEND DSC:CONPresuDeta2 post:prod_DescuentoCM tes:adt_espv_aliciva AS post:AliTrnCM 
			else
				cds extract DSC:CONPresuDeta2 post:AliTrnCM
				CALL MISCdesglosaIva(post:Imp_DctoCliMed, post:AliTrnCM, 1,0)
			end if
			/*Agrego vectores*/
			CDS APPEND DSC:CONPresuDeta2 \
			array docfis_tasa_por \
			array docfis_importe \
			array docfis_imp_siva \
			array docfis_imp_siva4

			CDS UPDATE DSC:CONPresuDeta2 LOCK
		else
			CDS DELETE DSC:CONPresuDeta2 
			CDS UNLOCK DSC:CONPresuDeta2
			CDS ENDSELECT DSC:CONPresuDeta2
			return (-2)
		end if
	END IF
	CDS UNLOCK DSC:CONPresuDeta2
	CDS ENDSELECT DSC:CONPresuDeta2
	
END SUB

/*******/
