/****o* Expedición/OFB:post:CDSBuscaGpoCteOp
* NOMBRE
*   OFB:post:CDSBuscaGpoCteOp
* DESCRIPCION
*   Esta rutina muestra los grupo de corte + apertura disponibles para el cierre de contenedor.
*   Cuando el operador selecciona/deselecciona un grupo de corte apertura la rutina lo marca >> / desmarca  
*   segun corresponda en la tabla GpoApTmp
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   OFB:post:PBgrupoCorteOp_Cont
* DOCUMENTACION
*    http://jira.sibs.net/jira/browse/AUT-351
* SOURCE
*/
#include "postcds.h"

SUB post:CDSBuscaGpoCteOp

	/*CREADA POR MFM - SEPTIEMBRE 2000*/
	
	CDS SELECT FROM DSC:GpoApTmp_table BECOMES DSC:GpoApTmp_ctx \
	WHERE CodOperador == op:op_operator_id
	let siaf:last_key = LASTKEY
    CDS MULTIDISPLAY DSC:post:GpoApTmp_ctx LISTgrupoTmp
    LET lib:err_retval = CDSRETVAL

    IF (CDSRETVAL == 0) THEN /* Esc */
		CDS ENDSELECT DSC:post:GpoApTmp_ctx
		RETURN (-2)
    ENDIF
    CALL lib:MISCcds_find_err
    IF (RETVAL != 0)
		CDS ENDSELECT DSC:post:GpoApTmp_ctx
		RETURN (-1)
    ENDIF
   
    CDS EXTRACT DSC:post:GpoApTmp_ctx ALL
    
    IF (post:MarcaIncluye == "") THEN
    	post:MarcaIncluye = ">>"
    ELSE
    	clearfield post:MarcaIncluye
    ENDIF
    
    CDS ENDSELECT DSC:post:GpoApTmp_ctx
    
    //MARCO EL REGISTRO SELECCIONADO
    CDS SELECT FROM DSC:GpoApTmp_table BECOMES DSC:GpoApTmp_ctx \
	WHERE CodOperador == op:op_operator_id && \
	post:adt_grucorte_cod == post:adt_grucorte_cod && \
	post:adt_grucorte_desc == post:adt_grucorte_desc && \
	post:adt_apertura_cod == post:adt_apertura_cod
    
    CDS FINDFIRST DSC:post:GpoApTmp_ctx LOCK
   
    CDS CLEARBUFFER DSC:post:GpoApTmp_ctx
    
    CDS APPEND DSC:post:GpoApTmp_ctx post:MarcaIncluye
    CDS UPDATE DSC:post:GpoApTmp_ctx LOCK
    CDS UNLOCK DSC:post:GpoApTmp_ctx
    CDS ENDSELECT DSC:post:GpoApTmp_ctx
    
    clearfield post:grupo_apertura

    MOVE post:adt_grucorte_cod TO SUBSTR(post:grupo_apertura,0,2)
    MOVE post:adt_grucorte_desc TO SUBSTR(post:grupo_apertura,3,20)
    MOVE post:adt_apertura_desc TO SUBSTR(post:grupo_apertura,24,20)
    MOVE post:MarcaIncluye TO SUBSTR(post:grupo_apertura,45,2)
    
    refresh post:grupo_apertura
    
END SUB	

/*******/
