/****o* Pago en Destino/OFB:post:CDSupd_NC_FC_ped
* NOMBRE
*   OFB:post:CDSupd_NC_FC_ped
* DESCRIPCION
*   Actualiza el estado local de la pieza pendiente en la sucursal
* ENTRADAS
*   Actualiza en la NCpedENOrigen los datos necesarios para 
*	generar el reporte de Notas de Credito y Refacturaciones ped
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*  	OFB:post:SVALimpre_fiscal_ped
* DOCUMENTACION
*  	NI50479
* SOURCE
*/
#include "postCDS.h"

SUB post:CDSupd_NC_FC_ped(estado)
	LOCAL l_retval LIKE lib:err_retval
	LOCAL l_letraComp LIKE letra_comp_ped

	CDS SELECT FROM DSC:NCpedEnOrigen_tbl BECOMES DSC:NCpedEnOrigen_ctx WHERE \
		post:pieza_ped == post:pieza_ped 		
	CDS FINDFIRST DSC:NCpedEnOrigen_ctx LOCK
	l_retval = CDSRETVAL	
	IF (l_retval == 0) THEN
		CDS CLEARBUFFER DSC:NCpedEnOrigen_ctx
		IF (substr(DocInic,0,1) == 1)
			l_letraComp = "A"
		ELSE 
			IF (substr(DocInic,0,1) == 2)
				l_letraComp = "B"
			END IF
		END IF
		clearfield post:pzaPed_regul
		IF (estado == "NTCR")
			fec_act_ncred = common:system_date
			NroCompNCRED = NroFact_Inic
			LetraCompNCRED = l_letraComp
			clearfield fec_act_factu
			clearfield NroCompFACTU
			clearfield LetraCompFACTU
			
			CDS APPEND DSC:NCpedEnOrigen_ctx \
				post:fec_act_ncred \
				post:fec_act_factu \
				post:NroCompNCRED \
				post:NroCompFACTU \
				post:LetraCompNCRED \
				post:LetraCompFACTU \
				post:pzaPed_regul \
				post:fact_sinConsumoCM
		ELSE
			IF (ConsumoAnticipo || modalidad_CM != "S")
 				fact_sinConsumoCM = "N"
 			ELSE
 				fact_sinConsumoCM = "S"
 			END IF
		
			fec_act_factu = common:system_date
			NroCompFACTU = NroFact_Inic
			IF (post:modalidad_CM == "C") THEN
				LetraCompFACTU = "X"
			ELSE 
				LetraCompFACTU = l_letraComp
			END IF
			CDS APPEND DSC:NCpedEnOrigen_ctx \
				post:fec_act_factu \
				post:NroCompFACTU \
				post:LetraCompFACTU \
				post:NroCompImpos \
				post:importe_sesion AS post:ImporteFACTU \
				post:pzaPed_regul \
				post:fact_sinConsumoCM
		END IF
		CDS UPDATE DSC:NCpedEnOrigen_ctx LOCK
		l_retval = CDSRETVAL
	END IF
	CDS UNLOCK NCpedEnOrigen_ctx
	CDS ENDSELECT DSC:NCpedEnOrigen_ctx
	RETURN l_retval
END SUB

/*******/
