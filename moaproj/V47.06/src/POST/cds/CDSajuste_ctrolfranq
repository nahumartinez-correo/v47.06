/****o* Franqueadoras/OFB:post:CDSajuste_ctrolfranq
* NOMBRE
*   OFB:post:CDSajuste_ctrolfranq
* DESCRIPCION
*   Rutina que inserta un registro en la control_franq cuando se realiza un ajuste de contadores
*	de franqueadora
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   RETURN -2: Falta cargar parametria de Datos Generales de la Sucursal
*   RETURN -2: No decida continuar, cuando está a punto de generar una falla de caja que supera el importe.
*   RETURN -2: Secuencia ya existente para este Codigo
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   OFB:post:SENDajuste_contfranq 
* DOCUMENTACION
*   Documentación relevante que deberá ser actualizada por la modificación de la rutina
* SOURCE
*/
#include "postcds.h"

/* Ajuste de contadores en control_franq */
SUB post:CDSajuste_ctrolfranq

	CALL post:CDSimp_max_ajuste(post:franq_cont_fin_aju,franq_cont_inic_aju)
	IF (retval < 0 )
		MSGBOX "Falta cargar parametria de Datos Generales de la Sucursal",0x00,"Error"
		return -2
	ELSE
		IF (retval == 0 )
			//Es mayor (0x00000134 es un msgbox YESNO2 pero con signo de exclamacion)
			FPRINT common:csr_string,"%s%e%s","Está a punto de generar una falla de caja que supera los $",giros:adt_dgralsuc_maxFrq,". Desea continuar de todos modos?"
			MSGBOX common:csr_string,0x00000134,"ATENCION"
			IF (RETVAL == OFB_IDNO) THEN
				return -2
			END IF
		END IF
	END IF

   
    post:franq_codigo = post:adt_franq_codigo
    call post:CDSobtiene_secuen("FRANQ")  //busco el nro.secuencia en CodSeq
	post:franq_seq = post:adt_codseq_nro
    post:franq_fecha = common:system_date
    post:franq_operador = op:op_operator_id
    post:franq_PresNro = 0
    post:franq_NroTrn = 0
    post:franq_cont_inic = post:franq_cont_inic_aju
    post:franq_cont_fin = post:franq_cont_fin_aju
    
    IF (post:franq_cont_inic.numchars == 0) THEN
    	post:franq_cont_inic = 0.00 //para asegurarse que no tenga valor nulo
    END IF
    IF (TESTFLAG (conting_franq, Conting_franq)) THEN
	    post:franq_marca_fallo = "C" //marca contingencia en el ajuste
    ELSE
	    post:franq_marca_fallo = "X" //marca fallo en el ajuste
   END IF
   
   // sba 20030430 - Agrego fecha de Recepcion de ajuste
   clearfield post:franq_fecha_recep
	
    CDS SELECT FROM DSC:control_franq_tbl BECOMES DSC:control_franq_ctx WHERE  \
    post:franq_codigo == FLD:post:adt_franq_codigo && \
    post:franq_fecha == common:system_date && \
    post:franq_seq == FLD:post:franq_seq

    CDS FINDFIRST DSC:control_franq_ctx LOCK 
    lib:err_retval = CDSRETVAL
    
    CDS CLEARBUFFER DSC:control_franq_ctx
        
    CDS APPEND DSC:control_franq_ctx \
    FLD:post:franq_codigo \
    FLD:post:franq_seq \
    FLD:post:franq_fecha \
    FLD:post:franq_operador \
    FLD:post:franq_PresNro \
    FLD:post:franq_NroTrn \
    FLD:post:franq_cont_inic \
    FLD:post:franq_cont_fin \
    FLD:post:franq_marca_fallo \
    FLD:post:franq_fecha_recep
    
    // FNR: NI50276 grabación de hora en control_frnaq
    MOVE common:time_of_day TO FLD:post:franq_hora_fallo
    CDS APPEND DSC:control_franq_ctx \
    FLD:post:franq_hora_fallo
    
    IF (lib:err_retval == -801) THEN
    	CDS INSERT DSC:control_franq_ctx LOCK    	
    ELSE
    	msgbox "Secuencia ya existente para este Codigo",OFB_OK,"ERROR"
    	CDS UNLOCK DSC:control_franq_ctx
        CDS ENDSELECT DSC:control_franq_ctx
    	RETURN (-2)
    END IF
            
    CDS UNLOCK DSC:control_franq_ctx
    CDS ENDSELECT DSC:control_franq_ctx
END SUB

/*******/
