/****o* Reportes Gestión Stock/OFB:post:CDSrecupero_Rtte
* NOMBRE
*   OFB:post:CDSrecupero_Rtte
* DESCRIPCION
*   Recupera los datos del rtte de la tabla CliOca si el cliente es minorista y de la CliMay_crem
*   si el cliente es mayorista.
* ENTRADAS
*   es_may  = nro cuenta cliente mayorista, si es vacio entonces el cliente es minorista.
*   tipo_doc = tipo de documento del rtte.
*   nro_doc = nro de documento del rtte.
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   post:CDSsave_GStock
* DOCUMENTACION
*   SC50190:http://livelink.intranet.siemens-itron.com.ar/livelink/livelink.exe?func=doc.ViewDoc&nodeId=639223&
* SOURCE
*/
#include "postcds.h"

SUB post:CDSrecupero_Rtte(es_may,tipo_doc,nro_doc)
local mayorista like post:adt_CDD_CtaOM
local l_localidad like post:CloLoc

    mayorista = es_may
    
    CLEARFIELD post:CloProvin
    
    IF (mayorista.numchars == 0) THEN //CLIENTE MINORISTA (CliOca)
       CDS SELECT FROM "CliOca" BECOMES DSC:clioca_ctx \
       WHERE  post:CloTipDoc == tipo_doc && \
              post:CloCuit == nro_doc
       
       CDS FINDFIRST DSC:clioca_ctx NOLOCK
       IF (CDSRETVAL == 0) THEN
       		
       		clearfield CloPersonaJuridica
       		clearfield CloRazon
       		clearfield CloApellido
       		clearfield CloNombre
       		clearfield CloCalle
       		clearfield CloNroDir 
       		clearfield CloPiso
       		clearfield CloLoc
       		clearfield CloCodPost
       		clearfield CloProvin
       		
       		clearfield adt_stockp_rem_loc
       		clearfield adt_stockp_rem_nom
       		clearfield adt_stockp_rem_domic
       		
       		CDS EXTRACT DSC:clioca_ctx \
       		CloPersonaJuridica \
       		CloRazon CloApellido CloNombre \
       		CloCalle CloNroDir CloPiso \
       		CloLoc  \
       		CloCodPost into adt_stockp_rem_CP \
       		CloProvin
       		
       		//PS 9928
       		fprint adt_stockp_rem_loc "%s" CloLoc
       		
       		IF (CloPersonaJuridica == "S") THEN
       			fprint adt_stockp_rem_nom "%s" CloRazon
       		ELSE
       			fprint adt_stockp_rem_nom "%s %s" CloNombre,CloApellido
       		ENDIF
       		
       		fprint adt_stockp_rem_domic "%s %s %s" CloCalle, CloNroDir, CloPiso
       	ENDIF
       	CDS ENDSELECT DSC:clioca_ctx
       
    ELSE //mayorista-CliMay
       CDS SELECT FROM "CliMay_crem" BECOMES "CliMay_ctx" \
       WHERE  post:adt_climay_tipodoc == tipo_doc && \
              post:adt_climay_nrodoc == nro_doc
       
       CDS FINDFIRST "CliMay_ctx" NOLOCK
       IF (CDSRETVAL == 0) THEN
       		clearfield adt_climay_razon
       		clearfield adt_climay_calle 
       		clearfield adt_climay_nropuerta
       		clearfield adt_climay_piso
       		clearfield adt_climay_loca
       		clearfield adt_climay_codpost
       		clearfield adt_pcia_cod
       		
       		clearfield adt_stockp_rem_loc
       		clearfield adt_stockp_rem_nom
       		clearfield adt_stockp_rem_domic
       		
       		CDS EXTRACT "CliMay_ctx" \
       		adt_climay_razon \
       		adt_climay_calle adt_climay_nropuerta adt_climay_piso \
       		adt_climay_loca  \
       		adt_climay_codpost into adt_stockp_rem_CP \
       		adt_pcia_cod into CloProvin
       		
       		//PS 9928
       		fprint adt_stockp_rem_loc "%s" adt_climay_loca
       		
       		fprint adt_stockp_rem_nom "%s" adt_climay_razon
       		fprint adt_stockp_rem_domic "%s %s %s" adt_climay_calle, adt_climay_nropuerta, adt_climay_piso
       	ENDIF
       	CDS ENDSELECT "CliMay_ctx"
       
    ENDIF
    
END SUB

/*******/
