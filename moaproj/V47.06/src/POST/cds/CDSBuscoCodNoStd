/****o* Cobro por Cuenta de Terceros/OFB:post:CDSBuscoCodNoStd
* NOMBRE
*   OFB:post:CDSBuscoCodNoStd
* DESCRIPCION
*   Busca en la tabla ParamCCT el código no standard y según los registros coincidentes
*   va mostrando los resultados para ir filtrando la selección
* ENTRADAS
*   LongCodBar: Longitud del código de barras ingresado
*	Seleccion : TODO, COMITENTE o SERVICIO
* SALIDAS
*	RETVAL = 0 : Un registro encontrado/seleccionado
*	RETVAL = 1 : Más de un registro encontrado (sólo para selección TODO)
*   RETVAL = -2: No se encontró ninguna coincidencia
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   post:MISCval_cb_CCT
* DOCUMENTACION
*   50361 - https://jirats.siemens.com.ar/jira/browse/AUT-1684
* SOURCE
*/
#include "postcds.h"

SUB post:CDSBuscoCodNoStd(LongCodBar,Seleccion)
LOCAL cant_reg like post:adt_cct_long_cb 

	/*Selecciono según parámetro recibido*/
	SELECT (Seleccion)
	CASE "TODO"
		CDS SELECT FROM DSC:ParamCCT_table BECOMES DSC:ParamCCT_ctx WHERE \
		post:adt_cct_cod_barra == "S" && \
		post:adt_cct_valid_std == "N" && \
		post:adt_cct_vig_inic <= fld:common:system_date && \
		(adt_cct_vig_fin >=  fld:common:system_date || adt_cct_vig_fin == "        ") && \
		post:adt_cct_long_cb == fld:LongCodBar SORT "primary"
		BREAK
	CASE "COMITENTE"
		CDS SELECT UNIQUE post:adt_cct_comit_desc,post:adt_cct_comitente FROM DSC:ParamCCT_table BECOMES DSC:ParamCCT_ctx WHERE \
		post:adt_cct_cod_barra == "S" && \
		post:adt_cct_valid_std == "N" && \
		post:adt_cct_vig_inic <= fld:common:system_date && \
		(adt_cct_vig_fin >=  fld:common:system_date || adt_cct_vig_fin == "        ") && \
		post:adt_cct_long_cb == fld:LongCodBar SORT "Comiten"
		BREAK
	CASE "SERVICIO"
		CDS SELECT UNIQUE post:adt_cct_desc_serv,post:adt_cct_cod_serv FROM DSC:ParamCCT_table BECOMES DSC:ParamCCT_ctx WHERE \
		post:adt_cct_cod_barra == "S" && \
		post:adt_cct_valid_std == "N" && \
		post:adt_cct_vig_inic <= fld:common:system_date && \
		(adt_cct_vig_fin >=  fld:common:system_date || adt_cct_vig_fin == "        ") && \
		post:adt_cct_long_cb == fld:LongCodBar && \
		post:adt_cct_comitente == post:adt_cct_comitente SORT "Servicio"
		BREAK
	END SELECT	
	CDS FINDFIRST DSC:ParamCCT_ctx NOLOCK
	IF (CDSRETVAL != -801) THEN
		call lib:MISCcds_find_err
	END IF
	IF (CDSRETVAL == 0) THEN
		/* Si encontró al menos uno, me fijo cuántos son*/
		cant_reg = 0
		CDS AGGREGATE DSC:ParamCCT_ctx COUNT post:adt_cct_long_cb AS cant_reg
		IF (CDSRETVAL == 0) THEN
			CDS EXTRACT DSC:ParamCCT_ctx cant_reg
			IF (cant_reg > 1) THEN
				/*Es más de uno, salvo para la seleccion todos, 
				muestro los registros*/
				SELECT (Seleccion)
				CASE "TODO"
					CDS ENDSELECT DSC:ParamCCT_ctx
					RETURN (1)
					BREAK
				CASE "COMITENTE"
					CDS MULTIDISPLAY DSC:ParamCCT_ctx LISTParamCCTComit
					BREAK
				CASE "SERVICIO"
					CDS MULTIDISPLAY DSC:ParamCCT_ctx LISTParamCCTServ
					BREAK
				END SELECT
				IF (CDSRETVAL > 0) THEN
					IF (Seleccion == "COMITENTE") THEN
						CDS EXTRACT DSC:ParamCCT_ctx ALL 
					ELSE
						CDS EXTRACT DSC:ParamCCT_ctx post:adt_cct_cod_serv
						call post:CDSSelCodNoStd("SERVICIO",LongCodBar)
						IF (RETVAL == -2) THEN
							CDS ENDSELECT DSC:ParamCCT_ctx
							RETURN (-2)
						END IF						
					END IF
					CDS ENDSELECT DSC:ParamCCT_ctx
					RETURN (0)
				ELSE
					CDS ENDSELECT DSC:ParamCCT_ctx
					RETURN (-2)
				END IF										
			ELSE
				CDS FINDFIRST DSC:ParamCCT_ctx NOLOCK
				call lib:MISCcds_find_err
				IF (RETVAL == 0) THEN
					SELECT (Seleccion)
					CASE "TODO"
						CDS EXTRACT DSC:ParamCCT_ctx all
						BREAK
					CASE "COMITENTE"
						CDS EXTRACT DSC:ParamCCT_ctx post:adt_cct_comitente
						call post:CDSSelCodNoStd("COMITENTE",LongCodBar)
						IF (RETVAL == -2) THEN
							CDS ENDSELECT DSC:ParamCCT_ctx
							RETURN (-2)
						END IF						
						BREAK
					CASE "SERVICIO"
						CDS EXTRACT DSC:ParamCCT_ctx post:adt_cct_cod_serv
						call post:CDSSelCodNoStd("SERVICIO",LongCodBar)
						IF (RETVAL == -2) THEN
							CDS ENDSELECT DSC:ParamCCT_ctx
							RETURN (-2)
						END IF						
						BREAK
					END SELECT
					CDS ENDSELECT DSC:ParamCCT_ctx
					RETURN (0)
				ELSE
					CDS ENDSELECT DSC:ParamCCT_ctx
					RETURN (-2)
				END IF
			END IF		
		ELSE
			MSGBOX "Fallo en la búsqueda de la tabla ParamCCT", OFB_OK, "Error"
			CDS ENDSELECT DSC:ParamCCT_ctx
			RETURN (-2)
		END IF
	ELSE
		CDS ENDSELECT DSC:ParamCCT_ctx
		RETURN (-2)
	END IF
END SUB

/*******/
