/****o* Pago en Destino/OFB:post:CDSload_docPED_data
* NOMBRE
*   OFB:post:CDSload_docPED_data
* DESCRIPCION
*   Limpia la tabla rep_doc_ped
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*  	OFB:post:MISCload_docPED_data
* DOCUMENTACION
*  	NI50479
* SOURCE
*/
#include "postCDS.h"

SUB post:CDSload_docPED_data
	LOCAL l_retval LIKE lib:err_retval
	LOCAL l_estado LIKE common:csr_string
	LOCAL l_id_pieza_ped LIKE post:id_pieza_ped
	LOCAL l_serv_desc LIKE post:adt_serv_desc

	l_estado = "NCRED"

Inicio:
	IF (l_estado == "NCRED") THEN		
		//1 - Consulto Notas de Credito
		CDS SELECT FROM DSC:post:NCpedEnOrigen_tbl BECOMES DSC:post:NCpedEnOrigen_ctx \
			WHERE post:fec_act_ncred == siaf:fecha_proceso					
	ELSE
		//2 - Consulto Refacturacion y Remitos de Imposicion
		CDS SELECT FROM DSC:post:NCpedEnOrigen_tbl BECOMES DSC:post:NCpedEnOrigen_ctx \
			WHERE post:fec_act_factu == siaf:fecha_proceso 
	END IF

	CDS FINDFIRST DSC:post:NCpedEnOrigen_ctx LOCK
	LET l_retval = CDSRETVAL
	WHILE (l_retval == 0)
		//limpio los campos
		CLEARFIELD post:ServicioPED
		CLEARFIELD post:pieza_ped
		CLEARFIELD post:adt_serv_desc
		CLEARFIELD post:fec_act_factu
		CLEARFIELD post:LetraCompFACTU
		CLEARFIELD post:NroCompFACTU 
		CLEARFIELD post:ImporteFACTU 
		CLEARFIELD post:NroCompImpos 
		CLEARFIELD post:fec_act_ncred 
		CLEARFIELD post:LetraCompNCRED 
		CLEARFIELD post:NroCompNCRED 
		CLEARFIELD post:ImporteNCRED 
	
		CDS EXTRACT DSC:post:NCpedEnOrigen_ctx \
			post:pieza_ped \
			post:fec_act_factu \
			post:LetraCompFACTU \
			post:NroCompFACTU \
			post:ImporteFACTU \
			post:NroCompImpos \
			post:fec_act_ncred \
			post:LetraCompNCRED \
			post:NroCompNCRED \
			post:imp_cobro_ped INTO post:ImporteNCRED \
			post:id_pieza_ped INTO l_id_pieza_ped
			
		//Traigo el codigo de servicio de la pieza
		CDS SELECT FROM DSC:post:DetNCpedEnOrigen_tbl BECOMES DSC:post:DetNCpedEnOrigen_ctx \
			WHERE post:id_pieza_ped == l_id_pieza_ped	\
			&& post:item_codigo_trx != 8500 \
			&& post:item_codigo_trx != 40002
		CDS FINDFIRST DSC:post:DetNCpedEnOrigen_ctx NOLOCK
		IF (CDSRETVAL == 0) THEN
			CDS EXTRACT DSC:post:DetNCpedEnOrigen_ctx \
				post:item_codigo_trx INTO post:ServicioPED
		END IF
		CDS ENDSELECT DSC:post:DetNCpedEnOrigen_ctx
		
		//Busca la descripcion del servicio			
		CDS SELECT FROM DSC:post:adt_serv_table BECOMES DSC:post:adt_serv_context \
			WHERE post:adt_serv_serv == post:ServicioPED
		CDS FINDFIRST DSC:post:adt_serv_context NOLOCK
		IF (CDSRETVAL == 0) THEN
			CDS EXTRACT DSC:post:adt_serv_context \
				post:adt_serv_desc INTO l_serv_desc
		END IF
		CDS ENDSELECT DSC:post:adt_serv_context
		
		//grabo la tabla temporal del reporte
		CDS SELECT FROM DSC:post:rep_doc_ped_tbl BECOMES DSC:post:rep_doc_ped_ctx \
			WHERE post:ServicioPED == post:ServicioPED \
			&& post:pieza_ped == post:pieza_ped
	 
		CDS FINDFIRST DSC:post:rep_doc_ped_ctx LOCK
		LET l_retval = CDSRETVAL
		
		CDS APPEND DSC:post:rep_doc_ped_ctx \
			post:ServicioPED \
			post:pieza_ped \
			l_serv_desc AS post:adt_serv_desc \
			post:fec_act_factu \
			post:LetraCompFACTU \
			post:NroCompFACTU \
			post:ImporteFACTU \
			post:NroCompImpos \
			post:fec_act_ncred \
			post:LetraCompNCRED \
			post:NroCompNCRED \
			post:ImporteNCRED 
		
		IF (l_retval == 0) THEN
			CDS REPLACE DSC:post:rep_doc_ped_ctx LOCK
		ELSE
			CDS INSERT DSC:post:rep_doc_ped_ctx LOCK
		END IF

		CDS UNLOCK DSC:post:rep_doc_ped_ctx
		CDS ENDSELECT DSC:post:rep_doc_ped_ctx
		
		CDS FINDNEXT DSC:post:NCpedEnOrigen_ctx LOCK
		LET l_retval = CDSRETVAL
	END WHILE	
	CDS UNLOCK DSC:post:NCpedEnOrigen_ctx
	CDS ENDSELECT DSC:post:NCpedEnOrigen_ctx
	
	IF (l_estado == "NCRED") THEN
		l_estado = "FACTU"
		GOTO Inicio
	END IF
	
END SUB

/*******/
