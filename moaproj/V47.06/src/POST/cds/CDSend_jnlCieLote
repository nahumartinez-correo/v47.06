/****o* Pendientes de agrupación/OFB:post:CDSend_jnlCieLote
* NOMBRE
*   OFB:post:CDSend_jnlCieLote
* DESCRIPCION
*   Recorre la LoteCIE para ver cuales faltan journalizar y journaliza
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   //
* DOCUMENTACION
*   NI50525 POS Integrado
* SOURCE
*/
#include "postcds.h"

SUB post:CDSend_jnlCieLote(lote)
	LOCAL l_retval LIKE lib:err_retval

	CDS SELECT FROM DSC:post:LoteCIE_tbl BECOMES DSC:post:LoteCIE_ctx WHERE \
		post:adt_lcie_termi_win == common:window_node && \
		post:adt_lcie_batchNumber == lote && \
		post:adt_lcie_jnlOK == "N"

	CDS FINDFIRST DSC:post:LoteCIE_ctx LOCK
	l_retval = CDSRETVAL
	
	CALL tlr:JNLinitialize
	
	WHILE (l_retval == 0)
	
		CDS EXTRACT DSC:post:LoteCIE_ctx ALL
				
		//Obtebgo la descripcion de la tarjeta que cerro su lote
		CALL CDSobt_CodInt_tarj			
			
		//seteo los campos a journalizar
		post:jnl_lcie_acquirerCod = post:adt_lcie_acquirerCod			// Código de procesador 
		post:jnl_lcie_batchNumber = post:adt_lcie_batchNumber			// Número de lote 
		post:jnl_lcie_issuerCode = post:adt_lcie_issuerCode		  		// Código de tarjeta 
		post:jnl_lcie_purchaCount = post:adt_lcie_purchaCount	    	// Cantidad de ventas 
		post:jnl_lcie_purchaAmoun = post:adt_lcie_purchaAmoun / 100 	// Monto total de ventas 
		post:jnl_lcie_voidCount = post:adt_lcie_voidCount			  	// Cantidad anulaciones de venta 
		post:jnl_lcie_voidAmount = post:adt_lcie_voidAmount	/ 100		// Monto total de anulaciones 
		post:jnl_lcie_refundCount = post:adt_lcie_refundCount			// Cantidad de devoluciones venta 
		post:jnl_lcie_refundAmoun = post:adt_lcie_refundAmoun / 100		// Monto total de devoluciones 
		post:jnl_lcie_refvoidCoun = post:adt_lcie_refvoidCoun			// Cantidad anulaciones devolución 
		post:jnl_lcie_refvoidAmou = post:adt_lcie_refvoidAmou / 100		// Monto total anul. Devolución
		post:jnl_lcie_date = post:adt_lcie_date							// Fecha ("DD/MM/AAAA")  
		post:jnl_lcie_time = post:adt_lcie_time							// Hora ("HH:MM:SS")
		post:jnl_lcie_terminalID = post:adt_lcie_terminalID				// Terminal pos ID
		
		 /* Journalizo el detalle*/
		siaf:adt_tran_codigo = 9035
	   	siaf:codigo_siaf = 9035
		tlr:tran_mnemonic = "DETCIEL"
   		siaf:nombre_transac = "DETALLE DE CIERRE DE LOTE"
		tlr:INDdcol_scrn[0] = SCR:post:SCRNdet_CieLote
       	tlr:INDjnl_append = OFB:post:JNLdet_CieLote
   		
    	CALL post:MISCmuevo_campos_jou
		CALL siaf:MISClleno_modalidad
			   	
       	UNGETKEY RT_SEND
		CALL tlr:JNLtran
		UNGETKEY RT_SEND
       		
 	   	//actualizo el detalle de cada tarjeta como journalizado en la tabla LoteCIE
	   	CDS FINDAGAIN DSC:post:LoteCIE_ctx LOCK
	   	IF (CDSRETVAL == 0)
	   		post:adt_lcie_jnlOK = "S"
	   		CDS APPEND DSC:post:LoteCIE_ctx post:adt_lcie_jnlOK
	   		CDS UPDATE DSC:post:LoteCIE_ctx LOCK
	   	END IF
	   	CDS UNLOCK DSC:post:LoteCIE_ctx
		
		CDS FINDNEXT DSC:post:LoteCIE_ctx NOLOCK
		l_retval = CDSRETVAL		
	
	END WHILE

	RESETFLAG drv:mode_flags,no_stack_list	

	CDS ENDSELECT DSC:post:LoteCIE_ctx 
	//borro los campos extraidos del journal
	CLEARFIELD post:jnl_lcie_acquirerCod
	CLEARFIELD post:jnl_lcie_batchNumber
	CLEARFIELD post:jnl_lcie_issuerCode 
	CLEARFIELD post:jnl_lcie_purchaCount 
	CLEARFIELD post:jnl_lcie_purchaAmoun 
	CLEARFIELD post:jnl_lcie_voidCount 
	CLEARFIELD post:jnl_lcie_voidAmount
	CLEARFIELD post:jnl_lcie_refundCount 
	CLEARFIELD post:jnl_lcie_refundAmoun 
	CLEARFIELD post:jnl_lcie_refvoidCoun 
	CLEARFIELD post:jnl_lcie_refvoidAmou 
	CLEARFIELD post:jnl_lcie_date 
	CLEARFIELD post:jnl_lcie_time 
	CLEARFIELD post:jnl_lcie_terminalID 
	CLEARFIELD post:jnl_lcie_CodInt 
	CLEARFIELD post:jnl_lcie_CodIntDesc
	
	RETURN l_retval
	
END SUB 

/*******/
