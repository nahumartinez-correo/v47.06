/****o* Expedición/OFB:post:CDScant_pzas_cont
* NOMBRE
*   OFB:post:CDScant_pzas_cont
* DESCRIPCION
*   Esta rutina busca en la DetTmp las piezas no marcadas con (*), o sea las que se incluiran en contenedor
*   y muestar la cantidad de las mismas por corte operativo
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   OFB:post:PBcant_pzas_cont
* DOCUMENTACION
*    http://jira.sibs.net/jira/browse/AUT-351
* SOURCE
*/
#include "postcds.h"

SUB post:CDScant_pzas_cont
  
LOCAL lcorteop LIKE post:jnl_grucorte_cod
LOCAL lcorteop_ant LIKE post:jnl_grucorte_cod
LOCAL lapertura like post:jnl_apertura_cod
LOCAL lapertura_ant like post:jnl_apertura_cod
LOCAL cont like post:cantidad

    CDS SELECT FROM DSC:post:DetTmp BECOMES DSC:post:DetTmp_ctx WHERE \
        (op:op_operator_id == op:op_operator_id && \
        post:MarcaBorrado == "") SORT "GCorteAp"
    CDS FINDFIRST post:DetTmp_ctx NOLOCK
    LET lib:err_retval = CDSRETVAL
    cont = 0
    
    IF (lib:err_retval == 0) THEN
	    CDS EXTRACT DSC:post:DetTmp_ctx post:jnl_grucorte_cod INTO lcorteop \
	    post:jnl_apertura_cod into lapertura
  		lcorteop_ant = lcorteop
  		lapertura_ant = lapertura
  	ELSE
  		CDS ENDSELECT DSC:post:DetTmp_ctx
  		RETURN -2
  	ENDIF 
  	
  	WHILE  (lib:err_retval == 0)
  		//INCREMENTO CONTADOR POR CORTE OPERATIVO
  		IF (lcorteop_ant == lcorteop && lapertura_ant == lapertura) THEN
  		   	cont = cont + 1
  		ELSE
  			fprint common:csr_string,"   %2.2s    %1.1s %6.6s",lcorteop_ant,lapertura_ant,cont
			call csr:f_saaddpl(DSC:cant_pzas_cont,common:csr_string) 
  			clearfield common:csr_string
  			lcorteop_ant = lcorteop
  			lapertura_ant = lapertura
  			cont = 1
  		ENDIF
  		
  		CDS FINDNEXT post:DetTmp_ctx NOLOCK
  		LET lib:err_retval = CDSRETVAL
  		IF (lib:err_retval == 0) THEN
  		 	 CDS EXTRACT DSC:post:DetTmp_ctx post:jnl_grucorte_cod INTO lcorteop \
	    post:jnl_apertura_cod into lapertura
  		ENDIF
  		
  	END WHILE
  	
  	fprint common:csr_string,"   %2.2s    %1.1s %6.6s",lcorteop_ant,lapertura_ant,cont
	call csr:f_saaddpl(DSC:cant_pzas_cont,common:csr_string) 
    CDS ENDSELECT DSC:post:DetTmp_ctx


END SUB

/*******/
