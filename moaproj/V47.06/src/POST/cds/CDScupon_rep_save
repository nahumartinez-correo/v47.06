/****o* Cobro por Cuenta de Terceros/OFB:post:CDScupon_rep_save
* NOMBRE
*   OFB:post:CDScupon_rep_save
* DESCRIPCION
*   Graba en la tabla cupon_rep los cupones CCT cobrados. 
*	Esta tabla es utilizada para el Reporte de Cobro por Cuenta de Terceros. 
* ENTRADAS
*   serv = tipo de servivio (CCT o CREM)
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*  OFB:post:LINK6670,post:CDSCaida_RT_CCT
* DOCUMENTACION
*   http://jira.sibs.net/jira/browse/AUT-1684
* SOURCE
*/
#include "postCDS.h"

SUB post:CDScupon_rep_save(serv)
	
	post:rep_cupon_serv = serv
	
	CDS SELECT FROM DSC:post:cupon_rep_table BECOMES DSC:post:cupon_rep_ctx \
		WHERE tlr:jnl_branch == common:sucursal_activa && \
		tlr:jnl_operator == op:op_operator_id && \
		tlr:jnl_date == common:system_date && \
		tlr:jnl_sequence == tlr:jnl_sequence SORT "primary"

	CDS FINDFIRST DSC:post:cupon_rep_ctx LOCK
	IF (CDSRETVAL == -801) THEN
		IF (post:cupon_rend_fis == "S") THEN
			post:rep_cupon_rend_fis = "1"
		ELSE
			post:rep_cupon_rend_fis = "0"
		ENDIF
		
		IF (post:rep_cupon_serv == "CCT") THEN
			//se instancian estos campos vacios ya que solo se utilizan para CREM
			CLEARFIELD post:Pieza 
			CLEARFIELD post:es_CDD
		ELSE
			//se instancian campos vacio ya que solo se utilizan para CCT
		    CLEARFIELD post:servicio_CCT
		    CLEARFIELD post:fecha_vto_CCT
		END IF

		CDS CLEARBUFFER DSC:post:cupon_rep_ctx
		CDS APPEND DSC:post:cupon_rep_ctx \
			FLD:tlr:jnl_branch \
			FLD:tlr:jnl_operator \
			FLD:tlr:jnl_date \
			FLD:tlr:jnl_time \
			FLD:tlr:jnl_bus_date \
			FLD:tlr:jnl_sequence
			
		CDS APPEND DSC:post:cupon_rep_ctx \
			FLD:post:rep_cupon_serv \
			FLD:post:rep_cupon_rend_fis \
			FLD:post:cupon_cta_om AS FLD:post:rep_cupon_cta_om \
			FLD:post:id_comitente_CCT AS FLD:post:rep_cupon_comitente \
			FLD:post:comitente_CCT AS FLD:post:rep_cupon_comit_desc \
			FLD:post:servicio_CCT AS FLD:post:rep_cupon_serv_desc \
			FLD:post:id_cupon_CCT AS FLD:post:rep_cupon_id_cupon \
			FLD:post:fecha_vto_CCT AS FLD:post:rep_cupon_fec_vto \
			FLD:post:importe_CCT AS FLD:rep_cupon_imp_cob \
			FLD:post:Pieza \
			FLD:post:es_CDD

		CDS INSERT DSC:post:cupon_rep_ctx LOCK
	ENDIF
	CDS UNLOCK DSC:post:cupon_rep_ctx
	CDS ENDSELECT DSC:post:cupon_rep_ctx
END SUB

/*******/
