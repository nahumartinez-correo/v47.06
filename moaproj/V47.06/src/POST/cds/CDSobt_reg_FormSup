/****o* Facturación/OFB:post:CDSobt_reg_FormSup
* NOMBRE
*   OFB:post:CDSobt_reg_FormSup
* DESCRIPCION
*   Recorre la DifFfact para ver cual es el registro que tiene la maxima cantidad vendida
*	teniendo en cuenta la formula superadora (!= de 1, 10, 100 o 1000)
* ENTRADAS
*   alicuota: alicuota de iva obtenida desde la AuxIva
* SALIDAS
*   cant_aux: cantidad maxima del registro seleccionado
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   OFB:post:PREScaptura_med_pago
* DOCUMENTACION
*   Documentación relevante que deberá ser actualizada por la modificación de la rutina
* SOURCE
*/
#include "postcds.h"

SUB post:CDSobt_reg_FormSup(alicuota)
	local cant_aux like cantidad
	CDS SELECT FROM DSC:DifFact_tbl BECOMES DSC:DifFact_ctx WHERE \
		post:adt_diffact_fecha == FLD:common:system_date && \
    	post:adt_diffact_op == FLD:op:op_operator_id && \
    	post:adt_diffact_presn == post:PresNro && \
    	post:adt_diffact_ali == alicuota && \
    	post:adt_diffact_flia != "PCL" && \
    	post:adt_diffact_codigo != "6600" && \
    	post:adt_diffact_cant != 1 && \
    	post:adt_diffact_cant != 10 && \
    	post:adt_diffact_cant != 100 && \
    	post:adt_diffact_cant != 10000
	CDS FINDFIRST DSC:DifFact_ctx NOLOCK
	IF (CDSRETVAL == 0)
		CDS AGGREGATE DSC:DifFact_ctx \
			max adt_diffact_cant
		CDS EXTRACT DSC:DifFact_ctx adt_diffact_cant into cant_aux
		CDS ENDSELECT DSC:DifFact_ctx
		RETURN (cant_aux)
	ELSE
		CDS ENDSELECT DSC:DifFact_ctx
		CDS SELECT FROM DSC:DifFact_tbl BECOMES DSC:DifFact_ctx WHERE \
			post:adt_diffact_fecha == FLD:common:system_date && \
		    post:adt_diffact_op == FLD:op:op_operator_id && \
		    post:adt_diffact_presn == post:PresNro && \
		    post:adt_diffact_ali == alicuota
		CDS FINDFIRST DSC:DifFact_ctx NOLOCK
		IF (CDSRETVAL == 0)
			CDS AGGREGATE DSC:DifFact_ctx \
				max adt_diffact_cant
			CDS EXTRACT DSC:DifFact_ctx adt_diffact_cant into cant_aux
			CDS ENDSELECT DSC:DifFact_ctx    		
			RETURN (cant_aux)
		END IF	
	END IF
	RETURN (CDSRETVAL)
END SUB

/*******/

