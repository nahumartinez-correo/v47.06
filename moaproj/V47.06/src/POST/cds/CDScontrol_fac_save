/****o* Facturación/OFB:post:CDScontrol_fac_save
* NOMBRE
*   OFB:post:CDScontrol_fac_save
* DESCRIPCION
*   Graba el formulario (FACT/NC) en la tabla ControlFact
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   OFB:post:CDSprimera_Fact_Ctrl ,OFB:post:MISCactNumFact,OFB:post:MISCaper_ctrl_fact
*	OFB:post:MISCcontrol_anu_save,OFB:post:MISCcontrol_fac_save,OFB:post:MISCestado_fac_save
* DOCUMENTACION
*   http://jira.sibs.net/jira/browse/AUT-354
* SOURCE
*/
#include "postcds.h"

SUB post:CDScontrol_fac_save(Operacion,Centro_Emision)
	local presAux like post:PresNro
	local l_nc like post:NotaCredito

	if (post:Tipo_Formulario == 4) // Filtro los tickets
		return
	endif
	
    CEC_aux = Centro_Emision
    if (NotaCredito == "")
    	NotaCredito = "N"
    endif
    
    //50249 - Se indica que es una nota de credito por incumplimiento para diferenciarla del resto
	if (OperacionNC  && !AnulOperNC) then
		post:NC_tipo = "I"
		l_nc = post:NotaCredito
		post:NotaCredito = "S"
	else
		post:NC_tipo = ""
		if (AnulOperNC) then
			post:NotaCredito = "N"
		endif
	endif
	
    CDS SELECT FROM DSC:ControlFactura_tbl BECOMES 	DSC:ControlFactura_ctx 	\
    WHERE adt_fecha == common:system_date && 								\
 	adt_tipo_formulario == Tipo_Formulario && 						\
	adt_br_CEC_fact == CEC_aux && 									\
	adt_nro_fact == adt_nro_fact && 									\
	NotaCredito == NotaCredito

    CDS FINDFIRST DSC:ControlFactura_ctx LOCK
    LET lib:err_retval = CDSRETVAL
	CDS CLEARBUFFER DSC:ControlFactura_ctx
	Select (Operacion)
		Case 0	// Insertar una Nueva Factura
			adt_fact_anula = 0
            Estado_Fact = "I"
            Cds Append DSC:ControlFactura_ctx  					\
				   common:system_date as FLD:adt_fecha 			\
                   Tipo_Formulario as FLD:adt_tipo_formulario 	\
                   FLD:adt_nro_fact NotaCredito					\
				   op:op_login_id as op_login_id_fact 			\
				   CEC_aux as adt_br_CEC_fact 					\
				   FLD:adt_fact_anula Estado_Fact 				\
                   common:time_of_day as adt_hora_fact

	   		//Agrego German 50249-Incumplimiento
	   		Cds APPEND DSC:ControlFactura_ctx  FLD:NC_tipo

			call MISCleeEstado(FACTURAS)
			if (retval == VERDAD)
	        	Cds Append DSC:ControlFactura_ctx  \
	          	   presNroNew as PresNro_Fact
			else
	        	Cds Append DSC:ControlFactura_ctx  \
	          	   PresNro as PresNro_Fact
			endif
            Cds Insert DSC:ControlFactura_ctx LOCK
				Break

		Case 1 //  Anulacion de la Factura
			adt_fact_anula = 1
			IF (lib:err_retval == 0) THEN
			   CDS APPEND DSC:ControlFactura_ctx  FLD:adt_fact_anula
			   
			   //Agrego German 50249-Incumplimiento
			   Cds APPEND DSC:ControlFactura_ctx  FLD:NC_tipo
		   	
			   CDS UPDATE  DSC:ControlFactura_ctx LOCK
			Else
				Cds Append DSC:ControlFactura_ctx  						\
						   common:system_date as FLD:adt_fecha 			\
                           Tipo_Formulario as FLD:adt_tipo_formulario 	\
                           FLD:adt_nro_fact FLD:adt_fact_anula 			\
						   op:op_login_id as op_login_id_fact 			\
						   CEC_aux as adt_br_CEC_fact 					\
                     	   Estado_Fact NotaCredito						\
              	   		   PresNro as PresNro_Fact
				
				//Agrego German 50249-Incumplimiento
		   		Cds APPEND DSC:ControlFactura_ctx  FLD:NC_tipo
				
				Cds Insert DSC:ControlFactura_ctx LOCK
			Endif
			Break
      Case 2 // Cambio de Estado
             // Estado_Fact Puede ser :
             //  I = impreso
             //  J = Journalizado
             //  R = Reversado
    		If (lib:err_retval == 0) THEN
   			   CDS APPEND DSC:ControlFactura_ctx  Estado_Fact
		   	   
	   		   CDS UPDATE  DSC:ControlFactura_ctx LOCK
            Endif
            Break
	   Case 3 // Actualizacion del numero de Presupuesto.
			IF (lib:err_retval == 0) THEN
			   CDS APPEND DSC:ControlFactura_ctx PresNro as PresNro_Fact
			   	
			   CDS UPDATE  DSC:ControlFactura_ctx LOCK
			Endif
	   		Break
		End Select
    CDS UNLOCK DSC:ControlFactura_ctx
    //restauro
    if (OperacionNC  && !AnulOperNC) then
    	post:NotaCredito = l_nc
    endif
END SUB

/*******/
