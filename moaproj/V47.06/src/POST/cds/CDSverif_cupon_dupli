/****o* Cobro por Cuenta de Terceros/OFB:post:CDSverif_cupon_dupli
* NOMBRE
*   OFB:post:CDSverif_cupon_dupli
* DESCRIPCION
*   Verifica si el cupon se encuentra duplicado.
* ENTRADAS
*   comitente = comitente del cupon
*   servicio = servicio del cupon
*   id_cupon = identificador del cupon
* SALIDAS
*   RETURN (-2): Cupon Duplicado
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*  OFB:post:MISCvalida_cupon_tmp
* DOCUMENTACION
*   http://jira.sibs.net/jira/browse/AUT-1684
* SOURCE
*/
#include "postCDS.h"

SUB post:CDSverif_cupon_dupli(comitente, servicio, id_cupon)
	LOCAL Lcomitente LIKE post:adt_cupon_comitente
	LOCAL Lservicio  LIKE post:adt_cupon_cod_serv
	LOCAL Lid_cupon  LIKE post:adt_cupon_id_cupon

	Lcomitente = comitente
	Lservicio = servicio
	Lid_cupon = id_cupon

	CDS SELECT FROM "cupon" BECOMES "cupon_ctx" WHERE \
		post:adt_cupon_tipo_serv == "CCT" && \
		post:adt_cupon_comitente == Lcomitente && \
		post:adt_cupon_cod_serv == Lservicio && \
		post:adt_cupon_id_cupon == Lid_cupon && \
		post:adt_cupon_cancelado != "S" SORT "primary"

	CDS FINDFIRST "cupon_ctx" LOCK
	IF (CDSRETVAL == 0) THEN
		//Cupon Duplicado
		CDS UNLOCK "cupon_ctx"
		CDS ENDSELECT "cupon_ctx"
	    RETURN (-2)
	END IF	
	CDS UNLOCK "cupon_ctx"
	CDS ENDSELECT "cupon_ctx"
END SUB

/*******/
