/****o* Caja Unificada/OFB:post:CDSsave_web_tmp
* NOMBRE
*   OFB:post:CDSsave_web_tmp
* DESCRIPCION
*   Graba / Actualiza el registro correspondiente al presupuesto en la tabla web_tmp.
* ENTRADAS
*   accion = ALTA - BAJA - MODIF - CABEC
*   estado = 	P=Pendiente de Registración
*			 				R=Registrada
*			 				M=Carga Manual
*			 				C=Cancelada
* SALIDAS
*   Para case "ESTADO" se devuelve la cantidad de registros actualizados.
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   OFB:post:PBaceptar_web,OFB:post:PBCancelarSesion_web,OFB:post:FVALfirma_web,OFB:post:MISCobtengo_cu
*   post:PBConfirmar_web
* DOCUMENTACION
*   http://jira.sibs.net/jira/browse/AUT-1440 
* SOURCE
*/
#include "postcds.h"

SUB post:CDSsave_web_tmp(accion,estado)
	LOCAL l_retval LIKE lib:err_retval
	LOCAL idx like post:cantidad 	
	LOCAL l_cant LIKE post:cantidad
	LOCAL l_web_tmp_cant_ecom LIKE post:web_tmp_cantidad
	
	post:web_tmp_Lestado = estado
	
	//gpb:NI50498 - Para Mercado Libre siempre la cantidad es 1 por registro
	//gpb:PS10853- Ecommerce
	l_web_tmp_cant_ecom = 1
	
  SELECT (accion)
	  CASE "CABEC"
	    CDS SELECT FROM DSC:web_tmp_tbl BECOMES DSC:web_tmp_ctx \
	    	WHERE post:web_tmp_nro_tx_web == "999" && \
	    	post:web_tmp_operator == op:op_operator_id && \
	    	post:web_tmp_fecha == common:system_date && \
	    	post:PresNro == post:PresNro
	    	
	    post:web_tmp_nro_tx_web = "999"
	  BREAK
	  CASE "ALTA"
	  	CDS SELECT FROM DSC:web_tmp_tbl BECOMES DSC:web_tmp_ctx \
	    	WHERE post:web_tmp_nro_tx_web == post:web_tmp_nro_tx_web && \
	    	post:web_tmp_operator == op:op_operator_id && \
	    	post:web_tmp_fecha == common:system_date && \
	    	post:PresNro == post:PresNro 
	  BREAK
	  CASE "ESTADO"
	  	CDS SELECT FROM DSC:web_tmp_tbl BECOMES DSC:web_tmp_ctx \
	  	WHERE post:web_tmp_operator == op:op_operator_id && \
	  	post:web_tmp_fecha == common:system_date && \
	  	post:PresNro == post:PresNro 
	  	
			CDS FINDFIRST DSC:web_tmp_ctx LOCK
			l_retval = CDSRETVAL
			l_cant = 0
			WHILE (l_retval == 0) THEN
	    	CDS CLEARBUFFER DSC:web_tmp_ctx
				CDS APPEND DSC:web_tmp_ctx \
					post:web_tmp_Lestado
				CDS UPDATE DSC:web_tmp_ctx LOCK
				l_cant = l_cant + 1
				CDS FINDNEXT DSC:web_tmp_ctx LOCK
				l_retval = CDSRETVAL
			END WHILE 
			CDS ENDSELECT DSC:web_tmp_ctx
			RETURN l_cant
	  BREAK
	END SELECT	
    
  CDS FINDFIRST DSC:web_tmp_ctx LOCK
	l_retval = CDSRETVAL
	IF (l_retval == -801) THEN
		CDS CLEARBUFFER DSC:web_tmp_ctx
		CDS APPEND DSC:web_tmp_ctx \
			post:web_tmp_nro_tx_web \
			op:op_operator_id AS post:web_tmp_operator \
			common:system_date AS post:web_tmp_fecha \
			post:PresNro \
			post:adt_pweb_motivo \
			post:adt_pweb_nombre \
			post:web_tmp_firma \
			post:web_tmp_codbarra \
			post:adt_pweb_ecommerce
		
		CDS APPEND DSC:web_tmp_ctx \
			post:web_tmp_cod_web
		
		//gpb:Tratamiento para Mercado Libre
		//    Ojo que el motivo puede cambiar por el de SAP!!!	
		//IF (post:adt_pweb_motivo == DSC:post:descMercadoLibre && post:adt_pweb_jnlMulti == "S" && accion != "CABEC") THEN	
		//gpb:PS10853 - Ecommerce
		IF (post:adt_pweb_ecommerce == "S" && post:adt_pweb_jnlMulti == "S" && accion != "CABEC") THEN
			CDS APPEND DSC:web_tmp_ctx \
				l_web_tmp_cant_ecom AS post:web_tmp_cantidad
		ELSE
			CDS APPEND DSC:web_tmp_ctx \
				post:web_tmp_cantidad 
		END IF
			
		CDS APPEND DSC:web_tmp_ctx \
			post:web_tmp_impweb \
			post:adt_pweb_tarifa
		
		CDS APPEND DSC:web_tmp_ctx \
			post:adt_pweb_iva \
			post:adt_pweb_ing_egr \
			post:adt_pweb_formfisc \
			post:adt_pweb_cod_doc \
			post:adt_pweb_codseq \
			siaf:nro_impre \
			post:adt_pweb_stock \
			post:adt_pweb_prodvs \
			post:adt_pweb_mandatoria \
			post:web_tmp_cant_impre \
			post:web_tmp_fecHora_est \
			post:adt_pweb_cod_cli 
		
		idx = 0
		WHILE (idx < 5)
			CDS APPEND DSC:web_tmp_ctx \
				post:adt_pweb_datos[idx] \
				post:adt_pweb_dat_oblig[idx] \
				post:web_tmp_adic[idx] 
			idx = idx +	1
		END WHILE
		
		CDS APPEND DSC:web_tmp_ctx \
			post:web_tmp_Lestado \
			post:web_tmp_Westado \
			post:firma_web \
			post:PreDForFis \
			siaf:adt_tran_afec_efvo \
    	siaf:afecta_efectivo 
    
    //gpb:por contingencia	
    CDS APPEND DSC:web_tmp_ctx \
    	post:adt_pweb_scaneoO \
    	post:adt_pweb_jnlMulti
    	
    //gpb:datos senasa	
    IF (post:adt_pweb_motivo == "SENAS")
	    CDS APPEND DSC:web_tmp_ctx \
	    	cupon_cta_cupon \
	 		fecha_vto_CCT \
			comitente_CCT \
			cupon_cta_sap \
			cupon_cod_serv \
			id_cupon_CCT    	
    END IF
    	
    CDS INSERT DSC:web_tmp_ctx LOCK
  
	END IF
		
	CDS ENDSELECT DSC:web_tmp_ctx
	
END SUB
    	
/*******/
