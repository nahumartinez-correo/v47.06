/****o* Recepción de Piezas/OFB:post:CDSstockp_temp_save
* NOMBRE
*   OFB:post:CDSstockp_temp_save
* DESCRIPCION
*   A medida que el operador va ingresando, y hasta que no confirma la TX, las piezas se 
*   se graban en una tabla temporal stockp_temp. Esta rutina es la que guarda las piezas
*   en dicha tabla.
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   OFB:post:PBAgregar,post:CDStbls_aux_crem
* DOCUMENTACION
*   CD LaboralCC01.doc: http://livelink.intranet.siemens-itron.com.ar/livelink/livelink.exe?func=doc.ViewDoc&nodeId=569016&
*   CD LaboralCC03.doc: http://livelink.intranet.siemens-itron.com.ar/livelink/livelink.exe?func=doc.ViewDoc&nodeId=569219&
*   SC50190.doc:http://livelink.intranet.siemens-itron.com.ar/livelink/livelink.exe?func=doc.ViewDoc&nodeId=639223&
*   50361 - https://jirats.siemens.com.ar/jira/browse/AUT-1684
*NI 50569-RPP
* SOURCE
*/
#include "postcds.h"
//#define# TRAN_CAIDA_VUELO -5

SUB post:CDSstockp_temp_save
	LOCAL operador LIKE stockp_es_grancl
	LOCAL l_retval LIKE lib:err_retval
	
	IF (stockp_destino == "CASILLA DE CORREO") THEN
		IF (stockp_rem_nom == "") THEN
			stockp_rem_nom = " "
		END IF
	END IF
	
	CDS SELECT FROM DSC:stockp_temp_tbl BECOMES DSC:stockp_temp_ctx \
		WHERE (post:stockp_pieza == FLD:post:stockp_pieza)
		
	CDS FINDFIRST DSC:stockp_temp_ctx LOCK
	LET l_retval = CDSRETVAL
	IF (l_retval == 0) THEN//Si la pieza existe
		IF (post:stockp_operador == op:op_operator_id) THEN
			operador = "V"
			/* MFM - Borro la pieza y luego la grabo con las modificaciones*/
			CDS DELETE DSC:stockp_temp_ctx 
		ELSE
			operador = "F"
		END IF
	END IF

	/* SBA - Si es Pieza Pactada en Suc verifico si está en Oasis */
	IF (stockp_destino == "PIEZA PACTADA EN SUC") THEN
		RESETFLAG giros:tran_send_mode,cancelacion

		/* RUTINA DE RECUPERACION DE TRAN CAIDAS EN VUELO - Si hay pendientes las recupero */
		CALL MISCrec_caidas_UP
		IF (RETVAL == TRAN_CAIDA_VUELO) THEN
			//No pongo ningun mensaje puesto que si no puede recuperar la caída de una tx, el operador
			//no tiene por que enterarse
			//MSGBOX "Problemas de Comunicación. Inténtelo nuevamente o seleccione otro medio de pago",0x00,"Atencion"
			CLEARFIELD hcomm:INDhost_sndrt
			CLEARFIELD hcomm:INDhost_rcvrt
			CLEARFIELD lib:stat_message
			REFRESH lib:stat_message		
			RETURN (-2)
		END IF	

		siaf:codigo_proc = 998002 /* Consulta de Encomienda*/
		
		CLEARFIELD FLD:EnPol_Poliza
		CLEARFIELD FLD:EnPol_Track
		MOVE FLD:stockp_pieza TO FLD:EnPol_Track
	
		// Seteo flag de Consulta Simple
		SETFLAG giros:tran_send_mode,consulta_simple

		/*giros:hora_viaje = common:time_of_day
		giros:dia_viaje  = common:system_date */
		giros:hora_viaje = siaf:hora
		giros:dia_viaje = siaf:fecha
		CALL post:CDSobtiene_secuen("STAN")
		
		// Seteo Rutinas de Mensajeria
		hcomm:INDhost_sndrt = OFB:post:SENDencomienda
		hcomm:INDhost_rcvrt = OFB:post:RCVEencomienda
		
	  giros:codigo_procesamiento = siaf:codigo_proc

		CALL hcomm:COMM_stat_init
		IF (!TESTFLAG(hcomm:state_flags,on_line) && (siaf:posible_off == "N")) THEN
			MSGBOX "OPERACION NO PERMITIDA FUERA DE LINEA",OFB_OK,"Atencion"
			RETURN (-2)
		END IF 		

		// Envio la Consulta
		CALL hcomm:COMM_host_tran
		IF (giros:err_firma == -1) THEN    // Error en Verificacion Firma 
			common:trap_reset = OFB:drv:KEYSreset_esc
			KEYDEF CLEAR
			CALL drv:KEYSnavigation
			RETURN -2
		END IF
		// Reseteo flag de Consulta Simple
		RESETFLAG giros:tran_send_mode,consulta_simple

		lib:err_retval = RETVAL
		
		CLEARFIELD lib:stat_message
		REFRESH lib:stat_message
		IF (lib:err_retval < 0) THEN
			// sba 20040114 - seteo el esc para poder salir de la trx.
			common:trap_reset = OFB:drv:KEYSreset_esc
			RETURN -2
		END IF
		
		IF (hcomm:tran_status == HOST_ACCEPTED) THEN
			IF (giros:Sucursal_comm == "") THEN
				// Verifico que el estado de la poliza sea 02 = Disponible o 10 = Cobrado
				// Si no tiene sucursal asignada nose puede seguir
				MSGBOX "LA POLIZA NO TIENE SUCURSAL ASIGNADA",0x00,"Error"
				common:trap_reset = OFB:drv:KEYSreset_esc
				RETURN -2
			END IF
			
			SELECT (giros:EstadoGiro_comm)
				CASE "02"	// Disponible
				CASE "10"	// Cobrada
				CASE "12"	// Cobrada 2 veces
				CASE "13"	// Cobrada con incidentes
				CASE "14"	// Cobrada anulada
					// sba 20040114 - seteo el esc para poder salir de la trx.
					common:trap_reset = OFB:drv:KEYSreset_esc
					CALL CDSverif_recep_endas
				BREAK
				CASE ELSE
					MSGBOX "EL ESTADO DE LA PIEZA NO CORRESPONDE",0x00,"Error"
					common:trap_reset = OFB:drv:KEYSreset_esc
					RETURN -2
			END SELECT
		ELSE
			CLEARFIELD lib:stat_message
			REFRESH lib:stat_message
			common:trap_reset = OFB:drv:KEYSreset_esc
			RETURN -2
		END IF
	END IF
	
	//seteo el campo stockp_es_grancl  dependiendo de las combinaciones de los campos de rendicion
	//centralizada rct
	CALL MISCset_es_grancl

	CDS CLEARBUFFER DSC:stockp_temp_ctx
	
	stockp_fecha = common:system_date
	stockp_operador = op:op_operator_id
	
	CDS APPEND DSC:stockp_temp_ctx stockp_pieza \
		stockp_es_grancl \
		stockp_fecha \
		stockp_operador \
		stockp_plazoguarda \
		stockp_vtofecha \
		stockp_destino \
		stockp_abonado \
		stockp_destin_nom \
		stockp_vd_imp \
		stockp_crem_imp \
		stockp_ctaOM \
		stockp_tipo_doc \
		stockp_doc \
		stockp_Orden \
		stockp_franq_inc \
		stockp_en_devol \
		stockp_dias_alm \
		stockp_Oficio \
		stockp_Observ \
		stockp_dest_ant \
		stockp_solo_entrega \
		stockp_vtofecha_oe \
		AsignaDestino \
		stockp_rend_cent \
		stockp_rct \
		stockp_laboral \
		es_mayorista \
		stockp_PAS
	
	//SC 50361 - Se agregan los nuevos campos CCT-CREM - Cliente Mayorista
	CDS APPEND DSC:stockp_temp_ctx \
		FLD:post:id_cupon_CCT \
		FLD:post:adt_cct_nis_liq \
		FLD:post:adt_cct_comitente \
		FLD:post:adt_cct_cod_serv \
		FLD:post:adt_cct_def_cupon \
		FLD:post:adt_cct_cta_sap \
		FLD:post:adt_cct_rend_fis \
		FLD:post:cod_barra_CREM \
		FLD:post:adt_climay_razon
		
	//50437 - agrego servicio para las VAP
	CDS APPEND DSC:stockp_temp_ctx \
		FLD:serv_stockp\
		post:lugar_guarda_pieza \
		ord_guarda_pieza

	//50479 - PED
	IF (stockp_destino == DSC:post:PED_desc) THEN
		SELECT (estado_pieza_ped)
			CASE "IMP" //De Impuesta pasa REC (Recibida)
				post:stockp_estado_ped = "REC"
				BREAK
			CASE "DAR" //De Devuelta Al Remittente pasa a RPD (Recibida Para Devolucion)
				post:stockp_estado_ped = "RPD"
				BREAK
		END SELECT	
		
		stockp_idPieza_ped = id_pieza_ped
		stockp_NISorig_ped = NisOrigen_ped
		stockp_NISdest_ped = common:sucursal_activa
		stockp_impCobro_ped = imp_cobro_ped
		stockp_cli_nro_ped = cli_nro_ped
		stockp_letraCompPed = letra_comp_ped
		stockp_nroCompPed = nro_comp_ped
		
		CDS APPEND DSC:stockp_temp_ctx \
			post:stockp_estado_ped	\
			post:stockp_idPieza_ped \
			post:stockp_NISorig_ped \
			post:stockp_NISdest_ped	\
			post:stockp_impCobro_ped \
			post:stockp_cli_nro_ped \
			post:stockp_letraCompPed \
			post:stockp_nroCompPed		
	END IF	
	
	
	/*IF ((stockp_destino == DSC:post:ECOM_desc) || stockp_destino == "ENTREGA EN SUCURSAL" || stockp_destino == "CERRADAS CON AVISO") && stockp_en_devol != "S") THEN*/
	if (stockp_destino == DSC:post:ECOM_desc && stockp_en_devol != "S") THEN
			//CALL post:OFBcons_ecommerce //gpb: PS10898 - Busqueda por T&T o TN (Ecommerce)
			//Consulta a T&T si la pieza tiene palabra clave (Y TRAE EL TN!!)
			CALL OFBcons_tyt_PC  
			IF (RETVAL == 0) THEN
				stockp_ecom_clave = "S"
				CDS APPEND DSC:stockp_temp_ctx \
					post:stockp_ecom_clave \
					post:stockp_ecom_tn \
			 		post:stockp_ecom_pclave \
			 		post:stockp_ecom_shipID \
			 		post:stockp_ecom_entrega
			else
			   if (retval == 1) then
			   		stockp_ecom_clave = "N"
			   else 
			   		stockp_ecom_clave = "X"
			   endif
			 	CDS APPEND DSC:stockp_temp_ctx \
					post:stockp_ecom_clave \
					post:stockp_ecom_tn
			END IF
	else
		post:stockp_ecom_clave = "N"
		clearfield			post:stockp_ecom_tn 
		clearfield	 		post:stockp_ecom_pclave 
		clearfield	 		post:stockp_ecom_shipID 
		clearfield	 		post:stockp_ecom_entrega
		CDS APPEND DSC:stockp_temp_ctx \
					post:stockp_ecom_clave \
					post:stockp_ecom_tn \
			 		post:stockp_ecom_pclave \
			 		post:stockp_ecom_shipID \
			 		post:stockp_ecom_entrega
	END IF
	
	//NI50569 - RPP
	CDS APPEND DSC:stockp_temp_ctx stockp_rpp_corp \
	post:adt_stockp_rpp_reg		
	
	IF (l_retval == -801) THEN
		CDS INSERT DSC:stockp_temp_ctx LOCK
		post:cantidad = post:cantidad + 1
		refresh post:cantidad
	ELSE
		IF (operador == "F") THEN
			CDS UNLOCK DSC:stockp_temp_ctx
			CDS ENDSELECT DSC:stockp_temp_ctx
			MSGBOX "ESTA PIEZA HA SIDO INGRESADA POR OTRO OPERADOR",0x00,"ERROR"
			RETURN -2
		ELSE
			/* MFM - Inserto la pieza con las modificaciones */
			CDS INSERT DSC:stockp_temp_ctx LOCK
		END IF
	END IF
	CDS UNLOCK DSC:stockp_temp_ctx
	CDS ENDSELECT DSC:stockp_temp_ctx
END SUB

/*******/
