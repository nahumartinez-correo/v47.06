/****o* Productos Varios/OFB:post:FVALvta_pv_cantidad
* NOMBRE
*   OFB:post:FVALvta_pv_cantidad
* DESCRIPCION
*   Se realizan las validaciones correspondientes al campo cantidad en la venta de Productos Vs.
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   OFB:post:DRAWventa_prodvs
*   OFB:post:MISCcombo_tx_dataval
*   OFB:post:MISCvalidaVtaPV_ABM
* DOCUMENTACION
*   Documentación relevante que deberá ser actualizada por la modificación de la rutina
* SOURCE
*/
#include "postfval.h"

SUB post:FVALvta_pv_cantidad
    LOCAL cant_stk 		LIKE lib:err_retval
    LOCAL LMensaje	    LIKE common:csr_string

    IF (TESTFLAG(tlr:tran_flags,correction)) THEN
        RETURN (0)
    END IF

    IF (adt_prodvs_cod.numchars == 0) THEN
        RETURN (0)
    END IF
    IF (cantidad2 > 99999) then
    	ERRMSG common:errmsg_list, "Cantidad Maxima permitida: 99999"
    	nextfield post:cantidad2
        RETURN (-2)
    END IF
    IF (cantidad2 == 0) THEN
        ERRMSG common:errmsg_list, "La cantidad debe ser mayor a cero"
        nextfield post:cantidad2
        RETURN (-2)
    END IF
	post:adt_stock_codprod = post:adt_prodvs_cod
	post:adt_stock_coddep = 00
	call post:CDSstock_load
	/* calculo el stock disponible */
    IF (adt_stock_stock == 0) THEN
    	IF (post:enCombo == "S") THEN
    		//cgw:muestra mensaje del producto del combo sin stock.
    		post:adt_prodvs_cod = post:adt_concb_producto
    		CALL post:CDSprodvs_desc_load //obtiene la descripcion del producto
    		IF (RETVAL == 0) THEN
    		    FPRINT LMensaje "%s SIN STOCK" post:adt_prodvs_desc
    		ELSE
    			//el producto no existe en ProdVs
    			FPRINT LMensaje "%s SIN STOCK" post:adt_concb_producto
    		END IF
    		MSGBOX LMensaje,0x00,"AVISO"
    		CLEARFIELD post:adt_prodvs_cod
    		CLEARFIELD post:adt_prodvs_desc
    	ELSE
            ERRMSG LST:common:errmsg_list,ERRstock_disp
        END IF
        nextfield post:cantidad2
        RETURN (-2)
    ELSE
    	//PS 9575 - Se debe sumar la cantidad que no se resta al entrar.!!!!
    	if ((adt_stock_stock - adt_stock_reserva + post:cantidad2_backUP) < post:cantidad2) then
    		cant_stk = post:adt_stock_stock - post:adt_stock_reserva + post:cantidad2_backUP
            FPRINT common:csr_string,"CANTIDAD DISPONIBLE EN STOCK %5d",cant_stk
            MSGBOX common:csr_string,0x00,"ERROR"
            nextfield post:cantidad2
            RETURN (-2)
        END IF
    END IF

    IF (adt_prodvs_tarifa.numchars != 0) THEN
   		CALL post:MISCcalcGralProdVs
    END IF

	/*Calcula la tarifa total del producto*/
    CALL MISCact_tarif_prod
    
    //cgw: NI50324 Ventas de Productos Varios - Descto CM Convenio por Producto
	//verifica si existe descto convenio producto CM 
	if (post:cli_tipo == CLIMED) then
		call post:MISCDctoCM_ProdVs
	end if
END SUB

/*******/
