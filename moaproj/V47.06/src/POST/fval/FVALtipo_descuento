/****o* Pendientes de agrupación/OFB:post:FVALtipo_descuento
* NOMBRE
*   OFB:post:FVALtipo_descuento
* DESCRIPCION
*   Se encarga de validar que un tipo de descuento exista y que esté asociado
*	al servicio de donde se está validando.
* ENTRADAS
* SALIDAS
*   retorna -2 si el servicio no existe.
* IMPACTOS
*   Impacta en el descuento de servicios postales y aunque no este habilitado
*	en servicios telegráficos.
* REFERENCIADO POR
*   post:SCRNen_bolsi, post:SCRNen_gral, post:SCRNgral, post:SCRNpi_gral, post:SCRNpn_gral
*	tele:FVALtipo_descuento
* DOCUMENTACION
*   SC 50187
* SOURCE
*/
#include "postfval.h"
SUB post:FVALtipo_descuento

    IF (post:tipo_descto != "") THEN
        /* Los Descuentos son solo para los Minoristas */
    	/* Para un cliente medio con Modalidad Facturación Diferida
    	 tampoco se permite ingresar descuentos*/
    	IF (post:cli_tipo != 1 && !post:ModalidadCC)
			cds endselect adt_descto_ctx
        	CDS SELECT FROM adt_descto_Serv_tbl BECOMES adt_descto_ctx WHERE \
	        post:adt_descto_cod == post:tipo_descto && \
    	    adt_descto_serv_cod == post:tipo_produc2
    	    ;post:adt_descto_flia == siaf:adt_tran_fliatran
        	CDS FINDFIRST adt_descto_ctx NOLOCK
	        IF (CDSRETVAL == -801)
    	    	MSGBOX "TIPO DE DESCUENTO INEXISTENTE",0x00,"ERROR"
        	    CDS ENDSELECT adt_descto_ctx
        		call drv:but_number(OFB:post:PBdescuento)
				if (retval >= 0) then
					drv:last_button = retval
				end if
        		CALL post:PBdescuento
	        	IF (RETVAL < 0 ) THEN
    	    		RETURN -2
        		END IF
        	END IF
        	CDS ENDSELECT adt_descto_ctx
    	ELSE
	    	if (post:ModalidadCC) then
	    		MSGBOX "NO CORRESPONDE HACERLE DESCUENTOS A LA MODALIDAD FACT. DIFERIDA",0x00,"ERROR"
	    	else
	    		IF (post:cli_tipo == 1)
	    			MSGBOX "NO CORRESPONDE HACERLE DESCUENTOS A LOS MAYORISTAS",0x00,"ERROR"
	    		END IF
	    	end if
			RETURN -2
    	END IF
    ENDIF
    NEXTFIELD post:descuento
    RETURN (0)
END SUB

/*******/
