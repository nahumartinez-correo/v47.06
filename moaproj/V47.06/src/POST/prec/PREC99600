/****o* Pendientes de agrupación/OFB:post:PREC99600
* NOMBRE
*   OFB:post:PREC99600
* DESCRIPCION
*   Rutina Pre Collect de la trx de Anulacion de Venta con Tarjeta.
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   LST:post:TRAN99600
* DOCUMENTACION
*   NI50525 POS Integrado
* SOURCE
*/
#include "postprec.h"

SUB post:PREC99600
	LOCAL l_retval LIKE lib:err_retval
	LOCAL l_window_node_orig LIKE siaf:jnl_window_node
	LOCAL l_mensaje LIKE common:csr_string
	
	CDS SELECT FROM DSC:tlr:TABjnl BECOMES DSC:tlr:jnl_context WHERE \
		tlr:jnl_bus_date == common:system_date && \
		siaf:codigo_siaf == 99099 && \
		post:PresNro == giros:PresNro_canc && \
		siaf:moneda == "06" && \
		tlr:jnl_operator == op:op_operator_id
	
	CDS FINDFIRST DSC:tlr:jnl_context NOLOCK
	l_retval = CDSRETVAL
	IF (l_retval == 0) THEN
		CDS EXTRACT DSC:tlr:jnl_context \
			post:Total_importe \
			post:codigo_interno \
			post:NroCompPago2 \
			post:Nro_Lote_Pos \
			post:NroTransac \
			post:Tipo_Operacion_Pos \
			post:importe_origen \
			siaf:moneda \
			post:codigo_medio_pago \
			post:DescripMedPag \
			post:medio_pago_desc
			
		IF (post:codigo_interno == 130410) THEN	
			//gpb:NI50546 Mercado Pago 
			LET post:mp_payment_id = post:NroCompPago2
			
			CDS EXTRACT DSC:tlr:jnl_context \
				post:codint_tarj_desc \
				post:jnl_opmp_codseq_nro INTO post:adt_mp_codseq_nro \
				post:jnl_opmp_codint_mp INTO post:mp_codint_mp \
				post:jnl_opmp_description INTO post:mp_description \
				post:jnl_opmp_device_name INTO post:mp_device_name \
				post:jnl_opmp_status INTO post:mp_status
				
			LET	post:adt_tmp_codint_mp = post:mp_codint_mp
			
		  IF (post:mp_codint_mp == "QRI") THEN
		  	//QR Integrado
		  	//Cargo la info de la Orden
		  	CDS EXTRACT DSC:tlr:jnl_context \
					post:jnl_opmp_currency_id INTO post:mp_currency_id  \
					post:jnl_opmp_quantity INTO post:mp_quantity
			ELSE
				//POINT Integrado
				//Cargo la info de la Orden
		  	CDS EXTRACT DSC:tlr:jnl_context \
					post:jnl_opmp_cc_type INTO post:mp_cc_type  \
					post:jnl_opmp_dis_bck_btn INTO post:mp_disable_back_btn \
					post:jnl_opmp_payer_email INTO post:mp_payer_email
			END IF
			//Cargo order_id,payment_id y payment_status
	  	CDS EXTRACT DSC:tlr:jnl_context \
				post:jnl_opmp_order_id INTO post:mp_order_id \
				post:jnl_opmp_payment_id INTO post:mp_payment_id \
				post:jnl_opmp_status_pago INTO post:mp_status_pago
			
			CDS EXTRACT DSC:tlr:jnl_context \
				post:jnl_opmp_auth_code INTO post:mp_auth_code \
				post:jnl_opmp_pay_method INTO post:mp_payment_method \
				post:jnl_opmp_pay_type INTO post:mp_payment_type \
				post:jnl_opmp_pay_date INTO post:mp_payment_date
		ELSE
			//gpb:NI50525 POS Integrado
			CDS EXTRACT DSC:tlr:jnl_context \
				post:adt_vpi_codseq_nro \
				post:jnl_opvpi_issuerCode \
				post:jnl_opvpi_merchaCode
		END IF
		CDS EXTRACT DSC:tlr:jnl_context \
			siaf:jnl_window_node INTO l_window_node_orig
	END IF
	CDS ENDSELECT DSC:tlr:jnl_context 

	IF (l_retval != 0) THEN
		RETURN -2
	END IF
	
	IF (l_window_node_orig != common:window_node) THEN
		FPRINT l_mensaje "Debe realizar la anulacion desde la terminal que realizo la venta [%s]",l_window_node_orig
		MSGBOX l_mensaje,OFB_OK ,"Aviso"
		UNGETKEY RT_ESC
		RETURN -2
	END IF 

	//Controlo si el cupon paso a tesoro
	tes:teso_cheq_moneda = siaf:moneda
	tes:teso_cheq_nro = post:NroCompPago2
	tes:teso_cheq_cuenta = post:codigo_interno
	tes:teso_cheq_banco = 0
	CALL tes:MISCbusco_cheque_rev
	IF (RETVAL == -1) THEN
		UNGETKEY RT_ESC
		RETURN -2
	END IF
	
	CLEARFIELD tes:teso_cheq_moneda
	CLEARFIELD tes:teso_cheq_nro
	CLEARFIELD tes:teso_cheq_cuenta
	CLEARFIELD tes:teso_cheq_banco
	
	IF (post:codigo_interno == 130410) THEN	
		//gpb:NI50546 Mercado Pago 
		LET post:adt_tpos_codint_vpi = post:jnl_opmp_codint_mp
	ELSE
		//gpb:NI50525 POS Integrado
		//Obtengo la descripcion de la tarjeta a partir del codigo interno vpi
		LET post:adt_lcie_issuerCode = post:jnl_opvpi_issuerCode
		CALL post:CDSobt_CodInt_tarj
		LET codint_tarj_desc = post:jnl_lcie_CodIntDesc
		LET post:adt_tpos_codint_vpi = post:jnl_opvpi_issuerCode
	END IF
	LET post:importe_origen = post:Total_importe
	
	REFRESH post:Total_importe
	REFRESH	post:codigo_interno
	REFRESH post:codint_tarj_desc
	REFRESH	post:NroCompPago2
	REFRESH	post:Nro_Lote_Pos
	REFRESH	post:NroTransac
	REFRESH	post:Tipo_Operacion_Pos
	
	LET post:Tipo_Operacion_PosAN = "ONLINE"
	REFRESH	post:NroCompPago2AN
	REFRESH	post:Nro_Lote_PosAN
	REFRESH	post:NroTransacAN
	REFRESH	post:Tipo_Operacion_PosAN
	
END SUB

/*******/
