/****o* Pendientes de agrupación/OFB:post:PREC99605
* NOMBRE
*   OFB:post:PREC99605
* DESCRIPCION
*   Rutina Pre Collect de la trx de Devolucion de Venta con Tarjeta.
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   LST:post:TRAN99605
* DOCUMENTACION
*   NI50525 POS Integrado
* SOURCE
*/
#include "postprec.h"

SUB post:PREC99605
	LOCAL l_retval LIKE lib:err_retval
	
	LET post:adt_opvpi_PresNro = giros:PresNro_canc

	CDS SELECT FROM DSC:tlr:TABjnl BECOMES DSC:tlr:jnl_context WHERE \
		tlr:jnl_bus_date == common:system_date && \
		siaf:codigo_siaf == 99099 && \
		post:PresNro == post:adt_opvpi_PresNro && \
		siaf:moneda == "06" && \
		tlr:jnl_operator == op:op_operator_id
	
	CDS FINDFIRST DSC:tlr:jnl_context NOLOCK
	l_retval = CDSRETVAL
	IF (l_retval == 0) THEN
		CDS EXTRACT DSC:tlr:jnl_context \
			post:Total_importe \
			post:codigo_interno \
			post:NroCompPago2 \
			post:Nro_Lote_Pos \
			post:NroTransac \
			post:Tipo_Operacion_Pos \
			post:adt_vpi_codseq_nro \
			post:jnl_opvpi_issuerCode \
			post:jnl_opvpi_merchaCode \
			post:importe_origen \
			siaf:moneda \
			post:codigo_medio_pago \
			post:DescripMedPag \
			post:medio_pago_desc \
			post:jnl_opvpi_amount INTO	post:vpi_amount \
			post:jnl_opvpi_instaCount INTO	post:vpi_instalmentCount \
			post:jnl_opvpi_issuerCode INTO post:adt_tpos_codint_vpi \
			post:jnl_opvpi_planCode INTO post:vpi_planCode \
			post:jnl_opvpi_ticketNumb INTO post:vpi_originalTicket \
			post:jnl_opvpi_date INTO post:vpi_originalDate \
			post:jnl_opvpi_receiptNum INTO	post:vpi_receiptNumber \
			post:jnl_opvpi_tip INTO	post:vpi_tip \
			post:jnl_opvpi_merchaCode INTO	post:vpi_merchantCode

	END IF
	CDS ENDSELECT DSC:tlr:jnl_context 

	IF (l_retval != 0) THEN
		RETURN -2
	END IF
	
	//Controlo si el cupon paso a tesoro
	tes:teso_cheq_moneda = siaf:moneda
	tes:teso_cheq_nro = post:NroCompPago2
	tes:teso_cheq_cuenta = post:codigo_interno
	tes:teso_cheq_banco = 0
	CALL tes:MISCbusco_cheque_rev
	IF (RETVAL == -1) THEN
		UNGETKEY RT_ESC
		RETURN -2
	END IF
	
	CLEARFIELD tes:teso_cheq_moneda
	CLEARFIELD tes:teso_cheq_nro
	CLEARFIELD tes:teso_cheq_cuenta
	CLEARFIELD tes:teso_cheq_banco	
	
	//Obtengo la descripcion de la tarjeta a partir del codigo interno vpi
	LET post:adt_lcie_issuerCode = post:jnl_opvpi_issuerCode
	CALL post:CDSobt_CodInt_tarj
	LET codint_tarj_desc = post:jnl_lcie_CodIntDesc
	
	LET post:importe_origen = post:Total_importe
	
	REFRESH post:Total_importe
	REFRESH	post:codigo_interno
	REFRESH post:codint_tarj_desc
	REFRESH	post:NroCompPago2
	REFRESH	post:Nro_Lote_Pos
	REFRESH	post:NroTransac
	REFRESH	post:Tipo_Operacion_Pos
	
	LET post:Tipo_Operacion_PosDV = "ONLINE"
	REFRESH	post:NroCompPago2DV
	REFRESH	post:Nro_Lote_PosDV
	REFRESH	post:NroTransacDV
	REFRESH	post:Tipo_Operacion_PosDV

END SUB

/*******/
