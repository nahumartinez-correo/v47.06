/****o* Pago en Destino/OFB:post:PBproximo_ped
* NOMBRE
*   OFB:post:PBproximo_ped
* DESCRIPCION
*   Validaciones del boton PROXIMO de la pantalla de Destinatarios PED
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   post:SVALped_post_Dtrio
* DOCUMENTACION
*   NI50479
* SOURCE
*/
#include "postpb.h"

SUB post:PBproximo_ped
	drv:button = drv:last_button
	CALL drv:but_up_enable

	CALL MISCflds_crem_req2
	IF (RETVAL < 0) THEN
		MSGBOX "Debe ingresar los datos del cliente para continuar.",OFB_OK,"ERROR"
		RESETFLAG drv:nav_funct_flags,nav_send
		RETURN (-2)
	END IF
   	
	IF (post:adt_TipoDoc_tipo != post:bck_TipoDoc || post:CloCuit != post:bck_CloCuit) THEN
		post:CloInscripto = "N"  //por default
		//despliega datos clientes minoristas
		POPUP scr:post:SCRNcli_minorista,-,PCOLcli_minorista
		IF (LASTKEY == 0x0190) THEN
			CLEARFIELD post:bck_cli_tipo           
			CLEARFIELD post:bck_TipoDoc
			CLEARFIELD post:bck_CloCuit               
			//datos popup clientes minoristas
			CALL post:ABMCliMedio_clear
			//datos SCRNidentifica_cli2
			CALL post:ABMCliMedio2_clear
			post:CloInscripto = "N"  //por default
			RESETVALDONE CloCuit
			NEXTFIELD CloCuit
			RESETFLAG drv:nav_funct_flags,nav_send
			RETURN (-2)
		END IF		
		//Veo si es minorista (esto se hace por si cambio tipo y nro de doc y paso a la proxima pantalla
		//para que no queden los datos sucios del cliente anterior
		CALL post:CDSes_minorista
		IF (RETVAL < 0) THEN
			CLEARFIELD CloCalle
			CLEARFIELD CloNroDir
			CLEARFIELD CloPiso
			CLEARFIELD CloCodPost
			CLEARFIELD CloTelefono
			CLEARFIELD CloProvin
			CLEARFIELD CloLoc
			CLEARFIELD CloPartido
			CLEARFIELD CloRegistral
			CLEARFIELD CloOcupacion
			CLEARFIELD CloMail
			CLEARFIELD pcia_desc
			CLEARFIELD CloOtraOcupacion
			CLEARFIELD CloCelular
		END IF
		post:bck_TipoDoc = post:adt_TipoDoc_tipo 
		post:bck_CloCuit = post:CloCuit
		post:bck_cli_tipo = post:cli_tipo
	END IF
    
	CLEARFIELD post:CliRazBusq
	SETVALDONE CloCuit
	SETVALDONE IvaCod
	SETVALDONE adt_TipoDoc_tipo
	;endcollects	
	INIT 33,0
	
	POPUP post:SCRNident_cli2_ped,-,PCOLident_cli2_ped
	IF (LASTKEY == 0x0190) THEN
		RESETFLAG drv:nav_funct_flags,nav_send
		RETURN -2
	END IF
	RESETFLAG drv:nav_funct_flags,nav_send
END SUB

/*******/
