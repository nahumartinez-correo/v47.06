/****o* Caja Unificada/OFB:post:PBConfirmar_web
* NOMBRE
*   OFB:post:PBConfirmar_web
* DESCRIPCION
*   Rutina que se ejecuta cuando se presiona el botón Confirmar de la pantalla de la TX
*   de Registración WEB.
*   Esta rutina debe validar los datos ingresados en Contingencia.
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual 
* REFERENCIADO POR
*   SCR:post:SCRN6680
* DOCUMENTACION
*   https://jirats.siemens.com.ar/jira/browse/AUT-1440
* SOURCE
*/
#include "postpb.h"
#define OFB_YESNO2                   0x00000104

SUB post:PBConfirmar_web

	drv:button = CB4 //BOTON Confirmar
	CALL drv:but_up

	//Validar que los campos obligatorios hayan sido cargados
	CALL MISCValFld_WebManual
	IF (RETVAL < 0) THEN
		ENABLECONTROL drv:PBcb4
		RETURN -1
	ENDIF
	
	MSGBOX "¿ESTA SEGURO QUE DESEA CONFIRMAR LA CARGA DE DATOS?", OFB_YESNO2, "ATENCION"
	IF (RETVAL == 6) THEN //SI
		PROTFIELD post:web_tmp_cod_web
		
		//Luego se registrarán dichos datos en la tabla web_tmp y se deberá cambiar el estado del registro a “R” (Registrada)
		//en esta primera etapa siempre tendre un solo registro por eso se hardcodea.
		//en una futura etapa este dato se deberá colectar por pantalla estando en contingencia (ya que es clave!!!)
		post:web_tmp_nro_tx_web = 1
		
		//gpb:NI 50498 - Mercado Libre
		//IF (post:adt_pweb_motivo == DSC:post:descMercadoLibre && post:adt_pweb_jnlMulti == "S") THEN
		//gpb:PS10853 - Ecommerce
		IF (post:adt_pweb_ecommerce == "S" && post:adt_pweb_jnlMulti == "S") THEN
			post:web_tmp_nro_tx_web = post:web_tmp_cantidad 
		END IF
		
		CALL CDSsave_web_tmp("ALTA","R")
		//PARA QUE QUEDE LA CABECERA ACTUALIZADA CON EL MISMO ESTADO
		CALL CDSsave_web_tmp("CABEC","R")
		
		//gpb:NI 50498 - Valido si es carga multiple
		IF (post:adt_pweb_jnlMulti == "S") THEN
			post:web_tmp_cantidad = post:web_tmp_cantidad - 1
			REFRESH post:web_tmp_cantidad
			
			IF (post:web_tmp_cantidad > 0) THEN
				CLEARFIELD post:web_tmp_adic[0]
				CLEARFIELD post:web_tmp_adic[1]
				CLEARFIELD post:web_tmp_adic[2]
				CLEARFIELD post:web_tmp_adic[3]
				CLEARFIELD post:web_tmp_adic[4]
				
				REFRESH post:web_tmp_adic[0]
				REFRESH post:web_tmp_adic[1]
				REFRESH post:web_tmp_adic[2]
				REFRESH post:web_tmp_adic[3]
				REFRESH post:web_tmp_adic[4]
				
				NEXTFIELD post:web_tmp_adic[0]
				
				ENABLECONTROL drv:PBcb4
				
				RETURN -1
			END IF
		END IF
		
		//proteger el botón de CONFIRMAR, CANCELAR SESION y todos los campos de carga manual. 
		DISABLECONTROL drv:PBcb4 //CONFIRMAR
		DISABLECONTROL drv:PBcb5 //CANCELAR SESION
		CALL csr:proc_list(F_PROTECT,LST:post:LISTManualWEB_flds)
		MSGBOX "PRESIONE ENTER PARA CONFIRMAR LA TRANSACCION",0X00,"AVISO"
		//UNGETKEY RT_ENTER
	ELSE
		ENABLECONTROL drv:PBcb4
	END IF
  
END SUB

/*******/
