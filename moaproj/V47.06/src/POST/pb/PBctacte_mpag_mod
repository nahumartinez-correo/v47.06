/****o* Pendientes de agrupación/OFB:post:PBctacte_mpag_mod
* NOMBRE
*   OFB:post:PBctacte_mpag_mod
* DESCRIPCION
*   Comportamiento del objeto (obligatorio)
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   Objetos que llaman al objeto documentado (obligatorio)
* DOCUMENTACION
*   Documentación relevante que deberá ser actualizada por la modificación de la rutina
* SOURCE
*/
#include "postpb.h"

SUB post:PBctacte_mpag_mod

    ////////////////////////////////////////////
    // Modifica el medio de pago del picklist //
    ////////////////////////////////////////////

    local imp2         like siaf:importe
    local varl_idx	   like siaf:idx
    local varl_picstr  like common:csr_string
    local varl_picstr2 like common:csr_string
    local varl_message like common:csr_string
    local varl_importe like post:imp_comprobante

    LET drv:button = drv:last_button
    CALL drv:but_up_enable
    CALL drv:but_redraw

    // se modificara el comprobante que este siendo mostrado en el combofield
    if (PicDescrip2.NUMCHARS == 0) then
        errmsg common:errmsg_list,"Debe Seleccionar un Valor de la Lista"
        return (-2)
    end if

    // verifico los campos modificados
    call MISCcc_mpag_inflds
    if (retval < 0) then
        return (-2)
    end if

    // armo el item nuevo
    clearfield varl_picstr
    
    fprint varl_picstr "%2.2s %3.3s %20.20s %12.12s %16.16e  %10.10e %10.10e" tipo_mpago, \
    banco_mpago,cuenta_mpago,cheque_mpago,importe_mpago,fec_e,fec_d
    
    // aca primero quiero confirmar la operatoria...
    let varl_message = "Ud. solicita modificar el item seleccionado por:"
    fprint varl_message "%s\n\n%s" varl_message,varl_picstr
    msgbox FLD:varl_message,OFB_YESNO,"Confirmacion"
    
    if (retval == OFB_IDYES) then
        //
        // busco el comprobante dentro del picklist
        //
        let varl_idx = PL_GETITEM
        call csr:myPickcontrol "mediospagos" varl_picstr varl_idx
        While (retval == 0 && PicDescrip2 != substr(varl_picstr,0,57))
            let varl_idx = varl_idx + 1
            call csr:myPickcontrol "mediospagos" varl_picstr varl_idx
        end while
        if (retval < 0) then
            errmsg common:errmsg_list,"Valor a modificar no encontrado"
            return (-2)
        end if
        //
        // borro el item dentro del picklist
        //
        let varl_idx = varl_idx - PL_GETITEM
        if (varl_idx >= 0) then
            call csr:myPickdelete "mediospagos" varl_picstr varl_idx
            if (retval < 0) then
                msgbox "Imposible Modificar Valor",OFB_OK,\
                   "Error de Aplicacion"
                return (-2)
            else
                // restamos al total
                let varl_importe = substr(varl_picstr,41,16)
                let adt_mp_cod = substr(varl_picstr,0,2)
                call post:CDSadt_MedPago_load
                call MISCconvierto_coti2(varl_importe,imp2)
                let siaf:importe = siaf:importe - imp2
                refresh FLD:siaf:importe
            end if
        end if
        //
        // Agrego el item modificado
        // 
        call PBctacte_mpag_add // reutilizo la rutina. Practico, no?
        if (retval < 0) then
            // si llego aca, es porque borro el item, y no puede insertar el nuevo
            // asi que intento reinsertar el original al menos
            call csr:f_saaddpl "mediospagos" varl_picstr
            let siaf:importe = siaf:importe + imp2
            refresh FLD:siaf:importe
            // aviso del error
            msgbox "Imposible Modificar Valor",OFB_OK,"Error de Aplicacion"
            return (-2)
        end if
        // si todo salio bien, limpio los campos
        call csr:proc_list F_CLEAR LST:post:LISTccte_cobza_flds3
        call csr:proc_list F_REFRESH LST:post:LISTccte_cobza_flds3
    end if
END SUB

/*******/
