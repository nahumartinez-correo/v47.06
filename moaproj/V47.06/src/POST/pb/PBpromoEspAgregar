/****o* Pendientes de agrupación/OFB:post:PBpromoEspAgregar
* NOMBRE
*   OFB:post:PBpromoEspAgregar
* DESCRIPCION
*   Comportamiento del objeto (obligatorio)
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   Objetos que llaman al objeto documentado (obligatorio)
* DOCUMENTACION
*   Documentación relevante que deberá ser actualizada por la modificación de la rutina
* SOURCE
*/
#include "postpb.h"

SUB post:PBpromoEspAgregar
	local l_strAGrabar like common:csr_string
	local l_strTmp like common:csr_string
	local bContinuar like siaf:i
	local b_existeTodasEsp like siaf:i
	local b_existeTodasDen like siaf:i
	local l_especie like post:adt_promo_esp
	local l_subespecie like post:adt_promo_subesp
	drv:last_button = drv:button
    call drv:but_down_disable
    call drv:all_but_disable
    l_especie = post:adt_promo_esp
	l_subespecie = post:adt_promo_subesp
	call post:MISCvalidarCargaEsp
	IF (TESTFLAG(adt_restBon_flags,RestEsphabil))
		post:adt_promo_RestHab = "S"
		call CSR:set_currentfield post:adt_promo_Rest_I_E
    	call siaf:FVALcampo_requerido
    	IF (RETVAL == -2)
    		call drv:back_but_status
			drv:button = drv:last_button
    		call drv:but_up_enable
    		NEXTFIELD post:adt_promo_Rest_I_E
    		return 0
    	end if
	ELSE
		post:adt_promo_RestHab = "N"
	END IF
    if (retval == 0)
		fprint common:csr_string, "%-20s - %-20s", SELECCION_TODAS_ESP, SELECCION_TODAS_DEN
		call post:MISCexisteCargado(dsc:pic_promo_especies, common:csr_string)
		b_existeTodasEsp = retval

		fprint common:csr_string, "%-20s - %-20s", adt_promo_esp, SELECCION_TODAS_DEN
		call post:MISCexisteCargado(dsc:pic_promo_especies, common:csr_string)
    	b_existeTodasDen = retval

    	select (opc_promo_especies)
	    	case ELIGE_TODAS_ESP
				fprint l_strAGrabar, "%-20s - %-20s - %1s - %7s - %1s - %1s", SELECCION_TODAS_ESP, SELECCION_TODAS_DEN, adt_promo_EspTipoBon, adt_promo_EspecieBon, post:adt_promo_RestHab, adt_promo_Rest_I_E
				break
	    	case ELIGE_TODAS_DEN
				fprint l_strAGrabar, "%-20s - %-20s - %1s - %7s - %1s - %1s", adt_promo_esp, SELECCION_TODAS_DEN, adt_promo_EspTipoBon, adt_promo_EspecieBon, post:adt_promo_RestHab, adt_promo_Rest_I_E
				break
	    	case ELIGE_UNA_DEN
				fprint l_strAGrabar, "%-20s - %-20s - %1s - %7s - %1s - %1s", adt_promo_esp, adt_promo_subesp, adt_promo_EspTipoBon, adt_promo_EspecieBon, post:adt_promo_RestHab, adt_promo_Rest_I_E
				break
		end select

		; Me fijo si el item que está cargando ya fue cargado y en ese caso muestro
		; un mensaje de error.
		l_strTmp =  substr(l_strAGrabar,0,43)
		call post:MISCexisteCargado(dsc:pic_promo_especies, l_strTmp)
		if (retval)
			errmsg LST:common:errmsg_list,"ESE VALOR YA FUE INGRESADO!!!"
		else
			; Me fijo si está cargando un item particular y ya tiene una carga para
			; todos los items, en ese caso muestro un mensaje de error.
			bContinuar = true
			if ((opc_promo_especies == ELIGE_TODAS_DEN || opc_promo_especies == ELIGE_UNA_DEN) && b_existeTodasEsp)
				errmsg LST:common:errmsg_list,"YA FUE CARGADA LA BONIFICACION PARA TODOS LAS ESPECIES !!!"
				bContinuar = false
			endif

			if (opc_promo_especies == ELIGE_UNA_DEN && b_existeTodasDen && bContinuar)
				errmsg LST:common:errmsg_list,"YA FUE CARGADA LA BONIFICACION DE LA ESPECIE SELECCIONADA PARA TODAS SUS DENOMINACIONES!!!"
				bContinuar = false
			endif

			if (bContinuar)
		    	select (opc_promo_especies)
			    	case ELIGE_TODAS_ESP
						if (adt_promo_cantEspSel > 0)
						    MSGBOX "YA EXISTEN BONIFICACIONES INGRESADAS, DESEA ELIMINARLAS Y AGREGAR LA BONIFICACION ACTUAL PARA TODOS LAS ESPECIES?",OFB_YESNO,"Agregar Bonificacion"
						    IF (RETVAL == OFB_IDYES) THEN
								call csr:f_sadroppl dsc:post:pic_promo_especies
								call csr:f_sanewpl dsc:post:pic_promo_especies, PIC:post:PICpromo_especies
						    	CALL csr:f_saaddpl dsc:post:pic_promo_especies l_strAGrabar
						    	call csr:f_sadroppl REST_ESP
								call csr:f_sanewpl REST_ESP, PIC:post:PICrest_rest
							    adt_promo_cantEspSel = 1
							    adt_restEsp_cantSel = 0
						    END IF
						else
					    	CALL csr:f_saaddpl dsc:post:pic_promo_especies l_strAGrabar
						    adt_promo_cantEspSel = adt_promo_cantEspSel + 1
						endif
						break
			    	case ELIGE_TODAS_DEN
						fprint common:csr_string, "%-20s", adt_promo_esp
						call post:MISCexisteCargado(dsc:pic_promo_especies, common:csr_string)
						if (retval)
						    MSGBOX "YA EXISTEN BONIFICACIONES INGRESADAS PARA ESTA ESPECIE, DESEA ELIMINARLAS Y AGREGAR LA BONIFICACION ACTUAL PARA TODAS LAS DENOMINACIONES?",OFB_YESNO,"Agregar Bonificacion"
						    IF (RETVAL == OFB_IDYES) THEN
								fprint common:csr_string, "%-20s", adt_promo_esp
								; borro todas las que cumplen con common:csr_string
								call csr:PickOp PICKOP_SEARCH dsc:pic_promo_especies common:csr_string //Buscar en el picklist
								idx = RETVAL
								while (idx >= 0)
									CALL CSR:myPickdelete(DSC:pic_promo_especies,common:csr_string,idx)
									adt_promo_cantEspSel = adt_promo_cantEspSel - 1
									fprint common:csr_string, "%-20s", adt_promo_esp
									call csr:PickOp PICKOP_SEARCH dsc:pic_promo_especies common:csr_string idx //Buscar en el picklist
									idx = RETVAL
								end while
								call post:MISCrestBonifDelAll(1)
						    	CALL csr:f_saaddpl dsc:post:pic_promo_especies l_strAGrabar
							    adt_promo_cantEspSel = adt_promo_cantEspSel + 1
						    END IF
						else
					    	CALL csr:f_saaddpl dsc:post:pic_promo_especies l_strAGrabar
						    adt_promo_cantEspSel = adt_promo_cantEspSel + 1
						endif
						break
			    	case ELIGE_UNA_DEN
				    	CALL csr:f_saaddpl dsc:post:pic_promo_especies l_strAGrabar
					    adt_promo_cantEspSel = adt_promo_cantEspSel + 1
						break
				end select
				call csr:PickOp PICKOP_SORT DSC:post:pic_promo_especies "0,43"
			endif
	    endif
		call post:ABMpromoEspClear
		post:adt_promo_esp = l_especie
		post:adt_promo_subesp = l_subespecie
		call post:ABMpromoEspRefresh
    endif
    call drv:back_but_status
    drv:button = drv:last_button
    call drv:but_up_enable
END SUB

/*******/
