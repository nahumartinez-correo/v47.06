/****o* Productos Varios/OFB:post:SVALventa_prodvs
* NOMBRE
*   OFB:post:SVALventa_prodvs
* DESCRIPCION
*   Rutina que se ejecuta al confirmar la venta de un producto vario: verifica restricciones,
*   reserva el stock vendido y cuando está en estado_presu 4 lo descuenta.
*   Actualiza la TX en la PresuDet y luego la journaliza. Actualiza totales.
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   LST:post:TRANventa_prodvs
* DOCUMENTACION
*   Documentación relevante que deberá ser actualizada por la modificación de la rutina
* SOURCE
*/
#include "postsval.h"

SUB post:SVALventa_prodvs
	LOCAL loc_tarifa LIKE post:adt_prodvs_tarifa
	LOCAL imp_ev LIKE tes:adt_espv_importe
	LOCAL loc_retval LIKE lib:err_retval
	LOCAL l_retval LIKE lib:err_retval
	LOCAL l_cant_pv LIKE adt_cant_pv
	LOCAL l_prodvs_cod LIKE adt_prodvs_cod

	//gpb:NI50479 - Genera NC PED
	IF (TESTFLAG(post:sesion_ped_flags,genera_nc_ped)) THEN
		IF (post:estado_presu == 4) THEN
//gpb:Lo dejo comentado porque antes pasaba... ahora ya tengo los datos en memoria.
;;			//Tengo que recuperar los datos de la PresuDet porque se estan perdiendo algunos
;;			//valores de campos que ya tenia...		
;;			CALL post:CDScons_presudet_ped
;;			IF (RETVAL != 0) THEN
;;				//no pudo recuperar el registro,debo cancelar el presupuesto
;;			END IF
			//actualizo estado
			CALL post:MISCGrabaDetalleTrn
			RETURN RETVAL
		END IF
	END IF

	SETVALDONE post:adt_prodvs_cod
	
	//cgw: NI50324 Ventas de Productos Varios - Descto CM Convenio por Producto
	//Si agrego un producto sin presionar el boton Agregar (por Enter).
	IF (post:estado_presu == 2 && adt_cant_pv == 0 && adt_NroTrn_pv == 0) THEN
		CALL post:PBProdVsAgregar
	END IF
	
	IF (adt_cant_pv > 0 && adt_NroTrn_pv > 0) THEN
		CALL post:CDSsel_prod_pv
		loc_retval = RETVAL
		l_cant_pv = adt_cant_pv
	ELSE
		IF (post:estado_presu == 2 || post:estado_presu == 3) THEN
			tmp_prodvs_cod = adt_prodvs_cod
			CALL CSR:set_currentfield post:adt_prodvs_cod
			CALL post:FVALvta_pv_cod
			IF (RETVAL != 0) THEN
				RETURN (-2)
			END IF
		END IF
		//Orden de Compra
		IF (post:consulta_ordenCM == "S" && post:estado_presu != 4) THEN
			CALL post:MISCValido_Datos_OC
			IF (RETVAL < 0) THEN
				RETURN (-2)
			END IF
		END IF
		l_cant_pv = 1
		//cgw: NI50324 Ventas de Productos Varios - Descto CM Convenio por Producto
		//verifica si existe descto convenio producto CM, en el caso que se agrega
		//el producto sin presionar el boton Agregar (con Enter). (post:estado_presu = 2)
		//Tambien considera si se modifica un producto vario desde grilla. (post:estado_presu = 3)
		IF (post:cli_tipo == CLIMED && post:estado_presu != 4) THEN
			CALL post:MISCDctoCM_ProdVs
		END IF
	END IF

	WHILE (l_cant_pv > 0)
		IF (loc_retval != 801 && adt_NroTrn_pv > 0) THEN
			CDS EXTRACT  DSC:ProdVs_tmp_ctx ALL
			post:adt_prodvs_cod = post:adt_cod_pv
		END IF

		CALL post:MISCval_adic_merch
		IF (RETVAL < 0) THEN
			RETURN (-2)
		END IF
    	
		/*MLD Para reingeniería no se pide supervision en la pasada final */
		IF (estado_presu != 4) THEN
			CALL siaf:SVALnivel_sup
			IF (RETVAL < 0) THEN
				IF (RETVAL == -3) THEN
					CALL post:MISCrechazo_transac
					RETURN -3
				END IF
				RETURN (RETVAL)
			END IF
		END IF
    	
		IF (post:estado_presu == 3) THEN
			//PS 9575-Cancelar el stock reservado con la cantidad anterior
			CALL post:CDScancelo_reserva(adt_stock_codprod,00,post:cantidad2_backUP)
		END IF
	
		/* MLD En la primera pasada hago la reserva de stock */
		IF (post:estado_presu == 2 || post:estado_presu == 3) THEN
			FLD:adt_stock_codprod = adt_prodvs_cod
			FLD:adt_stock_coddep = 00
			CALL post:CDSreserva_stock(adt_stock_codprod,00,cantidad2)
			IF (RETVAL < 0) THEN
				RETURN (-2)
			END IF
		END IF
		
		l_cant_pv = l_cant_pv - 1
		
		IF (loc_retval != 801 && adt_NroTrn_pv > 0) THEN
			CDS FINDNEXT DSC:ProdVs_tmp_ctx NOLOCK
			loc_retval = CDSRETVAL
		END IF
	END WHILE
	
	IF (adt_cant_pv > 0 && adt_NroTrn_pv > 0) THEN
		CALL post:CDSsel_prod_pv
		loc_retval = RETVAL
		l_cant_pv = adt_cant_pv
	ELSE
		l_cant_pv = 1
	END IF
	
	WHILE (l_cant_pv > 0)
		IF (loc_retval != 801 && adt_NroTrn_pv > 0) THEN
			CDS EXTRACT  DSC:ProdVs_tmp_ctx ALL
			post:adt_prodvs_cod = post:adt_cod_pv
		END IF
	    	
		IF (estado_presu == 1 || estado_presu == 4) THEN
			loc_tarifa = post:adt_prodvs_tarifa
			FLD:adt_stock_codprod = adt_prodvs_cod
			FLD:adt_stock_coddep = 00
			CALL CDSstock_load
			IF (TESTFLAG(tlr:tran_flags,correction)) THEN
				adt_stock_stock = adt_stock_stock + cantidad2
			ELSE
				prim_stock_stock = adt_stock_stock
				adt_stock_stock = adt_stock_stock - cantidad2
			END IF
			adt_stock_tumst = common:time_of_day
			adt_stock_fumst = common:system_date
			post:adt_prodvs_tarifa = loc_tarifa
			/* MLD Si estoy en una cobranza de cliente medio no se actualiza el saldo de productos varios*/
			IF (!post:OperacionCob && !post:OperacionAut) THEN
				tes:es_tesoro = "S"
			ELSE
				CLEARFIELD tes:es_tesoro
			END IF
		END IF
    	
		IF (post:codesp_prod.NUMCHARS != 0) then
			IF (post:estado_presu == 2 || post:estado_presu == 1) \
				|| (post:estado_presu == 3 && cod_agrupacion == 0) THEN
				IF (!TESTFLAG(tlr:tran_flags,correction)) THEN
					CALL post:CDSobtiene_secuen("CABEC")
					cod_agrupacion = adt_codseq_nro
				END IF
			END IF
		ELSE
			/* No tengo Cajas Ni cintas. Blanqueo el registro */
			IF (post:estado_presu == 1 || post:estado_presu == 2) THEN
				post:cod_agrupacion = 0
			END IF
		END IF
    	
		//PS Clientes Medios - Bienes de CAmbio
		IF (post:cli_tipo == CLIMED && (estado_presu == 1 || estado_presu == 2)) THEN
			post:bonificaCM = adt_prodvs_bonifCM
		END IF
    			
		/*Seteo los campos necesarios para tener el importe con y sin IVA - Gabriel Falciola*/
		siaf:ImporteConIva = post:ImporteConIvaNeto
		post:ImporteConIva = adt_prodvs_tarifa * cantidad2
		siaf:ImporteSinIva = post:ImporteSinIvaNeto
		post:TotalSinIva = post:ImporteSinIvaNeto
		post:PreDForFis = post:adt_prodvs_formfisca
		/* Calculo de Ventas UP */
		post:AliServUP = post:adt_prodvs_aliciva  // Alicuota de IVA
		post:ImpServUP = siaf:importe - fld_promo_imp // Importe con Iva
		post:tran_original = "S"
		/* Limpio campos que no utilizo */
		CALL csr:cleararray(post:ali_opc)
		CALL csr:cleararray(post:ali_opc)
		CALL csr:cleararray(post:flia_opc)
		CALL csr:cleararray(post:cos_opc)
		CLEARFIELD post:adt_tarif_fijo
		CLEARFIELD post:adt_tarif_fj_aliciva
		CLEARFIELD post:adt_tarif_uneg
		/*Se acumula el Importe Total con y sin IVA en la tabla AuxIngBr - Gabriel Falciola*/
		CALL MISCTotalTransac(siaf:codigo_siaf)
		CALL post:MISCAcumTotalesSes
    	
		/* MLD Me fijo si admite Cheque Diferido para Venta UP */
		CALL post:MISCRestFlia
		CALL post:MISCGrabaDetalleTrn
		
		IF (post:estado_presu == 4 && (!TESTFLAG(tlr:tran_flags,correction)) ) THEN
			CALL post:CDSobtiene_secuen("TICKO")
			LET nro_impre = post:adt_codseq_nro
		END IF
    	
		IF (post:estado_presu != 1 && post:estado_presu != 4) THEN
			post:cantidad = post:cantidad2
			IF (codesp_prod.NUMCHARS > 0) THEN
				/* Genero la venta */
				CALL MISCvta_adicional(8500,"",codesp_prod,subcod_prod,0,ESPECIE_PROD)
			END IF
		END IF
		l_retval = RETVAL
		l_cant_pv = l_cant_pv - 1
		IF (loc_retval != 801 && adt_NroTrn_pv > 0) THEN	
			//cgw:NI50324 - Tratamiento para generar trx Descuento CM del producto
			//desde la tabla ProdVs_tmp.
			CALL post:MISCGenDctoCM_PV    		    		
			CDS FINDNEXT DSC:ProdVs_tmp_ctx NOLOCK
			loc_retval = CDSRETVAL
		END IF
		//cgw:NI50324 - Tratamiento Modificacion trx Descuento CM del producto.
		//Los datos son extraidos de PresuDet (No de la tabla ProdVs_tmp).
		IF (post:estado_presu == 3) THEN
			CALL post:MISCGenDctoCM_PV
		END IF
	END WHILE
	
	CDS ENDSELECT DSC:ProdVs_tmp_ctx
	CALL post:CDSdel_vta_pv(1)
	CLEARFIELD adt_cant_pv
	CLEARFIELD adt_NroTrn_pv
	RETURN (l_retval)
END SUB

/*******/
