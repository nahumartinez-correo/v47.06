 /****o* SiMon/OFB:post:SVALpago_on_site
* NOMBRE
* OFB:post:SVALpago_on_site
* DESCRIPCION
*   Confiramacion de la transaccion de Pago On Site
* ENTRADAS
* SALIDAS
*
* REFERENCIADO POR
*    LST:post:TRANpago_on_site
* DOCUMENTACION
*   https://jirats.siemens.com.ar/jira/browse/AUT-2228
* SOURCE
*/
#include "postsval.h"

SUB post:SVALpago_on_site

	local imp_tran like siaf:importe
	local enSesion_aux like post:enSesion
    local lmsg like common:csr_string	
    local lmsg_aux like common:csr_string
	local l_benef_estado like beneficio_estado
	
	lmsg_aux = "                   "

    //Se fuerza que pase directamente a la ultima pantalla, sino queda en un loop infinito
    //Por Reingeniera (ya que no se muestran mas las pantallas para confirmar).
    if (post:estado_presu == 4)
		drv:screen_active = 1
    end if

    select (drv:screen_active)
	
		case 0
			IF (estado_presu != 3)
				IF (benef_tipo_doc.numchars == 0 && benef_nombre.numchars == 0 && codigo_barras.numchars == 0)
					MSGBOX "Debe ingresar algún valor para realizar la consulta",0x00,"Error"
					return -2
				END IF

				//Creo el picklist para guardar los multiples beneficios
			    clearfield beneficio
				clearfield operativo
				call csr:f_sadroppl("pic_beneficios")
				call csr:f_sanewpl("pic_beneficios",PIC:climedio_multiple)			
				//hago la consulta simple o multiple
				IF (codigo_barras.numchars > 0)
					//Consulta Simple
					beneficio = benef_on_site
					beneficio_estado = "T"
					operativo = operativo_on_site					
					CALL post:OFBconsulta_simon(970110)	
					IF (retval == -2)
						call csr:f_sadroppl("pic_beneficios")
						return -2
					END IF
					//Seteo los campos de la segunda pantalla
					CALL MISCset_flds_benef
				ELSE
				    CALL CSR:proc_list(F_CLEAR,LST:LISTon_site_fld2)
					//consulta multiple
					CALL MISCcons_mult_benef
					IF (retval < 0)
						call csr:f_sadroppl("pic_beneficios")
						call csr:f_sadroppl("pic_mult_benef")
						return -2
					END IF
				END IF
			END IF
			drv:next_screen = 1
			break
	
	case 1 
		// Se va a enviar la confirmacion.
		if (post:estado_presu < 3) then
			IF (benef_on_site.numchars == 0)
				msgbox "Debe seleccionar un Beneficio para confirmar la transaccion",0x00,"Error"
				return -2
			END IF	
			IF (estado_presu == 1)
				msgbox "Desea confirmar la transaccion ?",OFB_YESNO,"CONTINUAR"
				if (retval == 7)
					ungetkey RT_ESC
					call post:ESCtransaccion
					return -3
				end if
			END IF
		end if
			
		call siaf:SVALnivel_sup
		if (retval < 0) then
			if (RETVAL == -3) then
				call post:MISCrechazo_transac
				return -3
			else
				return (retval)
			endif
		endif			

			siaf:siaf_status = HOST_ACCEPTED
	
    		if (post:estado_presu == 4 || post:estado_presu == 1) then
    			
				CLEARFIELD cant_tikos_simon
				//CLEARFIELD cant_renglones
				CLEARFIELD bits_tickoSimon    			
    			
    			
    			tlr:INDdcol_scrn[0] = SCR:post:SCRNpagoONsite_jnl
    			resetflag giros:tran_send_mode,consulta_simple
    			
    			
			    //Seteo campos de mensajeria para el pago 
			    giros:codigo_procesamiento = 970010
				giros:hora_viaje = common:time_of_day
				giros:dia_viaje  = common:system_date
				Call post:CDSobtiene_secuen("STAN")
				marca_masivo = "N"
				importe_benef = siaf:importe
				beneficio = benef_on_site
				beneficio_cod_reimp = benef_cod_reimpre
				l_benef_estado = beneficio_estado
				beneficio_estado = "P"
				operativo = operativo_on_site

				enSesion_aux = post:enSesion
				clearfield post:enSesion
	    		call tlr:JNLtran
				// restauro la variable post:enSesion
	    		post:enSesion = enSesion_aux
				post:ctrol_ult_tt = ""

				siaf:siaf_status = hcomm:tran_status
				IF (hcomm:tran_status != HOST_ACCEPTED) then
					call MISCanulo_tx_pago
					SELECT (l_benef_estado)
					    CASE "P"
					         lmsg_aux = "BENEFICIO PAGADO"
					         break
					    CASE "S"
                             lmsg_aux = "BENEFICIO ASIGNADO"
                             break
					    CASE "X"
                             lmsg_aux = "BENEFICIO BLOQUEADO"
                             break
                    END SELECT
                    FPRINT lmsg "IMPOSIBLE REALIZAR EL PAGO\n%s" lmsg_aux
                    MSGBOX lmsg,OFB_ICONSTOP,"ATENCION!!"
				ELSE
					clearfield post:PreDForFis
					clearfield post:adt_serv_formfiscal
					CALL post:MISCTotalTransac(siaf:codigo_siaf)
					call post:MISCActMedPago
				END IF

				siaf:siaf_status = hcomm:tran_status

		    	/*limpio campos para que la JNLtran no ejecutre nada dos veces*/
				CLEARFIELD drv:INDhcomm_rtn
				CLEARFIELD drv:INDspc_rtn[1]
				CLEARFIELD drv:INDcur_tran
				CLEARFIELD drv:INDjnl_rtn
				CLEARFIELD drv:INDspc_rtn[0]

				
			end if		
			
	       
			CALL post:MISCGrabaDetalleTrn
				        
	        IF (estado_presu == 1 )
	        	tlr:INDdcol_scrn[0] = SCR:post:SCRNcaptura_adiciona
	        	 siaf:importe = siaf:importe * -1
	        	CALL post:MISCgrabo_sinsesion
	        END IF
	        
	        call csr:f_sadroppl("pic_mult_benef")
	        
	        break
    end select
END SUB
/*******/
