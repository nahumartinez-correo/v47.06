/****o* Registro RPP/OFB:post:SVALrepPzasRPP
* NOMBRE
*   OFB:post:SVALrepPzasRPP
* DESCRIPCION
*   Rutina SVAL del Reporte de Piezas RPP
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   RETURN (-2): No Existen Operadores con Nivel Cajero
*   RETURN (-2): No hay filtros de seleccion para el reporte
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   post:TRANRepPzasRPP
* DOCUMENTACION
*   NI50569
* SOURCE
*/
#include "postsval.h"

SUB post:SVALrepPzasRPP
	LOCAL l_operadorRPP        LIKE post:operadorRPP
	LOCAL l_fec_d              LIKE common:csr_string
	LOCAL l_fec_a              LIKE common:csr_string
	LOCAL l_registracionRPP    LIKE post:registracionRPP
	LOCAL l_cobradoRPP         LIKE post:cobradoRPP
  LOCAL l_entregadoRPP       LIKE post:entregadoRPP
  LOCAL Lind                 LIKE drv:INDlast_menu
  LOCAL l_piezaRPP           LIKE post:track_trace

	LET l_operadorRPP = post:operadorRPP
	LET l_fec_d = post:fec_d
	LET l_fec_a = post:fec_a
	LET l_registracionRPP = post:registracionRPP
	LET l_cobradoRPP = post:cobradoRPP
	LET l_entregadoRPP = post:entregadoRPP   
	LET l_piezaRPP = post:track_trace

  CALL post:MISCFtrim(l_operadorRPP)
  CALL post:MISCFtrim(l_fec_d) 
  CALL post:MISCFtrim(l_fec_a)
  CALL post:MISCFtrim(l_registracionRPP)
  CALL post:MISCFtrim(l_cobradoRPP)
  CALL post:MISCFtrim(l_entregadoRPP)
  CALL post:MISCFtrim(l_piezaRPP)

	//si no es busqueda por track and trace
	IF (!TESTFLAG (pieza_rpp_rep, pieza_rpp)) THEN
		IF (l_fec_d.NUMCHARS == 0 && l_fec_a.NUMCHARS == 0) THEN
			MSGBOX "Debe ingresar un rango de fechas.",0x00000000,"Error"
			RETURN (-2)
		END IF
		
		//Tratamiento filtro de Operadores. 
		IF (l_operadorRPP == "TODOS") THEN
			CLEARFIELD post:operador_idRPP
		ELSE
			//Obtiene el codigo id de operador para filtro del reporte.
			CDS SELECT FROM DSC:op:adt_op_table BECOMES DSC:op:adt_op_context \
				WHERE op:op_branch_id == fld:common:sucursal_activa && \
							op:op_login_id == l_operadorRPP
			CDS FINDFIRST DSC:op:adt_op_context LOCK
			IF (CDSRETVAL < 0) THEN
				CDS UNLOCK DSC:op:adt_op_context
				CDS ENDSELECT DSC:op:adt_op_context
				MSGBOX "No Existen Operadores con Nivel Cajero",0x00000000,"Error"
				RETURN (-2)
			END IF
			CDS EXTRACT DSC:op:adt_op_context op:op_operator_id INTO post:operador_idRPP
			LET l_operadorRPP = post:operador_idRPP
			CDS UNLOCK DSC:op:adt_op_context
			CDS ENDSELECT DSC:op:adt_op_context
		END IF
	ELSE
		//limpio los campos ya que solo busco por track and trace
		CLEARFIELD l_operadorRPP
		CLEARFIELD l_fec_d 
		CLEARFIELD l_fec_a
		CLEARFIELD l_registracionRPP
		CLEARFIELD l_cobradoRPP
		CLEARFIELD l_entregadoRPP
	END IF

	CALL post:MISCarmoRepRPP(l_operadorRPP,l_fec_d, l_fec_a,l_registracionRPP,l_cobradoRPP,l_entregadoRPP,l_piezaRPP)
	IF (RETVAL == -2) THEN
		RETURN (-2)
	END IF

	//libero los pick en memoria
	CALL csr:f_sadroppl "PICregistracionRPP"
	CALL csr:f_sadroppl "PICOperadorRPP"
	CALL csr:f_sadroppl "PICcobradoRPP"
	CALL csr:f_sadroppl "PICentregadoRPP"
	
	//Reestablezco la pantalla que contine las herramientas del escritoro
	//calculadora-calendario-bloque de estacion-correo-wordpad-ayuda
	Lind = LST:post:MENUrepPzasRPP //(Asignar menu que te interesa)
	IF (drv:INDlast_menu == Lind) THEN
		CALL giros:MISCjoinDesktopTool
	END IF
	
END SUB

/*******/

