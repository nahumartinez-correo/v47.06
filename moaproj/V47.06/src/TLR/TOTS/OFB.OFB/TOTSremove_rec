/****o* Pendientes de agrupación/OFB:tlr:TOTSremove_rec
* NOMBRE
*   OFB:tlr:TOTSremove_rec
* DESCRIPCION
*   Comportamiento del objeto (obligatorio)
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   Objetos que llaman al objeto documentado (obligatorio)
* DOCUMENTACION
*   Documentación relevante que deberá ser actualizada por la modificación de la rutina
* SOURCE
*/
#include "totsOFB.OFB.h"

/****************************************************************/
/* Remove Totals Record for Operator							*/
/* This routine is invoked from operator maintenance to remove	*/
/* a totals record for an operator.								*/
/* If there is not currently a record, do nothing.				*/
/* If there is currently a record, replace it with a record		*/
/* containing zeros for all of the totals fields.				*/
/* Upon Entry:													*/
/*   FLD:tlr:tots_branch       = operator's branch				*/
/*   FLD:tlr:tots_operator     = operator's id					*/
/* Upon Exit:													*/
/*   RETVAL =  0 if successful									*/
/*          = -2 if error										*/
/****************************************************************/
	SUB tlr:TOTSremove_rec
/* find totals record, if any, for this operator */
	CALL  OFB:tots_find_rec
/* if there is no totals record to remove, our work is done */
	if (retval == -CDS_RECORD_NOT_FOUND)
		cds endselect DSC:CONtots_temp
		return
	END IF
/* if record found, clear it */
	if (retval == 0)
	/* preserve branch, operator, bus_date and sequence */
		cds extract DSC:CONtots_temp tots_branch \
			tots_operator \
			tots_bus_date \
			tots_sequence
	/* flag record removed (to avoid cash displays) */
		move "R" to tots_status
	/* build skeletal record (no custom tots) */
		cds clearbuffer DSC:CONtots_temp
		clearfield tots_zeros_fixed
		cds append DSC:CONtots_temp \
			tots_branch \
			, \
			tots_operator \
			tots_bus_date \
			tots_sequence \
			tots_status
		cds append DSC:CONtots_temp \
			tots_zeros_fixed AS tots_start_cash \
			tots_zeros_fixed AS tots_cash_in \
			tots_zeros_fixed AS tots_cash_out \
			tots_zeros_fixed AS tots_net_cash
	/* replace entire totals record */
		cds replace DSC:CONtots_temp lock
		cds endselect DSC:CONtots_temp
		return
	END IF
/* if we get here, our mission has failed */
	errmsg common:errmsg_list,DSC:ERRtots_maint
	return (-2)
	END SUB

/*******/
