/****o* Pendientes de agrupación/OFB:tlr:TOTSadd_rec
* NOMBRE
*   OFB:tlr:TOTSadd_rec
* DESCRIPCION
* 	Add Totals Record for Operator							
* 	This routine is invoked from operator maintenance to add	
* 	a totals record for an operator.							
* 	If there is not already a record, create one with zeros in
* 	all of the fields.										
* 	If there is already a record, it was left by the remove	
* 	totals record routine, and is correct as is.				
* ENTRADAS
*   FLD:tlr:tots_branch       = operator's branch			
*   FLD:tlr:tots_operator     = operator's id				
* SALIDAS
*   RETVAL =  0 if successful								
*          = -2 if error									
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   Objetos que llaman al objeto documentado (obligatorio)
* DOCUMENTACION
*   Documentación relevante que deberá ser actualizada por la modificación de la rutina
* SOURCE
*/
#include "totsOFB.OFB.h"

	SUB tlr:TOTSadd_rec
/* find totals record, if any, for this operator */
	CALL  OFB:tots_find_rec
/* if there is already a record, this is either a modification to */
/* an active operator or the re-adding of a removed operator */
	if (retval == 0)
		cds extract DSC:CONtots_temp tots_status
	/* if the record is flagged as removed, mark it added again */
		if (tots_status == "R")
			move "A" to tots_status /* flag as just added */
			cds clearbuffer DSC:CONtots_temp
			cds append DSC:CONtots_temp tots_status
			cds update DSC:CONtots_temp nolock
		END IF
		cds endselect DSC:CONtots_temp
		return
	END IF
/* if no record, create skeletal totals record (no custom fields) */
	if (retval == -CDS_RECORD_NOT_FOUND)
		cds clearbuffer DSC:CONtots_temp
		clearfield tots_bus_date
		clearfield tots_sequence
		move "A" to tots_status /* flag as just added */
		clearfield tots_zeros_fixed
		cds append DSC:CONtots_temp \
			tots_branch \
			, \
			tots_operator \
			tots_bus_date \
			tots_sequence \
			tots_status
		cds append DSC:CONtots_temp \
			tots_zeros_fixed AS tots_start_cash \
			tots_zeros_fixed AS tots_cash_in \
			tots_zeros_fixed AS tots_cash_out \
			tots_zeros_fixed AS tots_net_cash
		cds insert DSC:CONtots_temp nolock
		cds endselect DSC:CONtots_temp
		return
	END IF
/* if we get here, our mission has failed */
	errmsg common:errmsg_list,DSC:ERRtots_maint
	return (-2)
	END SUB

/********/
