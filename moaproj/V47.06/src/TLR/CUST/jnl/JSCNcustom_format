/****o* Pendientes de agrupación/OFB:tlr:JSCNcustom_format
* NOMBRE
*   OFB:tlr:JSCNcustom_format
* DESCRIPCION
* 	Format Custom 2 Line Scan Display                         
* 	This 2 line display is used for the following journal scan
* 	functions:                                                
*   	display scan header                                     
*   	print scan output                                       
*   	quick scan pick list                                    
* ENTRADAS
*   journal record available on context DSC:CONjscn         
*   jscn_ds_flag   = pre-built w/ standard flag info        
*   jscn_ds_status = pre-built w/ standard status info      
* SALIDAS
*   jscn_ds_line1 = 1st line of display                     
*   jscn_ds_line2 = 2nd line of display                     
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   Objetos que llaman al objeto documentado (obligatorio)
* DOCUMENTACION
*   Documentación relevante que deberá ser actualizada por la modificación de la rutina
* SOURCE
*/
#include "custjnl.h"

SUB tlr:JSCNcustom_format
/* pre-clear fields that may not be appended */
    clearfield jnl_tran_name
    clearfield jnl_account_1
    clearfield jnl_amount_1
    clearfield jnl_tran_flags
    clearfield siaf:codigo_siaf
    clearfield siaf:jnl_sucursal
    clearfield siaf:jnl_window_node
    clearfield siaf:jnl_journal_host
    clearfield siaf:hora_reentry
    clearfield siaf:modalidad
    clearfield siaf:moneda
    clearfield post:jnl_flia_tran
    clearfield gali:sucursal
    clearfield gali:digito_verif
    clearfield post:adt_Modo
/* extract fields for display */
    cds extract DSC:CONjscn \
        jnl_bus_date \
        jnl_time \
        jnl_sequence \
        jnl_tran_name \
        jnl_branch \
        jnl_operator \
        jnl_account_1 \
        jnl_amount_1 \
        jnl_tran_flags \
        siaf:codigo_siaf \
        siaf:jnl_sucursal \
        siaf:jnl_window_node \
        siaf:jnl_journal_host \
        siaf:hora_reentry \
        siaf:modalidad \
        siaf:moneda \
        post:jnl_flia_tran \
        gali:sucursal \
        gali:digito_verif \
        post:adt_Modo
   cds extract DSC:CONjscn \
   post:nro_autorizac 
    /* build line 1 of display */
    fprint jscn_ds_line1, \
        "%10e %5e %4s %-8s-%04e %8e %18s %s %-10.10s" \
        jnl_bus_date,jnl_time,jnl_sequence,jnl_tran_name, \
        siaf:codigo_siaf,siaf:hora_reentry,substr(jscn_ds_flag,12,31),post:adt_Modo, \
        siaf:jnl_window_node
    /* build line 2 of display */

    if (post:jnl_flia_tran == "BG" ) then
        call csr:trimfield jnl_account_1
        if (jnl_account_1.numchars != 0) then
            fprint jscn_ds_line2 "%10.10e-%4.4e/%1.1s",\
                jnl_account_1,\
                gali:sucursal, \
                gali:digito_verif

            if (siaf:moneda.NUMCHARS != 0) then
                fprint common:csr_string " %s" siaf:moneda
            else
                fprint common:csr_string " %s" "  "
            end if
        else
            fprint jscn_ds_line2 "% 10.10s % 4.4s % 1.1s",\
                " ",\
                " ",\
                " "
            fprint common:csr_string " %s" "  "
        end if /* jnl_account_1 != 0 */

        fprint jscn_ds_line2,\
            "%6.6s %-5s %-5s %s", \
            siaf:jnl_journal_host,jnl_branch, \
            jnl_operator, jscn_ds_line2

    else
        fprint jscn_ds_line2,\
            "%6.6s %-5s %-5s %5e %10e ",\
            siaf:jnl_journal_host,jnl_branch, \
            jnl_operator,siaf:jnl_sucursal,jnl_account_1
        if (siaf:moneda.NUMCHARS != 0) then
            fprint common:csr_string " %s" siaf:moneda
        else
            fprint common:csr_string " %s" "  "
        end if
    end if /* if Familia BG */

    fprint jscn_ds_line2,\
            "%s%s%18e %15.15s", \
            jscn_ds_line2,common:csr_string, jnl_amount_1,jscn_ds_status

    fprint jscn_ds_line2,"%s%-3s",jscn_ds_line2,siaf:modalidad
	IF (post:jnl_flia_tran == "CSM" && post:nro_autorizac.NUMCHARS > 0) THEN
	    fprint tlr:jscn_ds_line2,"%s %012s",tlr:jscn_ds_line2,post:nro_autorizac
	END IF
END SUB

/*******/
