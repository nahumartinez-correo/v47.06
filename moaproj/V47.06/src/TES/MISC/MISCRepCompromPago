/****o* Compromisos de Pagos/OFB:tes:MISCRepCompromPago
* NOMBRE
*   OFB:tes:MISCRepCompromPago
* DESCRIPCION
*	Recorre tabla AuxComp para generar Reporte de Compromiso de Pago.
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   RETURN (-2): No hay Compromisos de Pago grabados en tabla AuxComp.
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   tes:MISCgenerar_rep_Comp
* DOCUMENTACION
*   Documentación relevante que deberá ser actualizada por la modificación de la rutina
* SOURCE
*/
#include "tesMISC.h"

SUB tes:MISCRepCompromPago
    
    LOCAL total_compromisos LIKE post:aux_comp_imp
    LOCAL l_retval LIKE lib:err_retval

    //cgw:NI50276 - Genera Reporte de Compromiso de Pago.

    total_compromisos = 0

	CDS SELECT FROM DSC:post:AuxComp_tbl BECOMES DSC:post:AuxComp_ctx \
	WHERE post:aux_item_cod == "" && post:aux_subitem_cod == "" SORT post:CompPago
    
	CDS FINDFIRST DSC:post:AuxComp_ctx NOLOCK
	lib:err_retval = CDSRETVAL

	IF (lib:err_retval < 0) THEN
	    RETURN (-2)
	END IF

	WHILE (lib:err_retval != -801) THEN

		CLEARFIELD post:aux_item_cod
		CLEARFIELD post:aux_subitem_cod
		CLEARFIELD post:aux_comp_desc
		CLEARFIELD post:aux_comp_imp
		CLEARFIELD post:seq_compromiso

		CDS EXTRACT DSC:post:AuxComp_ctx all				

		FPRINT common:csr_string "%-30.30s         %13.13e" post:aux_comp_desc,post:aux_comp_imp
		CALL tes:MISCwrite_file2(5,fld:common:csr_string)		
		CLEARFIELD common:csr_string

		total_compromisos = total_compromisos + post:aux_comp_imp

		// Busco los items para ese concepto

		CDS SELECT from DSC:post:AuxComp_tbl becomes DSC:post:AuxComp2_ctx WHERE \
		post:aux_concepto_cod == post:aux_concepto_cod && post:aux_item_cod != "" && \
		post:aux_subitem_cod == ""
		CDS FINDFIRST DSC:post:AuxComp2_ctx NOLOCK
		
		WHILE (CDSRETVAL == 0) THEN

			CDS EXTRACT DSC:post:AuxComp2_ctx ALL

			FPRINT common:csr_string "   %-30.30s      %13.13e" post:aux_comp_desc,post:aux_comp_imp
			CALL tes:MISCwrite_file2(5,fld:common:csr_string)
			CLEARFIELD common:csr_string

			// Busco los subitems para ese item

			CDS SELECT from DSC:post:AuxComp_tbl becomes DSC:post:AuxComp3_ctx WHERE \
			post:aux_concepto_cod == post:aux_concepto_cod && \
			post:aux_item_cod == post:aux_item_cod && post:aux_subitem_cod != ""

			CDS FINDFIRST DSC:post:AuxComp3_ctx NOLOCK
			l_retval = CDSRETVAL

			WHILE (l_retval == 0) THEN
				CDS EXTRACT DSC:post:AuxComp3_ctx ALL

				FPRINT common:csr_string "      %-30.30s   %13.13e" post:aux_comp_desc,post:aux_comp_imp
                CALL tes:MISCwrite_file2(5,fld:common:csr_string)
				CLEARFIELD common:csr_string

	    	    CDS FINDNEXT DSC:post:AuxComp3_ctx NOLOCK
				l_retval = CDSRETVAL
			END WHILE
	    	CDS ENDSELECT DSC:post:AuxComp3_ctx

    		CDS FINDNEXT DSC:post:AuxComp2_ctx NOLOCK
        END WHILE

    	CDS ENDSELECT DSC:post:AuxComp2_ctx

        CDS FINDNEXT DSC:post:AuxComp_ctx NOLOCK
        lib:err_retval = CDSRETVAL
	END WHILE
    CDS ENDSELECT DSC:post:AuxComp_ctx

    MOVE " " to common:csr_string
    CALL tes:MISCwrite_file2(5,fld:common:csr_string)

;    FPRINT common:csr_string "TOTAL:                                <st>1</st>%13.13e" total_compromisos

		FPRINT common:csr_string "<st>3</st>%-30.30s         %13.13e" , "TOTAL:",total_compromisos
    CALL tes:MISCwrite_file2(5,fld:common:csr_string)

END SUB

/*******/
