/****o* Contra Reembolso/OFB:tes:CDSSave_RFalloCR_mov
* NOMBRE
*   OFB:tes:CDSSave_RFalloCR_mov
* DESCRIPCION
*   Realiza la Recepcion obligatoria de los Fallos CREM pendientes, luego de una Apertura de Caja.
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   tes:MISCsave_mov_pend
* DOCUMENTACION
*   Documentación relevante que deberá ser actualizada por la modificación de la rutina
* SOURCE
*/
#include "tesCDS.h"

SUB tes:CDSSave_RFalloCR_mov
	LOCAL hubo_asignacion LIKE siaf:i
	LOCAL loc_jnl_rtn     LIKE drv:INDjnl_rtn
	LOCAL loc_tran_name   LIKE tlr:jnl_tran_name
	LOCAL loc_mnemonic    LIKE tlr:tran_mnemonic
	LOCAL loc_nom_tran    LIKE siaf:nombre_transac
	LOCAL mone            LIKE siaf:cant
	LOCAL loc_scr         LIKE drv:INDscr
	local loc_cur_tran    LIKE drv:INDcur_tran

	loc_jnl_rtn   = drv:INDjnl_rtn
	loc_tran_name = tlr:jnl_tran_name
	loc_mnemonic  = tlr:tran_mnemonic
	loc_nom_tran  = siaf:nombre_transac
	loc_scr       = drv:INDscr 
	loc_cur_tran  = drv:INDcur_tran   

	hubo_asignacion = 0
	siaf:codigo_siaf = 6660
    //siaf:nombre_transac = "RECEPCION DE FALLO CREM"
    //drv:descrip = "RECFALCR"
    siaf:nombre_transac = "RECEPCION DE FALLO CREM NEGATIVO"
    drv:descrip = "RECFACRN"
    siaf:restr_otra_casa = "N"
    siaf:linea_validacion = "S"
    siaf:exhibe_pantalla = "S"
    siaf:imprime_comprobante = "S"

	CALL siaf:PRESfecha_hora

	/* Busco en la tabla FallosCrem si ese operador tiene un Fallo CREM pendiente
	de Recepcion */
	/*Agrego sort para forzar que ordene por la clave primaria - Gabriel Falciola*/
	CDS SELECT FROM DSC:post:FallosCrem_tbl BECOMES DSC:post:FallosCrem_ctx \
	WHERE post:FalloCrem_estado == "S" && post:FalloCrem_opdes == op:op_operator_id sort "primary"
	    
	CDS FINDFIRST DSC:post:FallosCrem_ctx LOCK
	lib:err_retval = CDSRETVAL
	DO
		IF (lib:err_retval < 0) THEN
			BREAK
		END IF
		hubo_asignacion = 1

        setflag tlr:state_flags,tran_active

		CDS EXTRACT DSC:post:FallosCrem_ctx ALL        
        IF (post:FalloCrem_imp < 0) THEN
        	post:recep_falloCR_imp = post:FalloCrem_imp * -1
        ELSE
            post:recep_falloCR_imp = post:FalloCrem_imp
        END IF
        post:importe_origen = post:recep_falloCR_imp
		siaf:importe = post:recep_falloCR_imp
        
	    siaf:moneda = "00"
	    post:recep_falloCR_moneda = "00"		
		post:recep_falloCR_mondes = "EFECTIVO PESOS"
		siaf:desc_moneda = "EFECTIVO PESOS"
	    siaf:continua_transaccion = "S"

		let drv:INDscr = SCR:siaf:SCRNRecepFalloCREM
		let drv:INDcur_tran = LST:siaf:TRANRecepFalloCREM
		let siaf:tiene_reverso = "S"

		/* Busco el motivo para la Recepcion de Ajuste de Fallo CREM*/
		CALL tes:MISCllena_scrFalloCR

        IF (post:recep_falloCR_motivo.numchars > 0) THEN
            //selecciono correctamente motivo (sin inconsistencia en parametria)
		    drv:INDjnl_rtn = OFB:siaf:JNLRecepFalloCREM
		    CLEARFIELD drv:INDspc_rtn[2]

    	    siaf:codigo_siaf = 6660 //NEGATIVO.
    	    siaf:nombre_transac = "RECEPCION DE FALLO CREM NEGATIVO"
    	    //cgw: No se totaliza mas.
    	    IF (drv:dt_app != 4) THEN
    	        drv:INDspc_rtn[0] = 0 //LST:siaf:TOTS6660 //Setea totalizacion Recepcion de Fallo Negativo.
    	        tlr:INDtots_append = 0 //LST:siaf:TOTS6660
    	    END IF
    	    
    	    IF (siaf:adt_mot_tipo_rep == "ND") THEN
    	        drv:INDspc_rtn[2] = OFB:siaf:TRANrep_nota_def //Setea para imprimir nota de deficit.
    	    END IF

	 	    CDS CLEARBUFFER DSC:post:FallosCrem_ctx

            // Acepta Recepcion Fallo.
            post:FalloCrem_estado = "A"
            post:FalloCrem_fech_recep = common:system_date  // Fecha de Recepcion Fallo CREM.
	        CDS CLEARBUFFER DSC:post:FallosCrem_ctx
	        CDS APPEND DSC:post:FallosCrem_ctx \
		        post:FalloCrem_estado \
		        post:FalloCrem_fech_recep

	        CDS UPDATE DSC:post:FallosCrem_ctx LOCK
	        CDS UNLOCK DSC:post:FallosCrem_ctx

		    UNGETKEY 0x191
		    CALL tlr:JNLtran
        END IF

        resetflag tlr:state_flags,tran_active

		CDS FINDNEXT DSC:post:FallosCrem_ctx LOCK
		lib:err_retval = CDSRETVAL
	LOOP
    CDS ENDSELECT DSC:post:FallosCrem_ctx

	// Si no hay Fallos CREM pendientes para el operador, limpiar la marca de Fallo CREM en rend_caj.
	CALL post:MISClimp_rendcajCREM

	drv:INDjnl_rtn      = loc_jnl_rtn
	tlr:jnl_tran_name   = loc_tran_name
	tlr:tran_mnemonic   = loc_mnemonic
	siaf:nombre_transac = loc_nom_tran
	drv:INDscr          = loc_scr
	drv:INDcur_tran     = loc_cur_tran   

END SUB

/*******/
