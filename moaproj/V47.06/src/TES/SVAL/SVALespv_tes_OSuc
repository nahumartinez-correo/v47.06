/****o* Pendientes de agrupación/OFB:tes:SVALespv_tes_OSuc
* NOMBRE
*   OFB:tes:SVALespv_tes_OSuc
* DESCRIPCION
*   Comportamiento del objeto (obligatorio)
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   Objetos que llaman al objeto documentado (obligatorio)
* DOCUMENTACION
*   Documentación relevante que deberá ser actualizada por la modificación de la rutina
* SOURCE
*/
#include "tesSVAL.h"

SUB tes:SVALespv_tes_OSuc
    local tmp_importe like siaf:importe
    local tmp_cantidad like siaf:cantidad    
    
    IF (!(testflag(tlr:tran_flags,correction)))
       	
        if (siaf:importe == 0)
            move "(L) IMPORTE TOTAL NO PUEDE SER 0" to drv:error_msg
            call  OFB:drv:error_msg
            nextfield adt_evaux_cantidad
            return (-2)
        end if
        popup tes:SCRNconfirma
        if (siaf:continua_transaccion == "N" || lastkey == RT_ESC)
            return (-2)
        end if
        
        let adt_evaux_importe = 0
	    let adt_evaux_cantidad = 0
	    ;let espv_subcod = TOTAL_TESO
	    clearfield espv_subcod	
        clearfield espv_cod
        clearfield adt_espv_cod
        clearfield adt_espv_subcod 
	     /* El header de los movimientos graba el importe total
        y la cantidad correspondiente en cero. 27/08/99 - Andrea */  
        tmp_importe = siaf:importe
        tmp_cantidad = siaf:cantidad
        refresh tmp_importe
        refresh tmp_cantidad
	    siaf:importe = 0
	    siaf:cantidad = 0
        refresh siaf:importe
        refresh siaf:cantidad
	
	    FLD:drv:INDjnl_rtn = OFB:tes:JNLespv_otrasuc
	    ungetkey 0x190
	    call siaf:MISClleno_modalidad
        let drv:INDscr = SCR:tes:SCRNmov_ev_suc_hdr
	    call tlr:JNLtran
	    let drv:INDscr = SCR:tes:SCRNmov_ev_suc
        aux_sequence_mov = tlr:jnl_sequence
                        
	    siaf:importe = tmp_importe
        siaf:cantidad = tmp_cantidad
        refresh siaf:importe
        refresh siaf:cantidad
	
	    // le sumo 6 al codigo de transacción porque cada uno de los pases
	    // tiene ese código de transacción
	    let siaf:codigo_siaf = siaf:codigo_siaf + 6	
	    select (siaf:codigo_siaf)
	        case 9246
	             let siaf:nombre_transac = "SAL.EV.TES A OTRA SUC."
	             move "SEVTOS" to tlr:tran_mnemonic
	             break
	        case 9266
	             let siaf:nombre_transac = "ENT.EV.TES DE OTRA SUC."
	             move "EEVTOS" to tlr:tran_mnemonic
	             break
	    end select
	
	    let drv:INDscr = SCR:tes:SCRNmov_ev_suc
        cds select FROM DSC:AuxEspV_tbl becomes DSC:AuxEspV_ctx \
            where tes:adt_evaux_suc == op:op_branch_id && \
	              adt_evaux_op == op:op_tesoro_id && \
	              adt_evaux_tesoro == "S" && \
	              adt_evaux_es_venta == "N"
	    cds findfirst DSC:AuxEspV_ctx LOCK
        let lib:err_retval = CDSRETVAL  
		    
	    call csr:f_sadroppl dsc:Especie
        call csr:f_sanewpl dsc:Especie pic:Especie
            
        let post:importe = 0 
            
	    do
	        if (lib:err_retval != 0) then
	            break
	        end if
	         
		    cds extract DSC:AuxEspV_ctx ALL
		    move tes:adt_evaux_codigo to espv_cod
		    move tes:adt_evaux_codigo to adt_espv_cod
		    move tes:adt_evaux_subcod to adt_espv_subcod
		    move tes:adt_evaux_subcod to espv_subcod
		    move tes:adt_evaux_importe to siaf:importe
		    move adt_evaux_cantidad to siaf:cantidad
		    move tes:adt_evaux_suc to adt_espv_suc
		    let tes:es_tesoro = "S"
		 
		    FLD:drv:INDjnl_rtn = OFB:tes:JNLespv_otrasuc
		    ungetkey 0x190
		    call siaf:MISClleno_modalidad
		    call tlr:JNLtran
		 	         
            fprint common:csr_string "%-20s %-20s    %13.13s \n", \
                   adt_evaux_codigo,adt_evaux_subcod,adt_evaux_importe
	        call csr:f_saaddpl dsc:Especie common:csr_string
	        let post:importe = post:importe + siaf:importe 
	         
 	 	    cds delete dsc:AuxEspV_ctx
		    cds unlock DSC:AuxEspV_ctx
		    cds findnext DSC:AuxEspV_ctx LOCK
		    let lib:err_retval = CDSRETVAL
	    loop

	    CLEARFIELD drv:INDjnl_rtn
        let drv:INDspc_rtn[2] = ofb:TRANprint_salida_ev
        cds endselect DSC:AuxEspV_ctx
        call post:MISCactivar_buts
        
    ELSE
        CALL siaf:SVALnivel_sup_ev
        if (retval < 0)
            return (-2)
        end if 
        if (siaf:codigo_siaf == 9240 || siaf:codigo_siaf == 9260) then
            CALL MISCreverso_sesion
	        if (retval < 0) then
	            return (-2)
	        end if
	    end if	   
    END IF
END SUB

/*******/
