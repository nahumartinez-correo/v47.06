/****o* Pendientes de agrupación/OFB:siaf:MISCtrata_paramafip
* NOMBRE
*   OFB:siaf:MISCtrata_paramafip
* DESCRIPCION
*   Comportamiento del objeto (obligatorio)
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   Objetos que llaman al objeto documentado (obligatorio)
* DOCUMENTACION
*   Documentación relevante que deberá ser actualizada por la modificación de la rutina
* SOURCE
*/
#include "siafMISC.h"

SUB siaf:MISCtrata_paramafip(prm_data) 
    /////////////////////////////////////////////////////////
    // Esta rutina realiza un tratamiento sobre los campos //
    // pertenecientes a un formulario (segun su grupo).    //
    // Entrada: prm_data -> lista de datos a tratar, sepa_ //
    //                      rados por "|".                 //
    //          FLD:siaf:form_grupo -> grupo al cual per_  //
    //                      tenece el formualio de acuerdo //
    //                      a la tabla ParamAfip.          //
    //          FLD:siaf:INDscratch -> la variable apunta  //
    //                      a la rutina que trata en forma //
    //                      especifica a cada campo        //
    // Salida:  retval = 0  -> Ok!                         //
    //          retval = -2 -> Ooops!                      //
    /////////////////////////////////////////////////////////

    local varl_dlist	like post:sf1_comprobante
    local varl_offset	like siaf:idx
    local varl_data	like siaf:adt_afip_scralias
    local error		like siaf:i

    clearfield error
    let varl_dlist = prm_data
    let varl_offset = 0
    call csr:str_search(varl_dlist,"|",varl_offset)
    if (retval == -1 && varl_dlist.NUMCHARS > 0) then
        // hay un solo dato faltante...
        let varl_offset = varl_dlist.NUMCHARS 
        let retval = 0
    end if
    while (retval == 0) 
        let varl_data  = substr(varl_dlist,0,varl_offset)
            // busco el campo correspondiente al dato faltante
        CDS SELECT FROM "ParamAfip" BECOMES "CONparam" \
            WHERE siaf:form_grupo == siaf:form_grupo && \
                  siaf:adt_afip_scralias == FLD:varl_data
        if (cdsretval == 0) then
            CDS FINDFIRST "CONparam" NOLOCK
            if (cdsretval == 0) then
                CDS EXTRACT "CONparam" siaf:adt_afip_scrcampo
                if (cdsretval == 0) then
                    call FLD:siaf:INDscratch
                    if (retval < 0) then
                       let error = 1
                    end if
                else
                    let error = 1
                end if
            else
                let error = 1
            end if
        else
            let error = 1
        end if
        // resto de la lista el dato tratado...
        let varl_offset = varl_offset + 1
        fprint varl_dlist "%s" substr(varl_dlist,varl_offset, 99)
        call csr:str_search(varl_dlist,"|",varl_offset)
    end while

    // Veo si ocurrio algun error
    if (error) then
        return (-2)
    end if
END SUB

/*******/
