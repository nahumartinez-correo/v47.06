#include "ctc-STD.OFB.h"

/* NAME: hcomm:COMM_hoststat_update                                       */
/*                                                                        */
/*   This routine determines which host requires the status update and    */
/*   adjusts the status line accordingly.                                 */
//	Pindex = session index                                    

SUB hcomm:COMM_hoststat_update (Pindex)
	SELECT (host_status[Pindex])
	case CTC_STATUS_UP
		move dsc:hcomm:host_up to hcomm:host_stat
		break
	case CTC_STATUS_DOWN
		move dsc:hcomm:host_down to hcomm:host_stat
		break
	CASE ELSE
		move dsc:hcomm:host_csdn to hcomm:host_stat
		break
	END SELECT
	if (Pindex == 0)
		move hcomm:host_stat to lib:stat_host1
		resetflag hcomm:state_flags,on_line /* assume not online */
		if (lib:stat_host1 != dsc:hcomm:host_up && lib:stat_host1 !=\
			dsc:hcomm:host_sscp)
      /* if host is not up force host mode offline. */
			move dsc:hcomm:host_downmode to lib:stat_host_mode
			let lib:stat_host_mode.highlite = 1
			let lib:stat_host1.highlite = 1
			let lib:stat_reentry.highlite = 0
		else /* host must be up */
			let lib:stat_host1.highlite = 0
			if (lib:stat_host_mode.numchars == 0) /* is blank at system\
				startup */
				move dsc:hcomm:host_upmode to lib:stat_host_mode
				let lib:stat_host_mode.highlite = 0
				setflag hcomm:state_flags,on_line
				if (lib:stat_reentry == "REN")
					let lib:stat_reentry.highlite = 1
				END IF
			else
				if (lib:stat_host_mode == dsc:hcomm:host_upmode)
					setflag hcomm:state_flags,on_line
					if (lib:stat_reentry == "REN")
						let lib:stat_reentry.highlite = 1
					END IF
				END IF
			END IF
		END IF
		refresh lib:stat_host_mode
		refresh lib:stat_host1
		refresh lib:stat_reentry
		return
	elseif ( Pindex == 1 )
		move hcomm:host_stat to lib:stat_host2
		if (lib:stat_host2 == dsc:hcomm:host_down || lib:stat_host2 ==\
			dsc:hcomm:host_csdn)
			let lib:stat_host2.highlite = 1
		else
			let lib:stat_host2.highlite = 0
		END IF
		refresh lib:stat_host2
	END IF

//		Put more code in if you want to use more than 2 sessions
END SUB
