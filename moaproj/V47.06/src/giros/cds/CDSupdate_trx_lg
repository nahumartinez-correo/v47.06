/****o* Lista de Giros/OFB:giros:CDSupdate_trx_lg
* NOMBRE
*   OFB:giros:CDSupdate_trx_lg
* DESCRIPCION
*   Actualiza en la sesion_lg los datos que devuelve el switch luego de una emision de giro
* ENTRADAS
*   esta: estado actual del giro emitido
*	flia_giro: familia del giro que fue emitido
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   OFB:giros:OFBproceso_lg
* DOCUMENTACION
*   http://jira.sibs.net/jira/browse/AUT-520
* SOURCE
*/


#include "giroscds.h"


SUB giros:CDSupdate_trx_lg(esta,flia_giro)

	local varl_estado    like giros:estado_gm
	local varl_flia_giro like giros:flia_giro_fld
	local varl_codigo    like siaf:codigo_siaf
	local varl_status    like hcomm:tran_status
	local varl_serie	like giros:Serie_fld
	local varl_nro		like giros:NroGiro_fld
	local varl_digito	like giros:digito_fld

	fprint varl_estado,"%s",esta
	fprint varl_flia_giro,"%s",flia_giro

	giros:estado_lg = varl_estado
    giros:FechaEmision_fld = common:system_date
    
    varl_status = hcomm:tran_status
	//Resguardo los valores del giro
	varl_serie = giros:Serie_fld
	varl_nro = giros:NroGiro_fld
	varl_digito = giros:digito_fld
	
	
    
 	CDS CLEARBUFFER DSC:CONses_lg
	
	IF (hcomm:tran_status == HOST_REJECTED)
		//Mejora fase II
		//Lo unico que se graba es el estado del mensaje en la sesion_lg para diferenciarlo de los giros vivos
		//que quedan por enviar luego de un rechazo 
			clearfield clave_hdr_giros 
			clearfield codigo_procesamiento 
			clearfield giros:FechaEmision_fld 
			clearfield giros:CodRespuesta_comm 
			clearfield giros:hora_viaje 
			clearfield giros:dia_viaje 
			clearfield giros:firma_enviada 
			clearfield giros:Firma_comm 
			clearfield giros:cons_nro_ctrol
			clearfield giros:Serie_fld
			clearfield giros:NroGiro_fld
			clearfield giros:digito_fld
	ELSE
  			CDS APPEND DSC:CONses_lg FLD:giros:estado_lg AS FLD:giros:estadoRep_lg \
                FLD:giros:FechaEmision_fld  AS FLD:giros:fechaRep_lg  \
            	FLD:op:op_operator_id       AS FLD:giros:operatorRep_lg	
	END IF
	CDS APPEND DSC:CONses_lg \
		giros:estado_lg \
		giros:clave_hdr_giros \
		giros:codigo_procesamiento \
		giros:FechaEmision_fld \
		varl_status AS giros:lg_status \
		giros:CodRespuesta_comm \
		post:adt_codseq_nro \
		giros:hora_viaje \
		giros:dia_viaje \
		giros:firma_enviada \
		giros:Firma_comm \
		giros:cons_nro_ctrol

	if (varl_flia_giro == "T" || varl_flia_giro == "P")
		CDS APPEND DSC:CONses_lg \
			giros:Serie_fld \
			giros:NroGiro_fld \
			giros:digito_fld
	end if
	
 

	
	//Restauro los valores del giro 
	giros:Serie_fld = varl_serie
	giros:NroGiro_fld = varl_nro
	giros:digito_fld = varl_digito	
	
	CDS UPDATE DSC:CONses_lg LOCK NOWAIT
	CDS WAIT DSC:CONses_lg
	IF (cdsretval < 0)
		CALL  OFB:lib:MISCcds_find_err /* display error msg */
		return -2
	END IF

END SUB

/*******/
