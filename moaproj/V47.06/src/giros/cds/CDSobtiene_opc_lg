/****o* Lista de Giros/OFB:giros:CDSobtiene_opc_lg
* NOMBRE
*   OFB:giros:CDSobtiene_opc_lg
* DESCRIPCION
*   Obtiene el opcional desde la tabla opc_lg (Opcionales asociados a los productos de una Lista de Giros).
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   RETURN (-2): No pudo obtener el opcional asociado al giro de la Lista de Giros en tabla opc_lg
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   giros:FVALsel_opc_lg
* DOCUMENTACION
*   http://jira.sibs.net/jira/browse/AUT-520
* SOURCE
*/

#include "giroscds.h"

SUB giros:CDSobtiene_opc_lg
    LOCAL l_encontro       LIKE giros:a_emitir
	LOCAL l_itemlg_codprod LIKE post:adt_itemlg_codprod
	LOCAL l_itemlg_destino LIKE post:adt_itemlg_destino
	LOCAL l_opclg_codopc   LIKE post:adt_opclg_codopc
	LOCAL l_itemlg_rdesde  LIKE post:adt_itemlg_rdesde
    LOCAL l_opclg_impopc   LIKE post:adt_opclg_impopc
	LOCAL l_opclg_ivaopc   LIKE post:adt_opclg_ivaopc
	LOCAL l_opclg_mod_calc LIKE post:adt_opclg_mod_calc

    CDS SELECT FROM DSC:post:TABopc_lg BECOMES DSC:post:CTXopc_lg WHERE  \
	post:op_operator_id == op:op_operator_id && \
	post:adt_itemlg_codprod == post:tipo_produc2 && \
	post:adt_itemlg_destino == post:adt_tarif_dest && \
	post:adt_opclg_codopc == post:sel_opc && \
	post:adt_itemlg_rdesde <= giros:ImporteNeto

    CDS FINDFIRST DSC:post:CTXopc_lg LOCK
    IF (CDSRETVAL == -801 ) THEN
    	MSGBOX "NO SE PUEDE DETERMINAR LA TARIFA DEL OPCIONAL POR NO HALLARSE EN EL TARIFARIO",0x00,"Error"
    	CDS ENDSELECT DSC:post:CTXopc_lg
    	RETURN (-2)
    END IF	

    lib:err_retval = CDSRETVAL
    l_encontro = "N"

    DO
        IF (lib:err_retval < 0 || l_encontro == "S") THEN
        	//supero el rango tarifario de los opcionales.
        	BREAK
        END IF	
		CDS EXTRACT DSC:post:CTXopc_lg ALL
		IF (giros:ImporteNeto >= post:adt_itemlg_rdesde) THEN
			//reserva los valores del ultimo rango tariforio de opcional correcto hasta el momento.
			l_itemlg_codprod = post:adt_itemlg_codprod
			l_itemlg_destino = post:adt_itemlg_destino
			l_opclg_codopc = post:adt_opclg_codopc
		    l_itemlg_rdesde = post:adt_itemlg_rdesde
    		l_opclg_impopc = post:adt_opclg_impopc
			l_opclg_ivaopc = post:adt_opclg_ivaopc
			l_opclg_mod_calc = post:adt_opclg_mod_calc
		ELSE
			//supero el rango tarifario de los opcionales.
		    l_encontro = "S"
		END IF
		CDS FINDNEXT DSC:post:CTXopc_lg LOCK
		lib:err_retval = CDSRETVAL
	LOOP
	//se recupera los datos de opc_lg correspondiente al rango tarifario para opcionales.
	post:adt_itemlg_codprod = l_itemlg_codprod
	post:adt_itemlg_destino = l_itemlg_destino
	post:adt_opclg_codopc = l_opclg_codopc	
	post:adt_itemlg_rdesde = l_itemlg_rdesde
    post:adt_opclg_impopc = l_opclg_impopc
	post:adt_opclg_ivaopc = l_opclg_ivaopc
	post:adt_opclg_mod_calc = l_opclg_mod_calc

	CDS ENDSELECT DSC:post:CTXopc_lg
END SUB

/*******/
