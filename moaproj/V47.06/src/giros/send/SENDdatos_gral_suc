/****o* ABM´s/OFB:giros:SENDdatos_gral_suc
* NOMBRE
*   OFB:giros:SENDdatos_gral_suc
* DESCRIPCION
*   Rutina SVAL del ABM Datos Generales de la Sucursal.
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   RETURN (0): Al seleccionar Grabar, Actualizar, Borrar en la primera pantalla, valida los datos de
*               la segunda pantalla, y hay inconsistencia, no actualiza y pas a la segunda pantalla.
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   giros:TRANdatgrales
* DOCUMENTACION
*   Documentación relevante que deberá ser actualizada por la modificación de la rutina
* SOURCE
*/
#include "girossend.h"

sub giros:SENDdatos_gral_suc

    call drv:test_note_nav_funct //testea si cambio de solapa
    if (retval == -2) then
    	//resetea flags de pestañas y botones proximo(send) - previo de la screen.
    	call drv:reset_nav_funct
    	return 0 //cambio de solapa.
    end if

    if (TESTFLAG (drv:nav_funct_flags,nav_send)) then
    	//Si se presiono boton proximo(send) de la screen.
    	drv:next_tab = drv:tab_active + 1
        drv:next_screen = drv:screen_active + 1
    else
        if (TESTFLAG (drv:nav_funct_flags,nav_previous)) then
        	//Si se presiono boton previo de la screen.
        	drv:next_tab = drv:tab_active - 1
            drv:next_screen = drv:screen_active - 1
        end if    
    end if

    //resetea flags de pestañas y botones proximo(send) - previo de la screen.
    call drv:reset_nav_funct
    
    if (testflag(admin:mode_flags,update) || testflag(admin:mode_flags,delete)) then
    	if (drv:screen_active == 1) then //Si quiere Actualizar o Grabar desde la 1ra pantalla.
    	    if (testflag(admin:mode_flags,update)) then
    	        //Valida campos de la 2da pantalla.
    	        call giros:MISCvalid_datgrales2
    	        if (retval < 0) then
                    MSGBOX "DEBE COMPLETAR LOS DATOS OBLIGATORIOS DE LA SIGUIENTE PANTALLA",0x00000000,"ERROR"
                    resetflag admin:mode_flags,update
	                resetflag admin:mode_flags,delete
	                resetflag admin:mode_flags,imprimir
                    return -2
                end if
    	    end if	
    	end if
        call post:MISCAutorizoModif("DGralSuc")
        if (retval < 0) then //graba pendientes en Tabla_Temp;        	
        	call giros:MISCScreen1_SucGen  //Se posiciona en primera pantalla.
            //Una vez que actualizo, limpia campo indirecto que contenia OFB:giros:JNLAoM_SucGen.
            clearfield drv:INDjnl_rtn
            resetflag admin:mode_flags,update
	        resetflag admin:mode_flags,delete
	        resetflag admin:mode_flags,imprimir
        end if
    end if

	if (testflag(admin:mode_flags,update)) then
		call giros:CDSsave_modif_SucGen
		resetflag admin:mode_flags,update
        ;SMC journalizo Modificacion
        call post:MISCJournalTablas
        call giros:MISCScreen1_SucGen  //Se posiciona en primera pantalla.
        //Una vez que actualizo, limpia campo indirecto que contenia OFB:giros:JNLAoM_SucGen.
        clearfield drv:INDjnl_rtn
	else
		if (testflag(admin:mode_flags,delete)) then
			call giros:CDSborro_SucGen
			resetflag admin:mode_flags,delete
            ;SMC journalizo Modificacion
            call post:MISCJournalTablas
            call giros:MISCScreen1_SucGen  //Se posiciona en primera pantalla.
            //Una vez que actualizo, limpia campo indirecto que contenia OFB:giros:JNLAoM_SucGen.
            clearfield drv:INDjnl_rtn
		end if
	end if
end sub

/*******/
