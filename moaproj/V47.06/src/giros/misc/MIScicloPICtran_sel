/****o* Lista de Giros/OFB:giros:MIScicloPICtran_sel
* NOMBRE
*   OFB:giros:MIScicloPICtran_sel
* DESCRIPCION
*   Recorro el picklist PICtran_sel donde estan cargados los giros con sus cheques asociados
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   giros:PCOLanulacionLG
* DOCUMENTACION
*   http://jira.sibs.net/jira/browse/AUT-520
* SOURCE
*/
#include "girosmisc.h"

SUB giros:MIScicloPICtran_sel
    LOCAL idx LIKE siaf:idx
    LOCAL line_csr_chk LIKE common:csr_string
    LOCAL line_csr LIKE common:csr_string
  	LOCAL l_cheque LIKE common:csr_string
  	LOCAL l_sesion LIKE common:csr_string
  	LOCAL l_nro_trx LIKE common:csr_string
  	LOCAL l_anulado LIKE common:csr_string
  	LOCAL l_estado LIKE giros:lg_cheque
  	LOCAL l_indice LIKE siaf:i

	CLEARFIELD line_csr_chk 
	idx = PL_GETITEM 
	l_indice = 0
	
	CALL csr:PickOp 4  DSC:post:PICtran_sel "1,10"  
    CALL csr:myPickcontrol(dsc:post:PICtran_sel,line_csr_chk,idx)
    
    WHILE (!(RETVAL < 0))
        CLEARFIELD line_csr
        CLEARFIELD l_cheque
        CLEARFIELD l_sesion
        CLEARFIELD l_nro_trx
        CLEARFIELD l_anulado
        CLEARFIELD l_estado
        
        l_anulado = SUBSTR(line_csr_chk,87,7)
        CALL post:MISCFtrim(l_anulado)
        
        line_csr = SUBSTR(line_csr_chk,1,10)
        CALL post:MISCFtrim(line_csr)  
        
        l_sesion = SUBSTR(line_csr_chk,71,9)
        CALL post:MISCFtrim(l_sesion)
         
        l_nro_trx = SUBSTR(line_csr_chk,81,5)
        CALL post:MISCFtrim(l_nro_trx)
         
        l_cheque = line_csr
      
        IF (l_anulado == "ANULADO") THEN
            l_estado = "AN"
            
            //Si es un cheque anulado que no tiene asociado un giro
            IF (l_nro_trx.NUMCHARS == 0) THEN     
               
                //Grabo una tabla con los cheques anulados sin giros asociados
                CALL CDSsave_AnulChq(line_csr)
                
            END IF
        END IF
        
        CALL giros:CDSaceptChkSesionLG(l_sesion,l_nro_trx,l_cheque,l_estado)
        
        //Borro el registro en la tabla CaidasChq   
        CALL giros:CDSdeleteCaidasChq(l_sesion,l_cheque)
           
        CLEARFIELD line_csr_chk
        idx = idx + 1
        CALL csr:myPickcontrol(dsc:post:PICtran_sel,line_csr_chk,idx)
    END WHILE
     

END SUB

/*******/
