/****o* Lista de Giros/OFB:giros:MISCanul_chq_vivos
* NOMBRE
*   OFB:giros:MISCanul_chq_vivos
* DESCRIPCION
*   Esta rutina verifica en la sesion_lg si queda algún giro postal vivo para emitir luego de no haber podido
*	recuperar una caida en vuelo y genera registros con dichos giros, en la AnulChq para anularlos poteriormente.
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   OFB:giros:OFBproceso_lg
* DOCUMENTACION
*   http://jira.sibs.net/jira/browse/AUT-520
* SOURCE
*/
#include "girosmisc.h"

SUB giros:MISCanul_chq_vivos(tipo_giro,sesion_nro)

	local l_tipo_giro like giros:tipo_giro_fld
	local l_sesion_nro like giros:nro_sesion_lg
	local cheque_postal like adt_texto
	local varl_mensaje      like common:csr_string
	
	l_tipo_giro = tipo_giro
	l_sesion_nro = sesion_nro
	

	//recorro sesion_lg para ver si hay giros vivos a emitir que no sean remanentes
	CDS SELECT FROM DSC:TABsesion_lg BECOMES DSC:CONses_lg WHERE \
	giros:operator_lg == FLD:op:op_operator_id && \
	giros:nro_sesion_lg == l_sesion_nro && \
	giros:tipo_giro_fld == l_tipo_giro && \
	giros:estado_lg == "I" && \
	giros:a_emitir == "S" && \
	giros:NroGiro_fld != "" sort "primary"

	CDS FINDFIRST DSC:CONses_lg  LOCK
	lib:err_retval = CDSRETVAL
		
	WHILE (lib:err_retval == 0)
		
		CDS EXTRACT DSC:CONses_lg  ALL
	
		//anulao RecepChk
		CALL giros:CDSavanza_cheq("AN")
		
		//Armo el cmapo line_csr con el cheuqe para reutilizar la rutina de grabacion de la AnulChq		
        clearfield cheque_postal
        CALL post:MISCFtrim(giros:Serie_fld)	//longitud 1
        CALL post:MISCFtrim(giros:NroGiro_fld)	//longitud 6
        CALL post:MISCFtrim(giros:digito_fld)	//longitud 1
        MOVE giros:Serie_fld TO SUBSTR(cheque_postal,0,1)
        MOVE giros:NroGiro_fld TO SUBSTR(cheque_postal,2,6)
        MOVE giros:digito_fld TO SUBSTR(cheque_postal,9,1)
    	giros:codigo_procesamiento = 991101   
		clearfield giros:lg_status
		clearfield giros:hora_viaje
		clearfield giros:dia_viaje
		clearfield post:adt_codseq_nro
		clearfield giros:CodRespuesta_comm        
       
		//generao registro correspondiente en AnulChk
		CALL giros:CDSsave_AnulChq(cheque_postal)
		
		IF (lg_cheque != "AN")
			//si el cheque esta vivo (impreso correctamente) enviar mensaje de destruccion del cheque
	   		fprint varl_mensaje,"%s : %s-%s-%s %s","DESTRUYA MANUALMENTE EL CHEQUE",\
	   		giros:Serie_fld,giros:NroGiro_fld,giros:digito_fld, \
	   		"\nEL MISMO NO PODRA SER UTILIZADO YA QUE SE ANULARA AUTOMATICAMENTE."
			msgbox varl_mensaje,OFB_OK,"Error en envio de operaciones"
		END IF
		
		//limpiar sesion_lg para que el giro quede disponible
		CALL CDSupd_ChqAnul_ok	

		CDS FINDNEXT DSC:CONses_lg  LOCK
		lib:err_retval = CDSRETVAL

	END WHILE		

	CDS UNLOCK DSC:CONses_lg
	CDS ENDSELECT DSC:CONses_lg
		
	
END SUB

/*******/
