/****o* Giros Masivos/OFB:giros:MISCwri_teleg_env_gm
* NOMBRE
*   OFB:giros:MISCwri_teleg_env_gm
* DESCRIPCION
*   Recorre sesion_gm para imprimir reporte de Giros Telegraficos enviados de la sesion.
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   RETURN (-2) Problemas en la impresion del listado.
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   giros:MISClst_teleg_env_gm
* DOCUMENTACION
*   Documentación relevante que deberá ser actualizada por la modificación de la rutina
* SOURCE
*/
#include "girosmisc.h"

SUB giros:MISCwri_teleg_env_gm
    LOCAL l_importe1         LIKE siaf:importe
    LOCAL l_importe2         LIKE siaf:importe
    LOCAL l_importe3         LIKE siaf:importe
    LOCAL l_importe4         LIKE siaf:importe
    LOCAL l_importe5         LIKE siaf:importe
    LOCAL l_importe6         LIKE siaf:importe
    LOCAL l_importe7         LIKE siaf:importe

    LOCAL l_NombreBenef      LIKE giros:NombreBenef_fld
    LOCAL l_ApellidoBenef    LIKE giros:ApellidoBenef_fld
    LOCAL l_Serie            LIKE giros:Serie_fld
    LOCAL l_NroGiro          LIKE giros:NroGiro_fld
    LOCAL l_digito           LIKE giros:digito_fld
    LOCAL l_ImporteNeto      LIKE giros:ImporteNeto
    LOCAL l_Comision         LIKE giros:Comision_fld
    LOCAL l_ImporteSelloEmis LIKE giros:ImporteSelloEmis
    LOCAL l_ImporteSelloPag  LIKE giros:ImporteSelloPag
    LOCAL l_TasaSinIva       LIKE giros:TasaSinIva_fld
    LOCAL l_IvaTasa          LIKE giros:IvaTasa_fld
    LOCAL l_Tasas            LIKE giros:TasaSinIva_fld
    LOCAL l_costo_opc        LIKE post:costo_opc
    LOCAL l_ImporteTotal     LIKE giros:ImporteTotal_fld

    LOCAL t_ImporteNeto      LIKE giros:ImporteNeto
    LOCAL t_Comision         LIKE giros:Comision_fld
    LOCAL t_ImporteSelloEmis LIKE giros:ImporteSelloEmis
    LOCAL t_ImporteSelloPag  LIKE giros:ImporteSelloPag
    LOCAL t_Tasas            LIKE giros:TasaSinIva_fld
    LOCAL t_costo_opc        LIKE post:costo_opc
    LOCAL t_ImporteTotal     LIKE giros:ImporteTotal_fld

    //cgw: Detalle Reporte de giros telegraficos enviados.

    //inicializa totales.
    t_ImporteNeto = 0
    t_Comision = 0
    t_ImporteSelloEmis = 0
    t_ImporteSelloPag = 0
    t_Tasas = 0
    t_costo_opc = 0
    t_ImporteTotal = 0

    DO
       IF (lib:err_retval < 0) THEN
       	   BREAK
       END IF	
       CDS EXTRACT DSC:CTXSesion_GM \
           giros:NombreBenef_fld   INTO l_NombreBenef \
           giros:ApellidoBenef_fld INTO l_ApellidoBenef \
           giros:Serie_fld         INTO l_Serie \
           giros:NroGiro_fld       INTO l_NroGiro \
           giros:digito_fld        INTO l_digito \
           giros:ImporteNeto       INTO l_ImporteNeto \
           giros:Comision_fld      INTO l_Comision \
           giros:ImporteSelloEmis  INTO l_ImporteSelloEmis \
           giros:ImporteSelloPag   INTO l_ImporteSelloPag \
           giros:TasaSinIva_fld    INTO l_TasaSinIva \
           giros:IvaTasa_fld       INTO l_IvaTasa \
           post:costo_opc          INTO l_costo_opc \
           giros:ImporteTotal_fld  INTO l_ImporteTotal

       l_Tasas = l_TasaSinIva + l_IvaTasa //Tasas varias.

       l_importe1 = l_ImporteNeto 
       l_importe2 = l_Comision
       l_importe3 = l_ImporteSelloEmis
       l_importe4 = l_ImporteSelloPag
       l_importe5 = l_Tasas
       l_importe6 = l_costo_opc
       l_importe7 = l_ImporteTotal

       FPRINT common:csr_string "%-20.20s %-20.20s %1.1s-%6.6s-%1.1s" \
              l_ApellidoBenef, l_NombreBenef, l_Serie, l_NroGiro, l_digito
       FPRINT common:csr_string "%s %13.13e %13.13e %12.12e %12.12e" \
              common:csr_string, l_importe1, l_importe2, l_importe3, l_importe4
       FPRINT common:csr_string "%s %13.13e %13.13e %13.13e" \
              common:csr_string, l_importe5, l_importe6, l_importe7
       CALL post:MISCwrt_general2("H",0,"D")              
       IF (RETVAL < 0) THEN
    	   RETURN (-2)
       END IF

       //Acumula totales.
       t_ImporteNeto = t_ImporteNeto + l_ImporteNeto
       t_Comision = t_Comision + l_Comision
       t_ImporteSelloEmis = t_ImporteSelloEmis + l_ImporteSelloEmis
       t_ImporteSelloPag = t_ImporteSelloPag + l_ImporteSelloPag           
       t_ImporteTotal = t_ImporteTotal + l_ImporteTotal
       t_Tasas = t_Tasas + l_Tasas
       t_costo_opc = t_costo_opc + l_costo_opc

       CDS FINDNEXT DSC:CTXSesion_GM NOLOCK
       LET lib:err_retval = CDSRETVAL
    LOOP
    CDS ENDSELECT DSC:CTXSesion_GM

    IF (siaf:i != 46) THEN // No es el comienzo de luego de un encabezado.
        common:csr_string = "----------------------------------------------------------------------------------------------------------------------------------------------------"
        CALL post:MISCwrt_general2("H",0,"D")
        IF (RETVAL < 0) THEN
    	   RETURN (-2)
        END IF
    END IF

    l_importe1 = t_ImporteNeto
    l_importe2 = t_Comision
    l_importe3 = t_ImporteSelloEmis
    l_importe4 = t_ImporteSelloPag
    l_importe5 = t_Tasas
    l_importe6 = t_costo_opc
    l_importe7 = t_ImporteTotal

    common:csr_string = "                                                     "
    FPRINT common:csr_string "%s%13.13e %13.13e %12.12e %12.12e" \
           common:csr_string, l_importe1, l_importe2, l_importe3, l_importe4
    FPRINT common:csr_string "%s %13.13e %13.13e %13.13e" \
           common:csr_string, l_importe5, l_importe6, l_importe7
    CALL post:MISCwrt_general2("H",0,"D")
    IF (RETVAL < 0) THEN
    	RETURN (-2)
    END IF

END SUB

/*******/
