/****o* Lista de Giros/OFB:giros:MISCrecuperoCliMedLG
* NOMBRE
*   OFB:giros:MISCrecuperoCliMedLG
* DESCRIPCION
*   Recupera los datos del Cliente Medio
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   giros:PBcargaFileLG
* DOCUMENTACION
*   https://jirats.siemens.com.ar/jira/browse/AUT-1252
* SOURCE
*/
#include "girosmisc.h"

SUB giros:MISCrecuperoCliMedLG
    LOCAL l_err_cds LIKE lib:err_retval 

    IF (giros:NroCuitEmis_fld.NUMCHARS == 0) THEN
        
        CALL giros:CDSrecupEmiLG_0
        l_err_cds = RETVAL

        IF (l_err_cds == -2) THEN
            MSGBOX "No se pudo recuperar los datos del Emisor \nde la rutina giros:CDSrecupEmiLG_0",OFB_OK,"Error"
            RETURN l_err_cds
        END IF

        CALL giros:CDSrecupEmiLG_1
        l_err_cds = RETVAL
        
        IF (l_err_cds == -2) THEN
            MSGBOX "No se pudo recuperar los datos del Emisor \nde la rutina giros:CDSrecupEmiLG_1",OFB_OK,"Error"
            RETURN l_err_cds
        END IF
        
        CALL giros:CDSrecupEmiLG_2
        l_err_cds = RETVAL
        
        IF (l_err_cds == -2) THEN
            MSGBOX "No se pudo recuperar los datos del Emisor \nde la rutina giros:CDSrecupEmiLG_2",OFB_OK,"Error"
            RETURN l_err_cds
        END IF

        //limpio los campos que no utilizo
        CLEARFIELD giros:PisoEmis_fld           
        CLEARFIELD giros:DptoEmis_fld  

        IF (giros:TipoDocEmis_fld == "80" || giros:TipoDocEmis_fld == "99" || \
            giros:TipoDocEmis_fld == "86" || giros:TipoDocEmis_fld == "87") THEN 
            //80(CUIT),86 (CUIL),87(CDI),99(SIN IDENTIFICAT/VENTA GLOB.DIA) 
            giros:NroDocEmis_fld = SUBSTR(giros:NroCuitEmis_fld,2,8)
            
        ELSE
            //Tipo de documento sin formato de CUIT
            giros:NroDocEmis_fld = giros:NroDocEmis_fld * 1
            CLEARFIELD giros:NroCuitEmis_fld
        END IF
    
        IF (post:es_juridica.NUMCHARS == 0) THEN
            post:es_juridica = "S" // POR DEFAULT
            SETFLAG giros:pers_jur_flag,persona_juridica
        ELSE
            IF (post:es_juridica  == "S") THEN
                SETFLAG giros:pers_jur_flag,persona_juridica
            ELSE
                RESETFLAG giros:pers_jur_flag,persona_juridica
            END IF
        END IF

        //cgw: AUT-1194 - Asigna Codigo de Pais AR (Argentina).
        //Se mostrara como codigo "AR" extrayendola de la tabla Paises.
    	CALL giros:CDSbusca_primer_pais(CodPaisResidEmis,PaisResidEmis)
    	IF (giros:CodPaisResidEmis.numchars == 0) THEN
            //Si no existe en la parametria, sers fijado con codigo "AR".
            //La descripción se mostrara en blanco, como para que se note que no existe en la parametria
            //y pueda así ser cargado en la misma.
    		giros:CodPaisResidEmis = "AR"
    		CLEARFIELD giros:PaisResidEmis
    	END IF

        //Si el Emisor es persona juridica.
        IF (TESTFLAG(giros:pers_jur_flag,persona_juridica)) THEN
        	giros:NombreEmi_fld = "."
        	giros:ApellidoEmis_fld = giros:RazonSocEmis_fld
        ELSE
            // Al no tener en pantalla la opción d cargar nombre y apellido para persona física, instanciamos
            // estos campos con la Razon social, para que pueda tener valores que no traigan por las problemas
            // en los viajes al host.
            giros:NombreEmi_fld = giros:RazonSocEmis_fld
        	giros:ApellidoEmis_fld = giros:RazonSocEmis_fld
        END IF
    END IF
  
END SUB

/*******/
