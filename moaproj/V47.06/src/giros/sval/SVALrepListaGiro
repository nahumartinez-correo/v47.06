/****o* Lista de Giros/OFB:giros:SVALrepListaGiro
* NOMBRE
*   OFB:giros:SVALrepListaGiro
* DESCRIPCION
*   Rutina SVAL del Reporte de Lista de Giros
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   RETURN (-2): No Existen Operadores con Nivel Cajero
*   RETURN (-2): No hay filtros de seleccion para el reporte
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   giros:TRANRepListaGiro
* DOCUMENTACION
*   https://jirats.siemens.com.ar/jira/browse/AUT-1252
* SOURCE
*/
#include "girossval.h"


SUB giros:SVALrepListaGiro
	LOCAL l_oper_giros_lg      LIKE giros:oper_giros_lg
	LOCAL l_fec_d              LIKE common:csr_string
	LOCAL l_fec_a              LIKE common:csr_string
	LOCAL l_Lista_sel_desde_lg LIKE common:csr_string
	LOCAL l_Lista_sel_hasta_lg LIKE common:csr_string
	LOCAL l_DetalleContenidoLG LIKE common:csr_string
	LOCAL l_climed_numero_lg   LIKE common:csr_string
	LOCAL l_estado_giros_lg    LIKE giros:estado_giros_lg    
	LOCAL Lind LIKE drv:INDlast_menu
	
	LET l_oper_giros_lg = giros:oper_giros_lg
	LET l_fec_d = post:fec_d
	LET l_fec_a = post:fec_a
	LET l_Lista_sel_desde_lg = giros:Lista_sel_desde_lg
	LET l_Lista_sel_hasta_lg = giros:Lista_sel_hasta_lg
	LET l_DetalleContenidoLG = giros:DetalleContenidoLG    
	LET l_climed_numero_lg = giros:climed_numero_lg
	LET l_estado_giros_lg = giros:estado_giros_lg
    
  CALL post:MISCFtrim(l_oper_giros_lg)
  CALL post:MISCFtrim(l_fec_d) 
  CALL post:MISCFtrim(l_fec_a)
  CALL post:MISCFtrim(l_Lista_sel_desde_lg)
  CALL post:MISCFtrim(l_Lista_sel_hasta_lg)
  CALL post:MISCFtrim(l_DetalleContenidoLG)
  CALL post:MISCFtrim(l_climed_numero_lg)
  CALL post:MISCFtrim(l_estado_giros_lg)

	IF (l_fec_d.NUMCHARS == 0 && l_fec_a.NUMCHARS == 0 && l_Lista_sel_desde_lg.NUMCHARS == 0 && l_Lista_sel_hasta_lg == 0) THEN
		//gpb-AUT-1433
		MSGBOX "Debe ingresar por lo menos la lista o la fecha de emisión.",0x00000000,"Error"
		RETURN (-2)
	END IF

	//Tratamiento filtro de Operadores. 
	IF (l_oper_giros_lg == "TODOS") THEN
		CLEARFIELD giros:oper_id_lg
	ELSE
		//Obtiene el codigo id de operador para filtro del reporte.
		CDS SELECT FROM DSC:op:adt_op_table BECOMES DSC:op:adt_op_context \
			WHERE op:op_branch_id == fld:common:sucursal_activa && \
						op:op_login_id == l_oper_giros_lg
		CDS FINDFIRST DSC:op:adt_op_context LOCK
		IF (CDSRETVAL < 0) THEN
			CDS UNLOCK DSC:op:adt_op_context
			CDS ENDSELECT DSC:op:adt_op_context
			MSGBOX "No Existen Operadores con Nivel Cajero",0x00000000,"Error"
			RETURN (-2)
		END IF
		CDS EXTRACT DSC:op:adt_op_context op:op_operator_id INTO giros:oper_id_lg
		CDS UNLOCK DSC:op:adt_op_context
		CDS ENDSELECT DSC:op:adt_op_context
	END IF

	//Sort "RepLG" - giros:nro_sesion_lg, giros:NroCuitEmis_fld, giros:climed_numero_lg, giros:tipo_giro_fld, giros:estadoRep_lg, giros:fechaRep_lg
	giros:sort_giros_emi_lg = "RepLG"
	//giros:sort_giros_emi_lg = "RepCliLG"

	IF (giros:sort_giros_emi_lg == "RepLG") THEN
		//Ordenado por Lista de Giros.
		CALL giros:MISCcaseRepLG(oper_id_lg,l_fec_d,l_fec_a,l_Lista_sel_desde_lg,l_Lista_sel_hasta_lg,l_DetalleContenidoLG,l_estado_giros_lg)
	ELSE
		//giros:sort_giros_emi_lg == "RepCliLG"
		//Ordenado por Cliente, Lista de Giros. No se esta utilizando por el momento.
		CALL giros:MISCcaseRepCliLG(oper_id_lg,l_fec_d,l_fec_a,l_Lista_sel_desde_lg,l_Lista_sel_hasta_lg,l_DetalleContenidoLG,l_estado_giros_lg)
	END IF

	IF (RETVAL < 0) THEN
		//No hay filtros de seleccion para el reporte.
		RETURN (-2)
	END IF

	IF (siaf:pantalla_impreso == "A") THEN
		LET giros:accionFileLG = "S" // F = Find File  - S = Save File
		LET giros:extensionFileLG = ".txt"
		
		CALL giros:MISCnombreArchRep("LG")
		
		INIT 71 1 //Para que no se pueda clickear otro objeto
		CALL drv:MISCpopup(scr:giros:SCRNBuscador,ofb:giros:PDISBuscador,ofb:giros:PCOLBuscador,ofb:giros:SVALBuscador,LST:giros:BUTBuscador)
		IF (LASTKEY == RT_ESC)
			RETURN (-2)
		END IF
		INIT 71 0
	END IF
    
	//UNDISPLAY giros:SCRNrepListaGiro
	IF (LASTKEY == 0x0190) THEN
		RETURN (-2)
	END IF
	FYIMSG LST:common:fyimsg_list, "(L) PROCESANDO LISTADO..."
	common:csr_string_tmp = "REPORTE DE LISTA DE GIROS"

	//Reporte Lista de Giros
	CALL giros:MISCrepListaGiro
	
	CLEARFIELD giros:nombre_Arch_Repo
	
	//gpb:AUT-2413	
	Lind = LST:post:MENURepListaGiros //(Asignar menu que te interesa)
	IF (drv:INDlast_menu == Lind) THEN
		CALL giros:MISCjoinDesktopTool
	END IF	

END SUB

/*******/

